
entity MyRootEntity {
  toMany NameEntity mNames inverse toOne mRoot ;
}

#--------------------------------------------------------------*

entity NameEntity {
  toOne MyRootEntity mRoot inverse toMany mNames ;
  property String name default "Name" ;
  property Int aValue default 100 ;
}

#--------------------------------------------------------------*

document PMDocument root MyRootEntity {
  outlet PMTableView mNamesTableView $tableValue nameController ;
  arrayController nameController : root.mNames
     column "name" PMTextField name { sendContinously : no}
     column "int"  PMIntField aValue { sendContinously : no}
  ;

  outlet PMTableView mOtherTableView $tableValue otherController ;
  arrayController otherController : root.mNames filter aValue
     column "name" PMTextField name { sendContinously : no}
     column "int"  PMIntField aValue { sendContinously : no}
  ;

  outlet PMTableView mSelectionTableView $tableValue selController ;
  arrayController selController : nameController.selectedArray
     column "name" PMTextField name { sendContinously : no}
     column "int"  PMIntField aValue { sendContinously : no}
  ;

  transient String selectionCountString dependsFrom selController.sortedArray.count ;
  outlet PMTextFieldObserver mSelectionCountTextField $valueObserver self.selectionCountString ;

  transient String evenValueString dependsFrom otherController.sortedArray.count ;
  outlet PMTextFieldObserver evenValueTextField $valueObserver self.evenValueString ;

  transient String canRemoveString dependsFrom nameController.selectedArray.count ;
  outlet PMTextFieldObserver canRemoveTextField $valueObserver self.canRemoveString ;

  outlet PMButton addPathButton $run nameController.add ;

  outlet PMButton removePathButton $run nameController.remove $enabled nameController.selectedArray.count > 0 ;

  outlet PMIntFieldObserver countItemTextField $valueObserver root.mNames.count {autoFormatter:no} ;

  transient String countItemMessage dependsFrom root.mNames.count ;

  outlet PMTextFieldObserver countItemMessageTextField $valueObserver self.countItemMessage  ;

  transient Int total dependsFrom root.mNames.all.aValue ;
  outlet PMIntFieldObserver totalTextField $valueObserver self.total {autoFormatter:no} ;
  
  action increment ;
  outlet PMButton incrementButton $run self.increment $enabled root.mNames.count > 0 ;
  
  action decrement ;
  outlet PMButton decrementButton $run self.decrement $enabled root.mNames.count > 0 ;
  
  selectionController detailController : nameController.selectedArray ;
  outlet PMTextField nameDetailTextField $value detailController.name { sendContinously : yes} ;
  outlet PMIntField valueDetailTextField $value detailController.aValue { sendContinously : yes, autoFormatter:no} ;
}

#--------------------------------------------------------------*

