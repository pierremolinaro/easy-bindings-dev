semantics outletClassAnalysis :
  import "decoratedTypes.gSemantics" ;

#----------------------------------------------------------------------------*

routine analyzeOutletClasses
  ??@outletClassList inOutletClassList
  ??@protocolMap inProtocolMap
  !@outletClassMap outOutletClassMap
:
  outOutletClassMap := [@outletClassMap emptyMap] ;
  foreach inOutletClassList do
    @outletClassBindingMap allBindingMap ;
  #--- Has super class ?
    if mSuperClassName->string == "" then
      allBindingMap := [@outletClassBindingMap emptyMap] ;
    else
      [outOutletClassMap searchKey
        !mSuperClassName
        ?4*
        ?allBindingMap
        ?2*
      ] ;
    end if ;
  #--- Analyze options
    @protocolList adoptedProtocolList [emptyList] ;
    @bool implementsDeallocProtocol := false ;
    foreach mOptionList do
      if mValue->string != "PMWillDeallocProtocol" then
        error mValue: "only 'PMWillDeallocProtocol' is allowed here" ;
      elsif implementsDeallocProtocol then
        error mValue: "'PMWillDeallocProtocol' option already set" ;
      else
        implementsDeallocProtocol := true ;
        adoptedProtocolList += !false !mValue ;
      end if ;
    end foreach ;
  #---
    @outletClassBindingMap currentBindingMap [emptyMap] ;
    foreach mOutletClassBindingList do
      @outletClassBindingOptionMap outletClassBindingOptionMap [emptyMap] ;
      foreach mBindingOptionList do
        [!?outletClassBindingOptionMap insertKey !mBindingOptionName !mAcceptableTypeList] ;
      end foreach ;
      [!?allBindingMap insertKey !mBindingName !mAcceptableTypeFormatterList !outletClassBindingOptionMap] ;
      [!?currentBindingMap insertKey !mBindingName !mAcceptableTypeFormatterList !outletClassBindingOptionMap] ;
    end foreach ;
  #--- Analyze protocols
    foreach mAdoptedProtocolList do
      [inProtocolMap searchKey !mValue ?@bool isPredefined] ;
      adoptedProtocolList += !isPredefined !mValue ;
    end foreach ;
  #---
    [!?outOutletClassMap insertKey
      !mOutletClassName
      !mGeneration
      !mSuperClassName->string
      !adoptedProtocolList
      !implementsDeallocProtocol
      !allBindingMap
      !currentBindingMap
      !mHasTableValueBinding
    ] ;
  end foreach ;
end routine ;

#----------------------------------------------------------------------------*

end semantics ;
