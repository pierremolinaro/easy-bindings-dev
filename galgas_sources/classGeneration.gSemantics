semantics classGeneration :
  import "easyBindings_options.gOption" ;
  import "transientGeneration.gSemantics" ;

#----------------------------------------------------------------------------*

local filewrapper classGenerationTemplate in "../generation-templates/class-generation" {
}{
}{
  template classDeclarationPart1 "class-1.h.gTemplate"
    ?@string CLASS_NAME
    ?@string SUPER_CLASS_NAME
    ?@attributeListForImplementation PROPERTY_LIST_FOR_IMPLEMENTATION
    ?@transientListForImplementation TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@attributeListForImplementation ALL_PROPERTIES_FOR_INIT_METHOD_HEADER
    ?@stringset HEADER_INCLUSION_SET
  ;
  
  template classDeclarationPart2 "class-2.h.gTemplate"
    ?@string CLASS_NAME
    ?@string SUPER_CLASS_NAME
    ?@attributeListForImplementation PROPERTY_LIST_FOR_IMPLEMENTATION
    ?@transientListForImplementation TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@attributeListForImplementation ALL_PROPERTIES_FOR_INIT_METHOD_HEADER
  ;
  
  template classImplementation "class.m.gTemplate"
    ?@string CLASS_NAME
    ?@string SUPER_CLASS_NAME
    ?@attributeListForImplementation PROPERTY_LIST_FOR_IMPLEMENTATION
    ?@transientListForImplementation TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@attributeListForImplementation ALL_PROPERTIES_FOR_INIT_METHOD_HEADER
    ?@attributeListForImplementation INHERITED_PROPERTIES_FOR_SUPER_INIT_CALL
    ?@stringset IMPLEMENTATION_INCLUSION_SET
    ?@2stringlist TRANSIENT_DEPENDENCY_ARCS
  ;
  
  template classTransientComputationsHeaderFile "class-transient-computations.h.gTemplate"
    ?@string CLASS_NAME
    ?@transientListForImplementation TRANSIENT_LIST_FOR_IMPLEMENTATION
  ;
  
  template classTransientComputationImplementationFile "class-transient-computation-function.m.gTemplate"
    ?@string CLASS_NAME
    ?@easyBindingType RESULT_TYPE
    ?@string TRANSIENT_NAME
    ?@directDependancyList DIRECT_DEPENDANCY_LIST
  ;
}

#----------------------------------------------------------------------------*

routine generateClassCode
  ??@2stringlist inTransientDependencyArcList
  ??@classMap inClassMap
  ??@string inOutputDirectory
:
  if [@uint errorCount] == 0 then
    foreach inClassMap do
    #--- Header
      @string s := [filewrapper classGenerationTemplate.classDeclarationPart1
        ![lkey string]
        !mSuperClassName
        !mAttributeListForImplementation
        !mTransientListForImplementation
        !mAttributeListForInitMethodHeader
        !mHeaderFileInclusionSet
      ] ;
      @string s2 := [filewrapper classGenerationTemplate.classDeclarationPart2
        ![lkey string]
        !mSuperClassName
        !mAttributeListForImplementation
        !mTransientListForImplementation
        !mAttributeListForInitMethodHeader
      ] ;
      [@string generateFileWithPattern
        !inOutputDirectory
        ![lkey string] . ".h"
        !"//"
        !"\n\n" # Defaut user zone1
        !s
        !"\n\n}\n\n" # Defaut user zone2
        !s2
      ] ;
    #--- Implementation
      s := [filewrapper classGenerationTemplate.classImplementation
        ![lkey string]
        !mSuperClassName
        !mAttributeListForImplementation
        !mTransientListForImplementation
        !mAttributeListForInitMethodHeader
        !mInheritedAttributeListForImplementation
        !mImplementationFileInclusionSet
        !inTransientDependencyArcList
      ] ;
      [@string generateFileWithPattern
        !inOutputDirectory
        ![lkey string] . ".m"
        !"//"
        !"\n\n" # Defaut user zone1
        !s
        !"\n\n" # Defaut user zone2
        !"@end\n"
      ] ;
    #--- Transient computation function header file
      if [mTransientListForImplementation length] > 0 then
        s := [filewrapper classGenerationTemplate.classTransientComputationsHeaderFile
          ![lkey string]
          !mTransientListForImplementation
        ] ;
        [@string generateFileWithPattern
          !inOutputDirectory
          ![lkey string] . "-transient-computations.h"
          !"//"
          !"\n\n" # Defaut user zone1
          !s
          !"\n\n" # Defaut user zone2
          !"\n"
        ] ;
      end if ;
    #--- Transient computation function implementation files
      foreach mTransientListForImplementation do
          s := [filewrapper classGenerationTemplate.classTransientComputationImplementationFile
            ![lkey string]
            !mTransientType
            !mTransientName
            !mDependencyList
          ] ;
          [@string generateFileWithPattern
            !inOutputDirectory
            !"compute_" . [lkey string] . "_" . mTransientName . ".m"
            !"//"
            !"\n\n" # Defaut user zone1
            !s
            !"  return what ? ; // ADD YOUR CODE HERE\n" # Defaut user zone2
            !"}\n\n" . "//----------------------------------------------------------------------------*\n"
          ] ;
      end foreach ;
    end foreach ;
  end if ;
end routine ;

#----------------------------------------------------------------------------*


end semantics ;
