semantics attributeDeclarationAnalysis :
  import "transientAnalysis.gSemantics" ;
  import "bindingSemanticAnalysis.gSemantics" ;
  import "analyzeASTtype.gSemantics" ;

#----------------------------------------------------------------------------*

routine analyzeAttributeDeclaration
  ??@attributeList inAttributeList
  ??@contextAnalysisStruct inContextAnalysisStruct
  ??@string inObjectName # "document." . documentName
  ?!@transientDependencyGraph ioTransientDependencyGraph
  ?!@fileInclusionSet ioFileInclusionSet
  ?!@observablePropertyMap ioSelfObservableAttributeMap
  !@attributeListForImplementation outAttributeListForImplementation
  ?!@attributeListForImplementation ioAttributeListForInitHeader
:
  outAttributeListForImplementation := [@attributeListForImplementation emptyList] ;
  foreach inAttributeList do
    @easyBindingType type := [mType analyze !inContextAnalysisStruct] ;
    [mDefaultValue analyzeDefaultValueType
      ![@easyBindingTypeList listWithValue !type]
      !inContextAnalysisStruct
      ?@string directDefaultValueAsString
      ?@string objectiveCDefaultValueAsString
      !?ioFileInclusionSet
    ] ;
    outAttributeListForImplementation +=
      !type
      ![mAttributeName string]
      !objectiveCDefaultValueAsString
      !directDefaultValueAsString
    ;
    ioAttributeListForInitHeader +=
       !type
       ![mAttributeName string]
       !objectiveCDefaultValueAsString
       !directDefaultValueAsString
    ;
    const @lstring observationDependencyKey [new
      !inObjectName . "." . mAttributeName
      !mAttributeName->location
    ] ;
    [!?ioSelfObservableAttributeMap insertKey
      !mAttributeName
      !type
      !false # is not transient
      !observationDependencyKey->string
    ] ;
    [!?ioTransientDependencyGraph addNode
      !observationDependencyKey
      !observationDependencyKey->string
    ] ;
  end foreach ;
end routine ;

#----------------------------------------------------------------------------*

end semantics ;
