program easyBindings_program :
import "abstractSyntaxTree.gSemantics" ;
import "semanticAnalysis.gSemantics" ;
import "outletClassGeneration.gSemantics" ;
import "classGeneration.gSemantics" ;
import "entityGeneration.gSemantics" ;
import "preferencesGeneration.gSemantics" ;
import "documentGeneration.gSemantics" ;
import "easyBindings_grammar.gGrammar" ;

#----------------------------------------------------------------------------*

local filewrapper predefinedOutletClasses in "../generation-templates/predefined-outlet-classes" {
}{
}{
  template sourceFile "predefinedOutletClasses.easyBindings" ;
}

#----------------------------------------------------------------------------*

local filewrapper objective_c_sources in "../generation-templates/objective-c-sources" {
  "m", "h", "xib"
}{
}{
}

#----------------------------------------------------------------------------*

when . "easyBindings"
message "a source text file with the .easyBindings extension"
??@lstring inSourceFile {
#--- Parse predefined outlets
  @classList classList [emptyList] ;
  @entityList entityList [emptyList] ;
  @preferencesDeclarationList preferencesList [emptyList] ;
  @outletClassList outletClassList [emptyList] ;
  @documentDeclarationList documentDeclarationList [emptyList] ;
  @controllerClassList controllerClassList [emptyList] ;
  @protocolList protocolList [emptyList] ;
  grammar easyBindings_grammar on [filewrapper predefinedOutletClasses.sourceFile]
    !?classList
    !?entityList
    !?preferencesList
    !?outletClassList
    !?documentDeclarationList
    !?controllerClassList
    !?protocolList
    ?*
  ;
#--- Parse source file
  grammar easyBindings_grammar in inSourceFile
    !?classList
    !?entityList
    !?preferencesList
    !?outletClassList
    !?documentDeclarationList
    !?controllerClassList
    !?protocolList
    ?@location endOfSourceFile
  ;
#--- Semantic Analysis
  @classMap classMap ;
  @entityMap entityMap ;
  @preferencesMap preferencesMap ;
  @outletClassMap outletClassMap ;
  @documentMap documentMap ;
  @controllerClassMap controllerClassMap ;
  @stringlist sortedTransientOrderedList ;
  @2stringlist transientDependencyArcList ;
  semanticAnalysis
    !classList
    !entityList
    !preferencesList
    !outletClassList
    !documentDeclarationList
    !controllerClassList
    !protocolList
    !endOfSourceFile
    !inSourceFile->string
    ?outletClassMap
    ?controllerClassMap
    ?classMap
    ?entityMap
    ?preferencesMap
    ?documentMap
    ?sortedTransientOrderedList
    ?transientDependencyArcList
  ;
#--- Code generation
  const @string sourceDirectoryPath := [inSourceFile stringByDeletingLastPathComponent] ;
  generateOutletClassCode !outletClassMap !sourceDirectoryPath ;
  generateClassCode !transientDependencyArcList !classMap !sourceDirectoryPath ;
  generateEntityCode !transientDependencyArcList !entityMap !sourceDirectoryPath ;
  generatePreferencesCode !transientDependencyArcList !preferencesMap !sourceDirectoryPath ;
  generateDocumentCode !transientDependencyArcList !documentMap !sourceDirectoryPath ;
#--- Output dependency order definition file
  generateTransientDependencyFiles
    !transientDependencyArcList
    !sourceDirectoryPath
    !sortedTransientOrderedList
  ;
#--- Add embedded sources
  @stringset sources [setWithString !"easy-bindings-utilities"] ;
  sources += !"PMAllocationDebug" ;
  @filewrapper fw := [filewrapper objective_c_sources] ;
  foreach [fw allTextFilePathes] do
    const @string sourceName := [mValue lastPathComponent] ;
    if [sources hasKey ![sourceName stringByDeletingPathExtension]] then
      [@string generateFile
        !sourceDirectoryPath . [mValue stringByDeletingLastPathComponent]
        !sourceName
        ![fw textFileContentsAtPath !mValue]
       ] ;
    end if ;
  end foreach ;
}

#----------------------------------------------------------------------------*

end program ;
