//----------------------------------------------------------------------------------------------------------------------
#   AST
//----------------------------------------------------------------------------------------------------------------------

list @autoLayoutViewClassBindingSpecificationList {
  public var @lstring mOutletClassName
  public var @lstring mBindingName
  public var @outletClassBindingSpecificationModelList mOutletClassBindingSpecificationModelList
  public var @controllerBindingOptionList mBindingOptionList
}

//----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
//----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <auto_layout_view_binding_specification> (let @lstring inClassName,
                                                ?!@autoLayoutViewClassBindingSpecificationList ioBindingList) {
    $bindingName$ (?let @lstring bindingName)
    var @outletClassBindingSpecificationModelList outletClassBindingSpecificationModelList = []
    repeat
      let @bool modelShouldBeWritableProperty
      select
        $property$
        modelShouldBeWritableProperty = true
      or
        $transient$
        modelShouldBeWritableProperty = false
      end
      let @lstring modelTypeName
      select
        $Identifier$ (?modelTypeName)
      or
        $enum$
        modelTypeName = .new ("enum", .here)
      end
      outletClassBindingSpecificationModelList += !modelTypeName !modelShouldBeWritableProperty
    while
      $,$
    end
    var @controllerBindingOptionList bindingOptionList = []
    select
    or
      $($
      repeat
        $identifier$ (?let @lstring optionName)
        $:$
        $Identifier$ (?let @lstring optionType)
        bindingOptionList += !optionName !optionType
      while
        $,$
      end
      $)$
    end
    ioBindingList +=
      !inClassName
      !bindingName
      !outletClassBindingSpecificationModelList
      !bindingOptionList
  }

  #·····················································································································

}

//----------------------------------------------------------------------------------------------------------------------
#  SEMANTICS
//----------------------------------------------------------------------------------------------------------------------

map @autoLayoutBindingSpecificationMap {
  public var @lstring mOutletSuperClassName
  public var @autoLayoutViewBindingSpecificationMap mBindingMap
  insert insertKey error message "the '%K' binding is already declared in %L"
  search searchKey error message "there is no '%K' binding"
  remove removeKey error message "there is no '%K' binding"
}

#·······················································································································

map @autoLayoutViewBindingSpecificationMap {
  public var @outletBindingSpecificationModelList mOutletBindingSpecificationModelList
  public var @controllerBindingOptionDecoratedList mControllerBindingOptionList
  insert insertKey error message "the '%K' binding is already declared in %L"
  search searchKey error message "there is no '%K' binding"
}

#·······················································································································

proc buildAutoLayoutBindingSpecificationMap(
  ?!@semanticContext ioSemanticContext,
  let @autoLayoutViewClassBindingSpecificationList inBindingSpecificationListMap)
{
  for (mOutletClassName mBindingName mOutletClassBindingSpecificationModelList mBindingOptionList) in inBindingSpecificationListMap do
    var @outletBindingSpecificationModelList outletBindingSpecificationModelList = []
    for (mModelTypeName mModelShouldBeWritableProperty) in mOutletClassBindingSpecificationModelList do
      if mModelTypeName.string == "enum" then
        outletBindingSpecificationModelList +=
          !.enumType (!enumTypeName: "", !constantMap: [], !funcMap: []) # Any enum type
          !mModelShouldBeWritableProperty
      else
        ioSemanticContext.mClassMap.searchKey (mModelTypeName, ?let @classKind classKind, ?3*)
        let @typeKind typeKind
        switch classKind
        case prefs :
          error mModelTypeName : "an atomic class is required here" spoil (typeKind)
        case atomic (@typeKind kind) :
          typeKind = kind
        case document (*) :
          error mModelTypeName : "an atomic class is required here" spoil (typeKind)
        case entity (4*) :
          error mModelTypeName : "an atomic class is required here" spoil (typeKind)
        end
        outletBindingSpecificationModelList += !typeKind !mModelShouldBeWritableProperty
      end
    end
    var @controllerBindingOptionDecoratedList controllerBindingOptionDecoratedList = []
    for (mOptionName mOptionTypeName) in mBindingOptionList do
      ioSemanticContext.mClassMap.searchKey (mOptionTypeName, ?let @classKind classKind, ?3*)
      let @typeKind typeKind
      switch classKind
      case prefs :
        error mOptionTypeName : "an atomic class is required here" spoil (typeKind)
      case atomic (@typeKind kind) :
        typeKind = kind
      case document (*) :
        error mOptionTypeName : "an atomic class is required here" spoil (typeKind)
      case entity (4*) :
        error mOptionTypeName : "an atomic class is required here" spoil (typeKind)
      end
      controllerBindingOptionDecoratedList += !typeKind !mOptionName
    end
  //-- Insert in binding map
    var @autoLayoutViewBindingSpecificationMap bindingMap
    let @lstring outletSuperClassName
    if ioSemanticContext.mAutoLayoutBindingSpecificationMap.hasKey (mOutletClassName.string) then
      !?ioSemanticContext.mAutoLayoutBindingSpecificationMap.removeKey (mOutletClassName, ?outletSuperClassName, ?bindingMap)
    else
      bindingMap = []
      ioSemanticContext.mAutolayoutViewClassMap.searchKey (mOutletClassName, ?outletSuperClassName, ?8*)
    end
    !?bindingMap.insertKey (
      mBindingName,
      outletBindingSpecificationModelList,
      controllerBindingOptionDecoratedList
    )
    !?ioSemanticContext.mAutoLayoutBindingSpecificationMap.insertKey (mOutletClassName, outletSuperClassName, bindingMap)
  end
}

//----------------------------------------------------------------------------------------------------------------------
