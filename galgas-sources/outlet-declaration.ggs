#-----------------------------------------------------------------------------------------------------------------------
#  AST    
#-----------------------------------------------------------------------------------------------------------------------

list @bindingOptionList {
  public var @lstring mOptionName
  public var @abstractDefaultValue mOptionValue
}

#·······················································································································

enum @tableValueBinding {
  case noTableValueBinding
  case tableValueBinding (@lstring controllerName)
}

#·······················································································································

enum @graphicController {
  case none
  case defined (@lstring controller @lstring propertyName)
}

#·······················································································································

list @outletDeclarationList {
  public var @lstring mOutletTypeName
  public var @lstring mOutletName
  public var @tableValueBinding mTableValueBindingDescriptor
  public var @runActionDescriptor mRunDescriptor
  public var @multipleBindingDescriptor mEnabledBindingDescriptor
  public var @multipleBindingDescriptor mHiddenBindingDescriptor
  public var @regularBindingList mRegularBindingList
  public var @graphicController mGraphicController
}

#-----------------------------------------------------------------------------------------------------------------------
#  SYNTAX 
#-----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

#·······················································································································

  rule <outlet_declaration> (?!@outletDeclarationList ioOutletDeclarationList) {
    $outlet$
    $Identifier$ (?let outletTypeName)
    $identifier$ (?let outletName)
    <outlet_binding> (?let @tableValueBinding tableValueBinding
                     ?let @runActionDescriptor runActionDescriptor
                     ?let @multipleBindingDescriptor enabledBindingDescriptor
                     ?let @multipleBindingDescriptor hiddenBindingDescriptor
                     ?let @graphicController graphicController
                     ?let @regularBindingList regularBindingList)
    $;$
    ioOutletDeclarationList +=
      !outletTypeName
      !outletName
      !tableValueBinding
      !runActionDescriptor
      !enabledBindingDescriptor
      !hiddenBindingDescriptor
      !regularBindingList
      !graphicController
  }
 
 #·······················································································································

  rule <outlet_binding> (!@tableValueBinding tableValueBinding
                        !@runActionDescriptor runActionDescriptor
                        !@multipleBindingDescriptor enabledBindingDescriptor
                        !@multipleBindingDescriptor hiddenBindingDescriptor
                        !@graphicController graphicController
                        !@regularBindingList regularBindingList)
{
    tableValueBinding = @tableValueBinding.noTableValueBinding 
    runActionDescriptor = @runActionDescriptor.noAction 
    enabledBindingDescriptor = @multipleBindingDescriptor.noBinding
    hiddenBindingDescriptor = @multipleBindingDescriptor.noBinding
    graphicController = @graphicController.none
    regularBindingList = {}
    repeat
    while
      $tableValue$
      if tableValueBinding != .noTableValueBinding then
        error .here : "duplicated $tableValue binding"
      end
      $controllerName$ (?let @lstring controllerName)
      tableValueBinding = .tableValueBinding (!controllerName: controllerName)
    while
      $run$
      if runActionDescriptor != .noAction then
        error .here : "duplicated $run binding"
      end
      let @lstring runTargetName
      select
        runTargetName = @lstring (!"self" !.here)
      or
        $controllerName$ (?runTargetName)
        $.$
      end
      $identifier$ (?let @lstring runActionName)
      runActionDescriptor = .action (!target:runTargetName !action:runActionName)
  #--- Enabled
    while
      $enabled$
      if enabledBindingDescriptor != .noBinding then
        error .here : "duplicated $enabled binding"
      end
      <booleanMultipleBindingExpression> (?let @abstractBooleanMultipleBindingExpressionAST bindingExpression)
      enabledBindingDescriptor = .binding (!expression:bindingExpression)
  #--- Hidden
    while
      $hidden$
      if hiddenBindingDescriptor != .noBinding then
        error .here : "duplicated $hidden binding"
      end
      <booleanMultipleBindingExpression> (?let @abstractBooleanMultipleBindingExpressionAST bindingExpression)
      hiddenBindingDescriptor = .binding (!expression:bindingExpression)
  #--- Regular binding
    while
      $bindingName$ (?let @lstring bindingName)
      var @observablePropertyList observablePropertyList = {}
      repeat
        <observable_property> (?let @observablePropertyAST observableProperty)
        observablePropertyList += !observableProperty
      while
        $,$
      end
      <binding_option_list> (?let bindingOptions)
      regularBindingList +=
        !bindingName
        !observablePropertyList
        !bindingOptions
  #--- Graphic controller binding
    while
      $graphicController$
      $controllerName$ (?let @lstring graphicControllerName)
      let @lstring propertyName
      select
        propertyName = ["" here]
      or
        $.$
        $identifier$ (?propertyName)
      end
      if [graphicController isNone] then
        graphicController = .defined (!controller:graphicControllerName !propertyName: propertyName)
      else
        error graphicControllerName : "binding already defined" ;
      end   
    end
  }
  
  #·······················································································································

}

#-----------------------------------------------------------------------------------------------------------------------
#  SEMANTICS                              
#-----------------------------------------------------------------------------------------------------------------------

list @outletBindingModelList {
  public var @propertyKind mKind
  public var @location mErrorLocation
}

#·······················································································································

list @tableViewBindingGenerationList {
  public var @string mTableValueBindingOutletName
  public var @string mTableValueBindingControllerName
}

#·······················································································································

list @ebViewGraphicControllerBindingGenerationList {
  public var @string mEBViewOutletName
  public var @string mArrayControllerControllerName
}

#·······················································································································

proc analyzeOutlets(
  ?let @bool inPreferences
  ?let @propertyMap inRootObservablePropertyMap
  ?let @propertyMap inPreferencesPropertyMap
  ?let @string unused inRootEntityName
  ?let @semanticContext inSemanticContext
  ?let @outletDeclarationList inOutletDeclarationList
  ?let @propertyMap inObservablePropertyMap
  ?let @actionMap inActionMap
  ?let @string inReceiverSwiftTypeName
  ?!@generationStruct ioGeneration
  !@regularBindingsGenerationList outRegularBindingsGenerationList
  !@multipleBindingGenerationList outMultipleBindingGenerationList
  !@actionBindingListForGeneration outTargetActionList
  !@decoratedOutletMap outletMap
  !@tableViewBindingGenerationList outTableViewBindingGenerationList
  !@ebViewGraphicControllerBindingGenerationList outEBViewGraphicControllerBindingGenerationList)
{
  outRegularBindingsGenerationList = {}
  outTargetActionList = {}
  outMultipleBindingGenerationList = {}
  outletMap = {}
  outTableViewBindingGenerationList = {}
  outEBViewGraphicControllerBindingGenerationList = {}
  for (outlet_mOutletTypeName outlet_mOutletName outlet_mTableValueBindingDescriptor outlet_mRunDescriptor outlet_mEnabledBindingDescriptor outlet_mHiddenBindingDescriptor outlet_mRegularBindingList outlet_mGraphicController) in inOutletDeclarationList do
    [!?outletMap insertKey !outlet_mOutletName !outlet_mOutletTypeName.string]
    [inSemanticContext.mOutletClassMap searchKey
      !outlet_mOutletTypeName
      ?*
      ?let @bool handlesRunAction
      ?let @bool handlesTableViewBinding
      ?let @bool handlesEnabledBinding
      ?let @bool handlesHiddenBinding
      ?let @bool handleGraphicControllerBinding
      ?let @bool outletClassIsUserDefined
    ]
    if not outletClassIsUserDefined then
      ioGeneration.mNeededOutletClasses += !outlet_mOutletTypeName.string
    end
  #--- $graphicController
    switch outlet_mGraphicController
    case none :
    case defined (@lstring controllerName @lstring propertyName) :
      if not handleGraphicControllerBinding then
        error outlet_mOutletTypeName : "the '" + outlet_mOutletTypeName + "' type does not handle the $graphicController binding"
      else
        [inObservablePropertyMap searchKey
          !controllerName
          ?let @propertyKind kind
          ?2*
        ]
        switch kind 
        case property (2*) :
          error controllerName : "the bound model should be an array controller"
        case toMany (4*) :
          error controllerName : "the bound model should be an array controller"
        case toOne (5*) :
          error controllerName : "the bound model should be an array controller"
        case arrayController (entityName graphic) :
          if propertyName.string == "" then
            if not graphic then
              error controllerName : "the controlled entity (" + entityName + ") should be a graphic entity"
            end
          else
            [inSemanticContext.mClassMap searchKey
              !entityName
              ?*
              ?let observablePropertyMap
              ?2*
            ]
            [observablePropertyMap searchKey !propertyName ?let propertyKind ?2*]
            switch propertyKind 
            case property (2*) :
              error controllerName : "the bound model should be a toMany property"
            case toMany (kEntityName * isGraphic *) :
              if not isGraphic then
                error controllerName : "the controlled entity (" + kEntityName + ") should be a graphic entity"
              end
            case toOne (5*) :
              error controllerName : "the bound model should be a toMany property"
            case arrayController (2*) :
              error controllerName : "the bound model should be a toMany property"
            case selectionController (*) :
              error controllerName : "the bound model should be a toMany property"
            end
         end
        case selectionController (*) :
          error controllerName : "the bound model should be an array controller"
        end
        outEBViewGraphicControllerBindingGenerationList += !outlet_mOutletName.string !controllerName.string
      end
    end
  #--- $tableView
    switch outlet_mTableValueBindingDescriptor
    case noTableValueBinding :
    case tableValueBinding (@lstring controllerName) :
      if not handlesTableViewBinding then
        error outlet_mOutletTypeName : "the '" + outlet_mOutletTypeName + "' type does not handle the $tableView binding"
      else
        [inObservablePropertyMap searchKey
          !controllerName
          ?let @propertyKind unused kind
          ?2*
        ]
        if not [kind isArrayController] then
          error controllerName : "the bound model should be an array controller"
        end
        outTableViewBindingGenerationList += !outlet_mOutletName.string !controllerName.string
      end
    end
  #--- $run
    switch outlet_mRunDescriptor
    case noAction :
    case action (@lstring target @lstring action) :
      if not handlesRunAction then
        error outlet_mOutletTypeName : "the '" + outlet_mOutletTypeName + "' type does not handle run action"
      elsif target.string == "self" then
        [inActionMap searchKey !action]
        outTargetActionList +=
          !outlet_mOutletName.string
          !"self"
          !action.string
          !inReceiverSwiftTypeName
      else # It is a controller
        [inObservablePropertyMap searchKey
          !target
          ?*
          ?let @actionMap controllerActionMap
          ?*
        ]
        [controllerActionMap searchKey !action]
        outTargetActionList +=
          !outlet_mOutletName.string
          !if inPreferences then "preferences_" else "" end + target.string
          !action.string
          !"Controller_" + inReceiverSwiftTypeName + "_" + target
      end
    end
  #--- Enabled
    switch outlet_mEnabledBindingDescriptor
    case noBinding :
    case binding (@abstractBooleanMultipleBindingExpressionAST expression) :
      [expression analyzeExpressionForMultipleBinding
        !prefs: inPreferences
        !inRootObservablePropertyMap
        !inSemanticContext
        !inObservablePropertyMap
        !inPreferencesPropertyMap
        ?let @abstractBooleanMultipleBindingExpressionForGeneration enableExpression
        ?let @typeKind type
        ?let @location errorLocation
      ]
      if not handlesEnabledBinding then
        error errorLocation : "cet outlet n'accepte pas le binding $enabled"
      end
      if not [type isBoolType] then
        error errorLocation : "expression is not boolean"
      end
      outMultipleBindingGenerationList +=
        !outlet_mOutletName.string
        !"enabled"
        !enableExpression
    end
  #--- Hidden
    switch outlet_mHiddenBindingDescriptor
    case noBinding :
    case binding (@abstractBooleanMultipleBindingExpressionAST expression) :
      [expression analyzeExpressionForMultipleBinding
        !prefs: inPreferences
        !inRootObservablePropertyMap
        !inSemanticContext
        !inObservablePropertyMap
        !inPreferencesPropertyMap
        ?let @abstractBooleanMultipleBindingExpressionForGeneration hiddenExpression
        ?let @typeKind type
        ?let @location errorLocation
      ]
      if not [type isBoolType] then
        error errorLocation : "expression is not boolean"
      end
      if not handlesHiddenBinding then
        error errorLocation : "cet outlet n'accepte pas le binding $hidden"
      end
      outMultipleBindingGenerationList +=
        !outlet_mOutletName.string
        !"hidden"
        !hiddenExpression
    end
  #---------------------------------- Regular bindings
    analyzeRegularBinding (
      !inRootObservablePropertyMap
      !inPreferencesPropertyMap
      !inSemanticContext
      !inObservablePropertyMap
      !outlet_mOutletTypeName
      !outlet_mOutletName.string
      !outlet_mRegularBindingList
      !if inPreferences then "preferences_" else "self." end
      !?outRegularBindingsGenerationList
    )
  end
}

#·······················································································································

proc analyzeRegularBinding(
  ?let @propertyMap inRootPropertyMap
  ?let @propertyMap inPreferencesPropertyMap
  ?let @semanticContext inSemanticContext
  ?let @propertyMap inBoundModelPropertyMap
  ?let @lstring inOutletTypeName
  ?let @string inOutletName
  ?let @regularBindingList inRegularBindingList
  ?let @string inSelfSwiftName
  ?!@regularBindingsGenerationList ioRegularBindingsGenerationList)
{
  for (mBindingName mObservablePropertyList mBindingOptionList) in inRegularBindingList do
  #--- Analyze model
    var @outletBindingModelList boundModelTypeList = {}
    var @boundObjectList boundModelListForGeneration = {}
    for (mObservableProperty) in mObservablePropertyList do
      [mObservableProperty analyzeObservableProperty
        !inRootPropertyMap
        !inSemanticContext
        !inBoundModelPropertyMap
        ?let @propertyKind kind
        ?let @string unused swiftTypeStringForTransientFunctionArgument
        ?let @string unused defaultValueAsString
      ]
      switch kind
      case property (2*) : # Ok
      case toMany (4*) :
        error [mObservableProperty location]:"a toMany property cannot be bound"
      case toOne (5*) :
        error [mObservableProperty location]:"a toOne property cannot be bound"
      case arrayController (2*) :
        error [mObservableProperty location]:"an array controller cannot be bound"
      case selectionController (*) :
        error [mObservableProperty location]:"a selection controller cannot be bound"
      end
      boundModelListForGeneration += ![mObservableProperty modelStringForSelf !inSelfSwiftName] !kind
      boundModelTypeList += !kind ![mObservableProperty location]
    end
  #--- Analyze outlet binding
    var @outletBindingSpecificationModelList outletBindingSpecificationModelList = {}
    var @controllerBindingOptionDecoratedList controllerBindingOptionDecoratedList = {}
    var outletTypeName = inOutletTypeName
    var continues = true
    loop ([inSemanticContext.mBindingSpecificationMap count] + 1) while (outletTypeName.string != "") & continues do
      continues = false
      [inSemanticContext.mBindingSpecificationMap searchKey
        !outletTypeName
        ?let superOutletClassName
        ?let @outletBindingSpecificationMap bindingMap
      ]
      if [bindingMap hasKey !mBindingName.string] then
        [bindingMap searchKey
          !mBindingName
          ?outletBindingSpecificationModelList
          ?controllerBindingOptionDecoratedList
        ]
      else
        continues = true
        outletTypeName = superOutletClassName
      end
    end
    if continues then
      error mBindingName : "this binding is not defined"
    elsif [boundModelTypeList length] != [outletBindingSpecificationModelList length] then
      error mBindingName
      : [[outletBindingSpecificationModelList length] string]
        + " model(s) required for this binding (found: "
        + [boundModelTypeList length] + ")"
    else
      for (required_mModelType required_mModelShouldBeWritableProperty) in outletBindingSpecificationModelList, (found_mKind found_mErrorLocation) in boundModelTypeList do
        if required_mModelShouldBeWritableProperty && [found_mKind isTransient] then
          error found_mErrorLocation : "the model is transient and the binding requires an writable model"
        end
        if [required_mModelType isEnumType] && [found_mKind isEnumType] then # Ok
        elsif [required_mModelType swiftTypeName] != [found_mKind swiftTypeName] then
          error found_mErrorLocation : "the model type should be " + [required_mModelType swiftTypeName]
        end
      end
    end
  #--- Check options
    var bindingOptionString = ""
    if [controllerBindingOptionDecoratedList length] != [mBindingOptionList length] then
      var @string s
      if [controllerBindingOptionDecoratedList length] == 0 then
        s = "this binding has no option"
      else 
        s = "this binding requires the following options:"
        for (mOptionType mOptionName) in controllerBindingOptionDecoratedList do
          s += "\n  - " + mOptionName + " : " + [mOptionType swiftTypeName]
        end
      end
      error mBindingName : s
    else
      for (controller_mOptionType controller_mOptionName) in controllerBindingOptionDecoratedList,
          (binding_mOptionName binding_mOptionValue) in mBindingOptionList do
        if controller_mOptionName.string != binding_mOptionName.string then
          error binding_mOptionName : "the option name should be '" + controller_mOptionName + "'"
        end
        [binding_mOptionValue analyzeDefaultValueType 
          !{!controller_mOptionType}
          !inPreferencesPropertyMap
          ?let @string optionValueAsString
        ]
        bindingOptionString += ", " + controller_mOptionName.string + ":" + optionValueAsString
      end
    end
  #--- Generate binding
    ioRegularBindingsGenerationList +=
      !inOutletName
      !mBindingName.string
      !boundModelListForGeneration
      !bindingOptionString
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#   GENERATION TYPES                      
#-----------------------------------------------------------------------------------------------------------------------

list @boundObjectList {
  public var @string mBoundObjectString
  public var @propertyKind mKind
}

#·······················································································································

list @regularBindingsGenerationList {
  public var @string mOutletName
  public var @string mBindingName
  public var @boundObjectList mBoundObjectList
  public var @string mBindingOptionsString
}

#·······················································································································

list @actionBindingListForGeneration {
  public var @string mOutletName
  public var @string mTargetName
  public var @string mActionName
  public var @string mTargetTypeName
}

#·······················································································································

map @decoratedOutletMap {
  public var @string mOutletTypeName
  insert insertKey error message "the '%K' outlet is already declared"
  search searchKey error message "the '%K' outlet is not declared"
}

#-----------------------------------------------------------------------------------------------------------------------
