#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST   
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @entityDeclaration : @astDeclaration {
  @lstring mEntityName
  @lstring mSuperEntityName
  @simpleStoredPropertyList mSimpleStoredAttributeList
  @toOneRelationshipList mToOneRelationshipList
  @toManyRelationshipList mToManyRelationshipList
  @secondaryPropertyList mSecondaryPropertyList
  @stringset mSignatureList
  @lstringlist mActionDeclarationList
  @lstringlist mObsoleteEntityNames
  @bool mIsGraphicEntity
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <entity_declaration> ?let @bool inIsUserDefined ?!@astDeclarationStruct ioDeclarationAST {
    @bool graphicEntity
    select
      graphicEntity = false
    or
      $graphic$
      graphicEntity = true
    end
    $entity$
    @lstringlist obsoleteEntityNames = {}
    select
    or
      $($
      repeat
        $Identifier$ ?let @lstring obsoleteName
        obsoleteEntityNames += !obsoleteName
      while
        $,$
      end
      $)$
    end
    $Identifier$ ?let @lstring entityName
    @lstring superEntityName
    select
      superEntityName = ["" nowhere]
    or
      $:$
      $Identifier$ ? superEntityName
    end
    ${$
    @secondaryPropertyList secondaryPropertyList = {}
    @simpleStoredPropertyList simpleStoredAttributeList = {}
    @toOneRelationshipList toOneRelationshipList = {}
    @toManyRelationshipList toManyRelationshipList = {}
    @stringset signatureList = {}
    @lstringlist actionDeclarationList = {}
    repeat
    while
      <simple_stored_declaration> !?simpleStoredAttributeList !?signatureList
    while
      <transient_declaration> !?secondaryPropertyList
    while
      <toMany_relationship> !?toManyRelationshipList !?signatureList
    while
      <toOne_relationship> !?toOneRelationshipList
    while
      <action_declaration> !?actionDeclarationList
    while
      <controller_declaration> !?secondaryPropertyList
    end
    $}$
    ioDeclarationAST.mDeclarationList += !@entityDeclaration.new {
      !inIsUserDefined
      !entityName
      !superEntityName
      !simpleStoredAttributeList
      !toOneRelationshipList
      !toManyRelationshipList
      !secondaryPropertyList
      !signatureList
      !actionDeclarationList
      !obsoleteEntityNames
      !graphicEntity
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   TYPE INVENTORY                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @entityDeclaration typeInventory
  ?!@unifiedTypeMap ioUnifiedTypeMap
{
  [mSimpleStoredAttributeList typeInventory !?ioUnifiedTypeMap]
  for (item) in mSecondaryPropertyList do
    [item typeInventory
      !?ioUnifiedTypeMap
    ]
  end
  @actionMap actionMap = {}
  for (act) in mActionDeclarationList do
    [!?actionMap insertKey !act]
  end
  [mToOneRelationshipList typeInventory !?ioUnifiedTypeMap]
  [mToManyRelationshipList typeInventory !?ioUnifiedTypeMap]
  [!?ioUnifiedTypeMap insertKey
    !mEntityName
    !.entityType {!entityName:mEntityName.string !graphic:mIsGraphicEntity}
    !actionMap
  ]
  if mSuperEntityName.string != "" then
    [@unifiedTypeMap-proxy makeProxy !?ioUnifiedTypeMap !mSuperEntityName ?*]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @objectKind {
  case classObject
  case entityObject
  case graphicEntityObject
}

#·······················································································································

map @entityObservablePropertyMap {
  @observablePropertyMap mObservablePropertyMap
  @objectKind mObjectKind
  insert insertKey error message "the '%K' document is already declared in %L"
  search searchKey error message "there is no '%K' document"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

# Only get current entity observable properties, do not collect in super entities
override method @entityDeclaration buildObservablePropertyMapsFromStoredProperties
  ?let @unifiedTypeMap inUnifiedTypeMap
  ?!@semanticContext ioSemanticContext
{
  @observablePropertyMap observablePropertyMap = {}
#--- Analyze simple stored properties
  [mSimpleStoredAttributeList buildObservablePropertyMap
    !inUnifiedTypeMap
    !?observablePropertyMap
  ]
#--- Analyze relationships
  [mToOneRelationshipList buildObservablePropertyMap
    !inUnifiedTypeMap
    !?observablePropertyMap
  ]
  [mToManyRelationshipList buildObservablePropertyMap
    !inUnifiedTypeMap
    !?observablePropertyMap
  ]
#--- Enter in semantics context  
  [!?ioSemanticContext.mEntityObservablePropertyMap insertKey
    !mEntityName
    !observablePropertyMap
    !if mIsGraphicEntity then .graphicEntityObject else .entityObject end
  ]
}

#·······················································································································

override method @entityDeclaration buildInitialSecondaryPropertyListMap
  ?!@secondaryDeclarationListWorkingList ioSecondaryDeclarationListWorkingList
  ?!@uint ioSecondaryDeclarationCount
{
  ioSecondaryDeclarationCount = ioSecondaryDeclarationCount + [mSecondaryPropertyList length]
  ioSecondaryDeclarationListWorkingList += !self !mSecondaryPropertyList
}

#·······················································································································

override method @entityDeclaration solveSecondaryProperty
  ?!@semanticContext ioSemanticContext
  ?let @unifiedTypeMap unused inUnifiedTypeMap
  ?let @secondaryPropertyList inPropertiesToSolve
  !@secondaryPropertyList outUnsolvedProperties
{
  outUnsolvedProperties = {}
  [ioSemanticContext.mEntityObservablePropertyMap searchKey
    !mEntityName
    ?@observablePropertyMap observablePropertyMap
    ?*
  ]
  for (property) in inPropertiesToSolve do
    [property tryToSolveSecondaryProperty
      !?ioSemanticContext
      !{} # No root entity
      !?observablePropertyMap
      !?outUnsolvedProperties
    ]
  end
  [!?ioSemanticContext.mEntityObservablePropertyMap setMObservablePropertyMapForKey
    !observablePropertyMap
    !mEntityName.string
  ]
}

#·······················································································································

override method @entityDeclaration semanticAnalysis
  ?let @semanticContext inSemanticContext
  ?!@structForGeneration ioGeneration
{
  [inSemanticContext.mEntityObservablePropertyMap searchKey
    !mEntityName
    ?let @observablePropertyMap observablePropertyMap
    ?*
  ]
#--- TO DO : GET OBSERVABLE PROPERTIES FROM SUPER ENTITY
#--- Analyze simple stored properties
  [mSimpleStoredAttributeList simpleStoredPropertySemanticAnalysis
    !mEntityName.string
    !inSemanticContext.mUnifiedTypeMap
    !?ioGeneration.mValidationStubRoutineListForGeneration
    ?let @simpleStoredPropertyListForGeneration simpleStoredPropertyListForGeneration
  ]
#--- Analyze transients
  @transientDefinitionListForGeneration transientDefinitionListForGeneration = {}
  @arrayControllerForGeneration arrayControllerListForGeneration = {}
  @selectionControllerForGeneration selectionControllerForGeneration = {}
  for (item) in mSecondaryPropertyList do
    [item secondaryPropertySemanticAnalysis
      !mEntityName.string
      !"" # No root entity name
      !{} # No root entity
      !inSemanticContext
      !observablePropertyMap
      !?transientDefinitionListForGeneration
      !?arrayControllerListForGeneration
      !?selectionControllerForGeneration
      !?ioGeneration.mNeededOutletClasses
    ]
  end
  ioGeneration.mTransientListForGeneration += transientDefinitionListForGeneration
  ioGeneration.mSelectionControllerListForGeneration += selectionControllerForGeneration
  ioGeneration.mArrayControllerListForGeneration += arrayControllerListForGeneration
#--- Check requireds transients are implemented
  @stringset transientsDeclaredInSuperEntity = {}
  if mIsGraphicEntity then
    transientsDeclaredInSuperEntity += !"selectionDisplay"
    if [observablePropertyMap hasKey !"selectionDisplay"] then
      [observablePropertyMap searchKey
        !["selectionDisplay" here]
        ?let @propertyKind kind
        ?3*
      ]
      let errorLocation = [observablePropertyMap locationForKey !"selectionDisplay"]
      switch kind
      case property (type readOnly) :
        if not readOnly then
          error errorLocation : "the 'selectionDisplay' property should be a transient"
        elsif [type swiftTypeName] != "EBShape" then
          error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
        end
      case toMany (4*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case toOne (4*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case arrayController (3*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case selectionController (2*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      end
    else
      error mEntityName : "a graphic entity should declare the 'selectionDisplay' transient, with 'EBShape' type"
    end
    transientsDeclaredInSuperEntity += !"objectDisplay"
    if [observablePropertyMap hasKey !"objectDisplay"] then
      [observablePropertyMap searchKey
        !["objectDisplay" here]
        ?let @propertyKind kind
        ?3*
      ]
      let errorLocation = [observablePropertyMap locationForKey !"objectDisplay"]
      switch kind
      case property (type readOnly) :
        if not readOnly then
          error errorLocation : "the 'selectionDisplay' property should be a transient"
        elsif [type swiftTypeName] != "EBShape" then
          error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
        end
      case toMany (4*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case toOne (4*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case arrayController (3*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      case selectionController (2*) :
        error errorLocation : "the 'selectionDisplay' transient type should be 'EBShape'"
      end
    else
      error mEntityName : "a graphic entity should declare the 'objectDisplay' transient, with 'EBShape' type"
    end
  end
#--- Analyze toOne relationships
  [mToOneRelationshipList semanticAnalysis
    !inSemanticContext
    ?let @toOneEntityRelationshipListForGeneration toOneEntityRelationshipListForGeneration
  ]
#--- Analyze toMany relationships
  [mToManyRelationshipList semanticAnalysis
    !inSemanticContext
    ?let @toManyEntityRelationshipListForGeneration toManyEntityRelationshipListForGeneration
  ]
#--- Generation
  ioGeneration.mEntityListForGeneration +=
    !mEntityName.string
    !mSuperEntityName.string
    !observablePropertyMap
    !simpleStoredPropertyListForGeneration
    !transientDefinitionListForGeneration
    !toOneEntityRelationshipListForGeneration
    !toManyEntityRelationshipListForGeneration
    !mSignatureList
    !mObsoleteEntityNames
    !mIsGraphicEntity
    !transientsDeclaredInSuperEntity
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION TYPES                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @entityListForGeneration {
  @string mEntityName
  @string mSuperEntityName
  @observablePropertyMap mObservablePropertyMap
  @simpleStoredPropertyListForGeneration mSimpleStoredPropertyListForGeneration
  @transientDefinitionListForGeneration mDecoratedTransientListForGeneration
  @toOneEntityRelationshipListForGeneration mToOneEntityRelationshipList
  @toManyEntityRelationshipListForGeneration mToManyEntityRelationshipList
  @stringset mSignatureSet
  @lstringlist mObsoleteEntityNames
  @bool mIsGraphicEntity
  @stringset mTransientsDeclaredInSuperEntity
}

#·······················································································································

private filewrapper entityGenerationTemplate in "../generation-templates" {
}{
}{
  template entityImplementationInSwift "entity.swift.galgasTemplate"
    ?@string ENTITY_NAME
    ?@observablePropertyMap OBSERVABLE_PROPERTY_MAP
    ?@simpleStoredPropertyListForGeneration SIMPLE_STORED_PROPERTY_LIST_FOR_GENERATION
    ?@transientDefinitionListForGeneration TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@toOneEntityRelationshipListForGeneration TO_ONE_RELATIONSHIP_LIST
    ?@toManyEntityRelationshipListForGeneration TO_MANY_RELATIONSHIP_LIST
    ?@stringset SIGNATURE_SET
    ?@bool IS_GRAPHIC_ENTITY
    ?@stringset TRANSIENTS_DECLARED_IN_SUPER_ENTITY

  template managedObjectContext "EBManagedObjectContext.swift.galgasTemplate"
    ?@entityListForGeneration ENTITY_LIST
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc generateEntities
  ?let @entityListForGeneration inEntityListForGeneration
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  for () in inEntityListForGeneration do
    let s = [filewrapper entityGenerationTemplate.entityImplementationInSwift
      !mEntityName
      !mObservablePropertyMap
      !mSimpleStoredPropertyListForGeneration
      !mDecoratedTransientListForGeneration
      !mToOneEntityRelationshipList
      !mToManyEntityRelationshipList
      !mSignatureSet
      !mIsGraphicEntity
      !mTransientsDeclaredInSuperEntity
    ]
    let fileName = "entity-" + mEntityName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
#----- 
  if [inEntityListForGeneration length] > 0 then
    let fileName = "EBManagedObjectContext.swift"
    ioGeneratedFileSet += !fileName
    let s = [filewrapper entityGenerationTemplate.managedObjectContext
      !inEntityListForGeneration
    ]
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
