#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST   
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @entityDeclarationAST : @abstractDeclarationAST {
  @bool mIsAbstract
  @lstring mSuperEntityName
  @simpleStoredPropertyList mSimpleStoredAttributeList
  @stringset mSignatureList
  @lstringlist mActionDeclarationList
  @lstringlist mObsoleteEntityNames
  @bool mIsGraphicEntity
  @externSwiftDelegateList mExternSwiftDelegateList
}

#·······················································································································

override method @entityDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  [!?ioGraph addNode !mClassName !self]
  if mSuperEntityName.string != "" then
    [!?ioGraph addEdge !mClassName !mSuperEntityName]
  end
}

#·······················································································································

override getter @entityDeclarationAST nodeKey -> @lstring {
  result = mClassName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <entity_declaration> ?!@astDeclarationStruct ioDeclarationAST {
    @bool isAbstract
    select
      isAbstract = false
    or
      $abstract$
      isAbstract = true
    end
    @bool graphicEntity
    select
      graphicEntity = false
    or
      $graphic$
      graphicEntity = true
    end
    $entity$
    $Identifier$ ?let @lstring entityName
    @lstringlist obsoleteEntityNames = {}
    select
    or
      $($
      repeat
        $Identifier$ ?let @lstring obsoleteName
        obsoleteEntityNames += !obsoleteName
      while
        $,$
      end
      $)$
    end
    @lstring superEntityName
    select
      superEntityName = ["" nowhere]
    or
      $:$
      $Identifier$ ? superEntityName
    end
    ${$
    @simpleStoredPropertyList simpleStoredAttributeList = {}
    @stringset signatureList = {}
    @lstringlist actionDeclarationList = {}
    @externSwiftDelegateList externSwiftDelegateList = {}
    repeat
    while
      <simple_stored_declaration> !entityName !?simpleStoredAttributeList !?signatureList !?ioDeclarationAST
    while
      <transient_declaration> !entityName !["" nowhere] !?ioDeclarationAST
    while
      <toMany_relationship> !entityName !?signatureList !?ioDeclarationAST
    while
      <toOne_relationship> !entityName !?ioDeclarationAST
    while
      <action_declaration> !?actionDeclarationList
    while
      <controller_declaration> !entityName !["" nowhere]  !?ioDeclarationAST
    while
      <extern_swift_delegate> !?externSwiftDelegateList
    end
    $}$
    ioDeclarationAST.mUnifiedDeclarationList += !@entityDeclarationAST.new {
      !entityName
      !isAbstract
      !superEntityName
      !simpleStoredAttributeList
      !signatureList
      !actionDeclarationList
      !obsoleteEntityNames
      !graphicEntity
      !externSwiftDelegateList
    }
    if isAbstract && graphicEntity then
      ioDeclarationAST.mUnifiedDeclarationList += !@transientDeclarationAST.new {
        !entityName
        !["" here]
        !["EBShape" here]
        !["selectionDisplay" here]
        !{}
        !["" here]
        !false
      }
      ioDeclarationAST.mUnifiedDeclarationList += !@transientDeclarationAST.new {
        !entityName
        !["" here]
        !["EBShape" here]
        !["objectDisplay" here]
        !{}
        !["" here]
        !false
      }
    end
 }
 
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @entityDeclarationAST firstAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct unused ioGeneration
{
  buildActionMap (!mActionDeclarationList ?let @actionMap actionMap)
  [!?ioSemanticContext.mClassMap insertKey
    !mClassName
    !.entity {!superEntityName: mSuperEntityName.string !isGraphic: mIsGraphicEntity !isAbstract: mIsAbstract}
    !{}
    !actionMap
    !{}
  ]
}

#·······················································································································

override method @entityDeclarationAST secondAnalysisPhase
    ?!@semanticContext ioSemanticContext
    ?!@generationStruct unused ioGeneration
{
  if mSuperEntityName.string != "" then
  #--- Search for super entity property map
    [ioSemanticContext.mClassMap searchKey
      !mSuperEntityName
      ?*
      ?let @propertyMap inheritedPropertyMap
      ?2*
    ]  
#    message "Entity '" + mClassName + "\n"
#    message "  Super entity '" + mSuperEntityName + "' has " + [inheritedPropertyMap count] + " properties\n"
  #--- Search for current entity property map
    [ioSemanticContext.mClassMap searchKey
      !mClassName
      ?*
      ?let @propertyMap propertyMap
      ?2*
    ]
  #--- Merge properties
    var newPropertyMap = inheritedPropertyMap
#    for (lkey 3*) in inheritedPropertyMap do
#        message "  inherit '" + lkey + "'\n"
#    end
    for (lkey kind actionMap isOverriding) in propertyMap do
      if isOverriding then
        [inheritedPropertyMap searchKey !lkey ?3*]
      else
#        message "  insert '" + lkey + "'\n"
        [!?newPropertyMap insertKey !lkey !kind !actionMap !false]
      end
    end
  #--- Replace property map
    with mClassName in !?ioSemanticContext.mClassMap 
      error message searchKey
    do
      mPropertyMap = newPropertyMap
    end
  end
}

#·······················································································································

override method @entityDeclarationAST fourthAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
  [ioSemanticContext.mClassMap searchKey
    !mClassName
    ?*
    ?let @propertyMap propertyMap
    ?*
    ?let @propertyGenerationList propertyGenerationList
  ]
#--- If super entity is abstract, and current is not, check all transient properties are implemented
  @stringset overridenTransients = {}
  if (mSuperEntityName.string != "") & not mIsAbstract then
    [ioSemanticContext.mClassMap searchKey
      !mSuperEntityName
      ?let @classKind superClassKind
      ?let @propertyMap superPropertyMap
      ?2*
    ]
    switch superClassKind
    case prefs :
      error mSuperEntityName : "prefs cannot be a super entity"
    case atomic (*) :
      error mSuperEntityName : "an atomic class cannot be a super entity"
    case document (*) :
      error mSuperEntityName : "a document cannot be a super entity"
    case entity (* @bool isGraphic @bool isAbstract) :
      if isGraphic & not mIsGraphicEntity then
        error mClassName : "this entity should be declared with 'graphic', as super entity is graphic"
      end
      if isAbstract & not mIsAbstract then # Check all abstract properties are defined
        for superProperty in superPropertyMap do
          if [superProperty isAbstract] then
            if [propertyMap hasKey !superProperty.lkey.string] then
              overridenTransients += !superProperty.lkey.string
              [propertyMap searchKey !superProperty.lkey ?let @propertyKind propertyKind ?2*]
              let typeName = [propertyKind typeName]
              let superTypeName = [superProperty.mKind typeName]
              if typeName != superTypeName then
                error [propertyMap locationForKey !superProperty.lkey]
                  : "type '" + superTypeName + "' is required by declaration in super entity"
              end
            else
              error mClassName
                : "transient '" + superProperty.lkey + "' should be defined, it is declared abstract in super entity"
            end
          end
        end
      end
    end
  end
#--- Generation    
  ioGeneration.mEntityListForGeneration +=
    !mClassName.string
    !mObsoleteEntityNames
  ioGeneration.mFileGenerationList += !@entityForGeneration.new {
    !mClassName.string
    !mSuperEntityName.string
    !propertyGenerationList
    !mSignatureList
    !mIsGraphicEntity
    !mIsAbstract
    !overridenTransients
    !mExternSwiftDelegateList
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @entityForGeneration : @abstractFileGeneration {
  @string mEntityName
  @string mSuperEntityName
  @propertyGenerationList mPropertyGenerationList
  @stringset mSignatureSet
  @bool mIsGraphicEntity
  @bool mIsAbstract
  @stringset mOverridenTransients
  @externSwiftDelegateList mExternSwiftDelegateList
}

#·······················································································································

list @entityListForGeneratingEBManagedObjectContext {
  @string mEntityName
  @lstringlist mObsoleteEntityNames
}

#·······················································································································

override method @entityForGeneration generateCode
    ?let @string inOutputDirectory
    ?!@stringset ioGeneratedFileSet
{
  @atomicPropertyGenerationList atomicPropertyGenerationList = {}
  @transientPropertyGenerationList transientPropertyGenerationList = {}
  @toOnePropertyGenerationList toOnePropertyGenerationList = {}
  @toManyPropertyGenerationList toManyPropertyGenerationList = {}
  for (property) in mPropertyGenerationList do
    if let atomicProperty = property as @atomicPropertyGeneration then
      atomicPropertyGenerationList += !atomicProperty
    elsif let transientProperty = property as @transientPropertyGeneration then
      transientPropertyGenerationList += !transientProperty
    elsif let toOneProperty = property as @toOnePropertyGeneration then
      toOnePropertyGenerationList += !toOneProperty
    elsif let toOneProperty = property as @toManyPropertyGeneration then
      toManyPropertyGenerationList += !toOneProperty
    end
  end
  var superEntityName = ""
  if mSuperEntityName != "" then
    superEntityName = mSuperEntityName
  elsif mIsGraphicEntity then
    superEntityName = "EBGraphicManagedObject"
  else
    superEntityName = "EBManagedObject"
  end
  let s = [filewrapper entityGenerationTemplate.entityImplementationInSwift
    !mEntityName
    !superEntityName
    !mPropertyGenerationList
    !atomicPropertyGenerationList
    !transientPropertyGenerationList
    !toOnePropertyGenerationList
    !toManyPropertyGenerationList
    !mSignatureSet
    !mIsGraphicEntity
    !mIsAbstract
    !mOverridenTransients
    !mExternSwiftDelegateList

  ]
  let fileName = "entity-" + mEntityName + ".swift"
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !s
  ]
}

#·······················································································································

proc generateEBManagedObjectContext
  ?let @entityListForGeneratingEBManagedObjectContext inEntityListForGeneration
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  if [inEntityListForGeneration length] > 0 then
    let fileName = "EBManagedObjectFactory.swift"
    ioGeneratedFileSet += !fileName
    let s = [filewrapper entityGenerationTemplate.managedObjectFactory
      !inEntityListForGeneration
    ]
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
}

#·······················································································································

private filewrapper entityGenerationTemplate in "../generation-templates" {
}{
}{
  template entityImplementationInSwift "entity.swift.galgasTemplate"
    ?@string ENTITY_NAME
    ?@string SUPER_CLASS_NAME
    ?@propertyGenerationList PROPERTY_LIST_FOR_GENERATION
    ?@atomicPropertyGenerationList SIMPLE_STORED_PROPERTY_LIST_FOR_GENERATION
    ?@transientPropertyGenerationList TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@toOnePropertyGenerationList TO_ONE_RELATIONSHIP_LIST
    ?@toManyPropertyGenerationList TO_MANY_RELATIONSHIP_LIST
    ?@stringset SIGNATURE_SET
    ?@bool IS_GRAPHIC_ENTITY
    ?@bool IS_ABSTRACT
    ?@stringset OVERRIDEN_TRANSIENTS
    ?@externSwiftDelegateList EXTERN_DELEGATE_LIST_FOR_IMPLEMENTATION

  template managedObjectFactory "EBManagedObjectFactory.swift.galgasTemplate"
    ?@entityListForGeneratingEBManagedObjectContext ENTITY_LIST
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
