
#-----------------------------------------------------------------------------------------------------------------------
#   AST
#-----------------------------------------------------------------------------------------------------------------------

list @controllerBindingOptionList {
  @lstring mOptionName
  @lstring mOptionTypeName
}

#·······················································································································

list @outletClassBindingSpecificationModelList {
  @lstring mModelTypeName
  @bool mModelShouldBeWritableProperty
}

#·······················································································································

list @outletClassBindingSpecificationList {
  @lstring mOutletClassName
  @lstring mBindingName
  @outletClassBindingSpecificationModelList mOutletClassBindingSpecificationModelList
  @controllerBindingOptionList mBindingOptionList
}

#-----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
#-----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <outlet_binding_specification> ?!@outletClassBindingSpecificationList ioControllerTemplateList {
    $binding$
    $Identifier$ ?let @lstring outletClassName
    $bindingName$ ?let @lstring bindingName
    $:$
    @outletClassBindingSpecificationModelList outletClassBindingSpecificationModelList = {}
    repeat
      @bool modelShouldBeWritableProperty
      select
        $property$
        modelShouldBeWritableProperty = true
      or
        $transient$
        modelShouldBeWritableProperty = false
      end
      @lstring modelTypeName
      select
        $Identifier$ ?modelTypeName
      or
        $enum$
        modelTypeName = .new {!"enum" !.here}
      end
      outletClassBindingSpecificationModelList += !modelTypeName !modelShouldBeWritableProperty
    while
      $,$
    end
    @controllerBindingOptionList bindingOptionList = {}
    select
    or
      ${$
      repeat
        $identifier$ ?let @lstring optionName
        $:$
        $Identifier$ ?let @lstring optionType
        bindingOptionList += !optionName !optionType
      while
        $,$
      end
      $}$
    end
    $;$
    ioControllerTemplateList +=
      !outletClassName
      !bindingName
      !outletClassBindingSpecificationModelList
      !bindingOptionList
  }

  #·····················································································································

}

#-----------------------------------------------------------------------------------------------------------------------
#  SEMANTICS
#-----------------------------------------------------------------------------------------------------------------------

map @bindingSpecificationMap {
  @lstring mOutletSuperClassName
  @outletBindingSpecificationMap mBindingMap
  insert insertKey error message "the '%K' binding is already declared in %L"
  search searchKey error message "there is no '%K' binding"
  remove removeKey error message "there is no '%K' binding"
}

#·······················································································································

list @outletBindingSpecificationModelList {
  @typeKind mModelType
  @bool mModelShouldBeWritableProperty
}

#·······················································································································

list @controllerBindingOptionDecoratedList {
  @typeKind mOptionType
  @lstring mOptionName
}

#·······················································································································

map @outletBindingSpecificationMap {
  @outletBindingSpecificationModelList mOutletBindingSpecificationModelList
  @controllerBindingOptionDecoratedList mControllerBindingOptionList
  insert insertKey error message "the '%K' binding is already declared in %L"
  search searchKey error message "there is no '%K' binding"
}

#·······················································································································

proc buildBindingSpecificationMap
  ?!@semanticContext ioSemanticContext
  ?let @outletClassBindingSpecificationList inBindingSpecificationListMap
{
  for () in inBindingSpecificationListMap do
    @outletBindingSpecificationModelList outletBindingSpecificationModelList = {}
    for () in mOutletClassBindingSpecificationModelList do
      if mModelTypeName.string == "enum" then
        outletBindingSpecificationModelList +=
          !.enumType {!enumTypeName: "" !constantMap: {} !funcMap: {}} # Any enum type
          !mModelShouldBeWritableProperty
      else
        [ioSemanticContext.mClassMap searchKey !mModelTypeName ?let @classKind classKind ?3*]
        @typeKind typeKind
        switch classKind
        case prefs :
          error mModelTypeName : "an atomic class is required here" : typeKind
        case atomic (@typeKind kind) :
          typeKind = kind
        case document (*) :
          error mModelTypeName : "an atomic class is required here" : typeKind
        case entity (3*) :
          error mModelTypeName : "an atomic class is required here" : typeKind
        end
        outletBindingSpecificationModelList += !typeKind !mModelShouldBeWritableProperty
      end
    end
    @controllerBindingOptionDecoratedList controllerBindingOptionDecoratedList = {}
    for () in mBindingOptionList do
      [ioSemanticContext.mClassMap searchKey !mOptionTypeName ?let @classKind classKind ?3*]
      @typeKind typeKind
      switch classKind
      case prefs :
        error mOptionTypeName : "an atomic class is required here" : typeKind
      case atomic (@typeKind kind) :
        typeKind = kind
      case document (*) :
        error mOptionTypeName : "an atomic class is required here" : typeKind
      case entity (3*) :
        error mOptionTypeName : "an atomic class is required here" : typeKind
      end
      controllerBindingOptionDecoratedList += !typeKind !mOptionName
    end
  #--- Insert in binding map
    @outletBindingSpecificationMap bindingMap
    @lstring outletSuperClassName
    if [ioSemanticContext.mBindingSpecificationMap hasKey !mOutletClassName.string] then
      [!?ioSemanticContext.mBindingSpecificationMap removeKey !mOutletClassName ?outletSuperClassName ?bindingMap]
    else
      bindingMap = {}
      [ioSemanticContext.mOutletClassMap searchKey !mOutletClassName ?outletSuperClassName ?6*]
    end
    [!?bindingMap insertKey
      !mBindingName
      !outletBindingSpecificationModelList
      !controllerBindingOptionDecoratedList
    ]
    [!?ioSemanticContext.mBindingSpecificationMap insertKey !mOutletClassName !outletSuperClassName !bindingMap]
  end
}

#-----------------------------------------------------------------------------------------------------------------------
