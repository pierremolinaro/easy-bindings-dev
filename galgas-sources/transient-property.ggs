#-----------------------------------------------------------------------------------------------------------------------
#   AST   
#-----------------------------------------------------------------------------------------------------------------------

class @transientDeclarationAST : @abstractDeclarationAST {
  public var @lstring mRootEntityName
  public var @lstring mTransientTypeName
  public var @lstring mTransientName
  public var @observablePropertyList mDependencyList
  public var @bool mIsOverriding
}

#·······················································································································

override proc @transientDeclarationAST.enterInPrecedenceGraph (?!@declarationPrecedenceGraph ioGraph) {
  let node = @lstring (!self.mClassName.string + " " + self.mTransientName !self.mTransientName.location)
  [!?ioGraph addNode !node !self]
  [!?ioGraph addEdge !node !self.mClassName]
  [!?ioGraph addEdge !node !self.mTransientTypeName]
  for dependency in self.mDependencyList do
    switch dependency.mObservableProperty
    case rootProperty (propertyName) :
      if self.mRootEntityName.string != "" then
        [!?ioGraph addEdge !node !.new (!self.mRootEntityName.string + " " + propertyName !propertyName.location)]
      end
    case rootPropertyWithOption (propertyName *) :
      if self.mRootEntityName.string != "" then
        [!?ioGraph addEdge !node !.new (!self.mRootEntityName.string + " " + propertyName !propertyName.location)]
      end
    case rootPropertyRelationship (relationshipName *) :
      if self.mRootEntityName.string != "" then
        [!?ioGraph addEdge !node !.new (!self.mRootEntityName.string + " " + relationshipName !relationshipName.location)]
      end
    case rootPropertyNone (relationshipName) :
      if self.mRootEntityName.string != "" then
        [!?ioGraph addEdge !node !.new (!self.mRootEntityName.string + " " + relationshipName !relationshipName.location)]
      end
    case selfWithoutProperty (*) :
    case selfProperty (propertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string + " " + propertyName !propertyName.location)]
    case selfPropertyWithOption (@lstring propertyName *) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string + " " + propertyName !propertyName.location)]
    case selfPropertyArray (@lstring propertyName *) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string + " " + propertyName !propertyName.location)]
    case selfPropertyObject (@lstring propertyName *) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string + " " + propertyName !propertyName.location)]
    case selfPropertyNone (@lstring propertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string + " " + propertyName !propertyName.location)]
    case prefsProperty (@lstring propertyName) :
      [!?ioGraph addEdge !node !.new (!preferencesName () + " " + propertyName !propertyName.location)]
    case prefsPropertyWithOption (@lstring propertyName *) :
      [!?ioGraph addEdge !node !.new (!preferencesName () + " " + propertyName !propertyName.location)]
    case selfControllerProperty (@lstring controllerName unused propertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string  + " " + controllerName !controllerName.location)]
    case selfControllerSecondaryProperty (@lstring controllerName unused propertyName unused secondaryPropertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string  + " " + controllerName !controllerName.location)]
    case selfControllerAllProperties (@lstring controllerName unused propertyName unused secondaryPropertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string  + " " + controllerName !controllerName.location)]
    case selfControllerOneProperty (@lstring controllerName unused propertyName) :
      [!?ioGraph addEdge !node !.new (!self.mClassName.string  + " " + controllerName !controllerName.location)]
    case signatureProperty (*) :
    case versionProperty (*) :
    case versionShouldChangeProperty (*) :
    case superProperty (@lstring superEntityName @lstring propertyName) :
      [!?ioGraph addEdge !node !.new (!superEntityName.string + " " + propertyName !propertyName.location)]
    case superPropertyWithDefaultValue (@lstring superEntityName @lstring propertyName *) :
      [!?ioGraph addEdge !node !.new (!superEntityName.string + " " + propertyName !propertyName.location)]
    end
  end
}

#·······················································································································

override func @transientDeclarationAST.nodeKey() -> @lstring {
  result = @lstring (!self.mClassName.string + " " + self.mTransientName !self.mTransientName.location)
}

#-----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
#-----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <transient_declaration>(
            ?let @lstring inCurrentEntity
            ?let @lstring inRootEntity
            ?!@astDeclarationStruct ioDeclarationAST)
  {
    $abstract$
    $transient$
    $Identifier$ (?let transientTypeName)
    $identifier$ (?let @lstring transientName)
    $;$
    ioDeclarationAST.mUnifiedDeclarationList += !@transientDeclarationAST (
      !inCurrentEntity
      !inRootEntity
      !transientTypeName
      !transientName
      !{}
      !false
    )
  }

  #·····················································································································
  
  rule <transient_declaration>(
            ?let @lstring inCurrentEntity
            ?let @lstring inRootEntity
            ?!@astDeclarationStruct ioDeclarationAST)
  {
    let @bool isOverriding
    select
      isOverriding = false
    or
      $override$
      isOverriding = true
    end
    $transient$
    $Identifier$ (?let transientTypeName)
    $identifier$ (?let @lstring transientName)
    $dependsFrom$
    var @observablePropertyList dependanceList = {}
    repeat
      <observable_property> (?let @observablePropertyAST observablePropertyAST)
      dependanceList += !observablePropertyAST
    while
      $,$
    end
    $;$
    ioDeclarationAST.mUnifiedDeclarationList += !@transientDeclarationAST (
      !inCurrentEntity
      !inRootEntity
      !transientTypeName
      !transientName
      !dependanceList
      !isOverriding
    )
  }
}

#-----------------------------------------------------------------------------------------------------------------------
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#-----------------------------------------------------------------------------------------------------------------------

override proc @transientDeclarationAST.firstAnalysisPhase(
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct unused ioGeneration)
{
  with self.mClassName in !?ioSemanticContext.mClassMap 
    error message searchKey
  do
    [ioSemanticContext.mClassMap searchKey !self.mTransientTypeName ?let classKind ?3*]
    switch classKind
    case prefs :
      error self.mTransientTypeName : "an atomic class is required here"
    case atomic (kind) :
      [!?mPropertyMap insertKey
        !self.mTransientName
        !.property (!type: kind !accessibility: .transient (!isAbstract:[self.mDependencyList length] == 0))
        !{}
        !self.mIsOverriding
      ]
    case document (*) :
      error self.mTransientTypeName : "an atomic class is required here"
    case entity (4*) :
      error self.mTransientTypeName : "an atomic class is required here"
    end
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#   SECOND ANALYSIS PHASE
#-----------------------------------------------------------------------------------------------------------------------

override proc @transientDeclarationAST.thirdAnalysisPhase(
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration) {
#--- Property type
  [ioSemanticContext.mClassMap searchKey !self.mTransientTypeName ?let classKind ?3*]
#--- Check type
  let @typeKind typeKind
  switch classKind
  case prefs :
    error self.mTransientTypeName: "an atomic class is required here" : typeKind
  case atomic (@typeKind kind) :
    typeKind = kind
  case document (*) :
    error self.mTransientTypeName: "an atomic class is required here" : typeKind
  case entity (4*) :
    error self.mTransientTypeName: "an atomic class is required here" : typeKind
  end
#--- Root properties
  let @propertyMap rootProperties
  if self.mRootEntityName.string == "" then
    rootProperties = {}
  else
    [ioSemanticContext.mClassMap searchKey !self.mRootEntityName ?* ?rootProperties ?2*]
  end
#--- Current class properties
  [ioSemanticContext.mClassMap searchKey !self.mClassName ?let currentClassKind ?let properties ?2*]
#--- Analyse master properties
  var @transientDependencyListForGeneration dependencies = {}
  for (mObservableProperty) in self.mDependencyList do
    [mObservableProperty analyzeObservableProperty
      !rootProperties
      !ioSemanticContext
      !properties
      ?let @propertyKind dependencyKind
      ?let @string swiftTypeStringForTransientFunctionArgument
      ?let @string defaultValueAsString
    ]
#    message "defaultValueAsString " + defaultValueAsString + "\n"
    switch dependencyKind
    case property (* *) :
    case toMany (4*) :
    case toOne (5*) :
    case arrayController (2*) :
      error [mObservableProperty location] : "only an atomic property can be observed"
    case selectionController (*) :
      error [mObservableProperty location] : "only an atomic property can be observed"
    end
    dependencies +=
      !mObservableProperty
      ![mObservableProperty modelStringFunctionArgument]
      !swiftTypeStringForTransientFunctionArgument
      !defaultValueAsString
  end
  ioGeneration.mFileGenerationList += !@transientRoutineGeneration (
    !self.mClassName
    !self.mTransientName.string
    !typeKind
    !"transient"
    !dependencies
  )
  with self.mClassName in !?ioSemanticContext.mClassMap 
    error message searchKey
  do
    let @bool generate
    switch currentClassKind
    case prefs :
      generate = true
    case document (*) :
      generate = true
    case atomic (*) :
      generate = true
    case entity (* graphic 2*):
      if graphic then
        generate = (self.mTransientName.string != "selectionDisplay") & (self.mTransientName.string != "objectDisplay")
      else
        generate = true
      end
    end
    mPropertyGenerationList += !@transientPropertyGeneration (
      !self.mTransientName.string
      !self.mClassName.string
      !typeKind
      !dependencies
      !generate
    )
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#   CODE GENERATION                       
#-----------------------------------------------------------------------------------------------------------------------

class @transientPropertyGeneration : @propertyGeneration {
  public var @string mClassName
  public var @typeKind mType
  public var @transientDependencyListForGeneration mDependencyList
  public var @bool mGenerate
}

#·······················································································································

list @transientPropertyGenerationList {
  public var @transientPropertyGeneration mProperty
}

#·······················································································································

override func @transientPropertyGeneration.declarationInSelectionControllerCode() -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n"
  result += "  //   Selection observable property: " + self.mPropertyName + "\n"
  result += "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
  result += "  let " + self.mPropertyName + "_property = EBTransientProperty_" + [self.mType swiftTypeName] + " ()\n\n"
}

#·······················································································································

override func @transientPropertyGeneration.initCode() -> @string {
  result  = ""
}

#·······················································································································

override func @transientPropertyGeneration.bindPropertyInSelectionController() -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
  result += "  private final func bind_property_" + self.mPropertyName + " () {\n"
  result += "    self.selectedArray_property.addEBObserverOf_" + self.mPropertyName + " (self." + self.mPropertyName + "_property)\n"
  result += "    self." + self.mPropertyName + "_property.mReadModelFunction = { [weak self] in\n"
  result += "      if let model = self?.selectedArray_property {\n"
  result += "        switch model.selection {\n"
  result += "        case .empty :\n"
  result += "          return .empty\n"
  result += "        case .multiple :\n"
  result += "          return .multiple\n"
  result += "        case .single (let v) :\n"
  result += "          var s = Set <" + [self.mType swiftTypeName] + "> ()\n"
  result += "          var isMultipleSelection = false\n"
  result += "          for object in v {\n"
  result += "            switch object." + self.mPropertyName + "_property.selection {\n"
  result += "            case .empty :\n"
  result += "              return .empty\n"
  result += "            case .multiple :\n"
  result += "              isMultipleSelection = true\n"
  result += "            case .single (let vProp) :\n"
  result += "              s.insert (vProp)\n"
  result += "            }\n"
  result += "          }\n"
  result += "          if isMultipleSelection {\n"
  result += "            return .multiple\n"
  result += "          }else if s.count == 0 {\n"
  result += "            return .empty\n"
  result += "          }else if s.count == 1 {\n"
  result += "            return .single (s.first!)\n"
  result += "          }else{\n"
  result += "            return .multiple\n"
  result += "          }\n"
  result += "        }\n"
  result += "      }else{\n"
  result += "        return .empty\n"
  result += "      }\n"
  result += "    }\n"
  result += "  }\n"
}

#·······················································································································

override func @transientPropertyGeneration.propertyDeclarationCode(
   ?let @bool inPreferences
   ?let @stringset inOverriddenTransients) -> @string
{
  result  = ""
  if self.mGenerate & not [inOverriddenTransients hasKey !self.mPropertyName] then
    if inPreferences then
      result += "//" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n"
      result += "//   Transient property: " + self.mPropertyName + "\n"
      result += "//" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
      result += "@MainActor let preferences_" + self.mPropertyName + "_property = EBTransientProperty_" + [self.mType swiftTypeName] + " ()\n\n"
      result += "//" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
      result += "@MainActor var preferences_" + self.mPropertyName + " : " + [self.mType swiftTypeName] + "? {\n"
      result += "  switch preferences_" + self.mPropertyName + "_property.selection {\n"
      result += "  case .empty, .multiple :\n"
      result += "    return nil\n"
      result += "  case .single (let v) :\n"
      result += "    return v\n"
      result += "  }\n"
      result += "}\n\n"
    else
      result += "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n"
      result += "  //   Transient property: " + self.mPropertyName + "\n"
      result += "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
      result += "  final let " + self.mPropertyName + "_property = EBTransientProperty_" + [self.mType swiftTypeName] + " ()\n\n"
      result += "  //" + @string.stringWithSequenceOfCharacters  (!'·' !116) + "\n\n"
      result += "  final var " + self.mPropertyName + " : " + [self.mType swiftTypeName] + "? {\n"
      result += "    switch self." + self.mPropertyName + "_property.selection {\n"
      result += "    case .empty, .multiple :\n"
      result += "      return nil\n"
      result += "    case .single (let v) :\n"
      result += "      return v\n"
      result += "    }\n"
      result += "  }\n\n"
    end
  end
}

#·······················································································································

override func @transientPropertyGeneration.configurationCode (?let @bool inPreferences) -> @string {
  let prefix = if inPreferences then "preferences_" else "self." end
  result  = ""
  if [self.mDependencyList length] > 0 then
    let needs_unwSelf = [self.mDependencyList needs_unwSelf !inPreferences]
    result += "  //--- Atomic property: " + self.mPropertyName + "\n"
    if needs_unwSelf then
      result += "    " + prefix + self.mPropertyName + "_property.mReadModelFunction = { [weak self] in\n"
      result += "      if let unwSelf = self {\n"
      result += [self.mDependencyList transientComputeFunctionCall !inPreferences !self.mClassName !self.mPropertyName !"transient"]
      result += "      }else{\n"
      result += "        return .empty\n"
      result += "      }\n"
    else
      result += "    " + prefix + self.mPropertyName + "_property.mReadModelFunction = {\n"
      result += [self.mDependencyList transientComputeFunctionCall !inPreferences !self.mClassName !self.mPropertyName !"transient"]
    end
    result += "    }\n"
    for (mDependency 3*) in self.mDependencyList do
      let s = [mDependency generateAddObserverCall !inPreferences]
      if s != "" then
        result += "    " + s + " (" + prefix + self.mPropertyName + "_property)\n"
      end
    end
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#   GENERATION TYPE                       
#-----------------------------------------------------------------------------------------------------------------------

list @transientDependencyListForGeneration {
  public let @observablePropertyAST mDependency
  public let @string mFunctionArgumentName
  public let @string mFunctionArgumentTypeString
  public let @string mDefaultValueAsString
}

#·······················································································································

func @transientDependencyListForGeneration.transientComputeFunctionCall(
                                        ?let @bool inPreferences
                                        ?let @string inOwnerName
                                        ?let @string inTransientName
                                        ?let @string inFunctionNamePrefix) -> @string outResult {
  let uwself = if inPreferences then "preferences_" else "unwSelf." end
  outResult = ""
  let ident = "        "
  for (dependency 2* defaultValue) in self do (idx)
    if defaultValue != "" then
      outResult += ident + "var s" + idx + " = " + [dependency modelStringForSelf !uwself] + ".selection" + "\n"
      outResult += ident + "switch s" + idx + " {\n"
      outResult += ident + "case .single :\n"
      outResult += ident + "  ()\n"
      outResult += ident + "case .empty, .multiple :\n"
      outResult += ident + "  s" + idx + " = .single (" + defaultValue + ")\n"
      outResult += ident + "}\n"
    else
      outResult += ident + "let s" + idx + " = " + [dependency modelStringForSelf !uwself] + ".selection" + "\n"
    end
  end
  outResult += ident + "switch ("
  for (4*) in self
  do (idx)
    outResult += "s" + idx
  between
    outResult += ", "
  end
  outResult += ") {\n"
  outResult += ident + "case ("
  for (4*) in self
  do (idx)
    outResult += ".single (let v" + idx + ")"
  between
    outResult += ",\n              "
  end
  outResult += ") :\n"
  outResult += ident + "  return .single ("
  outResult += inFunctionNamePrefix + "_" + inOwnerName + "_" + inTransientName
  outResult += " ("
  for (4*) in self
  do (idx)
    outResult += "v" + idx
  between
    outResult += ", "
  end
  outResult += "))\n"
  outResult += ident + "case ("
  for (4*) in self
  do
    outResult += ".multiple"
  between
    outResult += ",\n              "
  end
  outResult += ") :\n"
  outResult += ident + "  return .multiple\n"
  outResult += ident + "default :\n"
  outResult += ident + "  return .empty\n"
  outResult += ident + "}\n"
}

#·······················································································································

func @transientDependencyListForGeneration.needs_unwSelf (?let @bool inPreferences) -> @bool outResult {
  outResult = false
  for (dep 3*) in self while not outResult do
    outResult = [dep needs_unwSelf !inPreferences]
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#   GENERATION                            
#-----------------------------------------------------------------------------------------------------------------------

func @observablePropertyAST.generateAddObserverCall (?let @bool inPreferences) -> @string outResult {
  let prefix = if inPreferences then "preferences_" else "self." end
  switch self
  case selfControllerProperty (@lstring controllerName @lstring propertyName) : 
    outResult = prefix + controllerName.string + "." + propertyName + "_property.addEBObserver"
  case selfControllerSecondaryProperty (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = prefix + controllerName.string + "." + propertyName + "_property." + secondaryPropertyName + "_property.addEBObserver"
  case selfControllerAllProperties (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = prefix + controllerName.string + "." + propertyName + "_property.addEBObserverOf_" + secondaryPropertyName 
  case selfControllerOneProperty (@lstring controllerName @lstring propertyName) : 
    outResult = prefix + controllerName.string + "." + propertyName + "_property.<<4>>" 
  case selfWithoutProperty (*) :
    outResult = "" # Do not generate add observer
  case selfProperty (@lstring masterName) :
    outResult = prefix + masterName.string + "_property.addEBObserver"
  case selfPropertyWithOption (@lstring masterName *) :
    outResult = prefix + masterName.string + "_property.addEBObserver"
  case selfPropertyArray (@lstring masterName @lstring elementPropertyName) :
    outResult = prefix + masterName.string + "_property.addEBObserverOf_" + elementPropertyName
  case selfPropertyObject (@lstring masterName @lstring elementPropertyName) :
    outResult = prefix + masterName.string + "_property." + elementPropertyName + "_property.addEBObserver"
  case selfPropertyNone (@lstring masterName) :
    outResult = prefix + masterName.string + "_property.addEBObserver"
  case prefsProperty (@lstring propertyName) :
    outResult = "preferences_"  + propertyName + "_property.addEBObserver"
  case prefsPropertyWithOption (@lstring propertyName *) :
    outResult = "preferences_"  + propertyName + "_property.addEBObserver"
  case rootProperty (@lstring masterName) :
    outResult = "self.rootObject." + masterName + "_property.addEBObserver"
  case rootPropertyNone (@lstring masterName) :
    outResult = "self.rootObject." + masterName + "_property.addEBObserver"
  case rootPropertyWithOption (@lstring masterName @lstring optionName) :
    outResult = "self.rootObject." + masterName + "_property." + optionName + "_property.addEBObserver"
  case rootPropertyRelationship (@lstring relationshipName @lstring propertyName) :
    outResult = "self.rootObject." + relationshipName + "_property.addEBObserverOf_" + propertyName
  case signatureProperty (*) :
    outResult = "<< ligne 243 >>"
  case versionProperty (*) :
    outResult = "<< ligne 245 >>"
  case versionShouldChangeProperty (*) :
    outResult = "<< ligne 247 >>"
  case superProperty (* @lstring propertyName) :
    outResult = prefix + propertyName.string + "_property.addEBObserver"
  case superPropertyWithDefaultValue (* @lstring propertyName *) :
    outResult = prefix + propertyName.string + "_property.addEBObserver"
  end
}

#·······················································································································

func @observablePropertyAST.generateRemoveObserverCall() -> @string outResult {
  switch self
  case selfControllerProperty (@lstring controllerName @lstring propertyName) : 
    outResult = "self." + controllerName.string + "." + propertyName + "_property.removeEBObserver"
  case selfControllerSecondaryProperty (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = "self." + controllerName.string + "." + propertyName + "_property." + secondaryPropertyName + "_property.removeEBObserver"
  case selfControllerAllProperties (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = "self." + controllerName.string + "." + propertyName + "_property.removeEBObserverOf_" + secondaryPropertyName
  case selfControllerOneProperty (@lstring controllerName @lstring propertyName) : 
    outResult = "self." + controllerName.string + "." + propertyName + "_property.removeEBObserverOf_<<5>>"
  case selfWithoutProperty (*) :
    outResult = "" # Do not generate add observer
  case selfProperty (@lstring masterName) :
    outResult = "self." + masterName.string + "_property.removeEBObserver"
  case selfPropertyWithOption (@lstring masterName *) :
    outResult = "self." + masterName.string + "_property.removeEBObserver"
  case selfPropertyArray (@lstring masterName @lstring elementPropertyName) :
    outResult = "self." + masterName.string + "_property.removeEBObserverOf_" + elementPropertyName
  case selfPropertyObject (@lstring masterName @lstring elementPropertyName) :
    outResult = "self." + masterName.string + "_property." + elementPropertyName + "_property.removeEBObserver"
  case selfPropertyNone (@lstring masterName) :
    outResult = "self." + masterName.string + "_property.removeEBObserver"
  case prefsProperty (@lstring propertyName) :
    outResult = "preferences_"  + propertyName + "_property.removeEBObserver"
  case prefsPropertyWithOption (@lstring propertyName *) :
    outResult = "preferences_"  + propertyName + "_property.removeEBObserver"
  case rootProperty (@lstring masterName) :
    outResult = "self.rootObject." + masterName + "_property.removeEBObserver"
  case rootPropertyWithOption (@lstring masterName @lstring optionName) :
    outResult = "self.rootObject." + masterName + "_property." + optionName + "_property.removeEBObserver"
  case rootPropertyNone (@lstring masterName) :
    outResult = "self.rootObject." + masterName + "_property.removeEBObserver"
  case rootPropertyRelationship (@lstring relationshipName @lstring propertyName) :
    outResult = "self.rootObject." + relationshipName + "_property.removeEBObserverOf_" + propertyName
  case signatureProperty (*) :
    outResult = "<< ligne 270 >>"
  case versionProperty (*) :
    outResult = "<< ligne 272 >>"
  case versionShouldChangeProperty (*) :
    outResult = "<< ligne 274 >>"
  case superProperty (* @lstring propertyName) :
    outResult = "self." + propertyName.string + "_property.removeEBObserver"
  case superPropertyWithDefaultValue (* @lstring propertyName *) :
    outResult = "self." + propertyName.string + "_property.removeEBObserver"
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#    TRANSIENT ROUTINE GENERATION         
#-----------------------------------------------------------------------------------------------------------------------

class @transientRoutineGeneration : @abstractFileGeneration {
  public var @string mOwnerName
  public var @string mTransientName
  public var @typeKind mTransientType
  public var @string mGeneratedFunctionNamePrefix
  public var @transientDependencyListForGeneration mDependencyList
}

#·······················································································································

override proc @transientRoutineGeneration.generateCode(
    ?let @string inOutputDirectory
    ?toOne: @stringset unused inToOneEntities
    ?toMany: @stringset unused inToManyEntities
    ?generateClass_PreferencesArrayOf: @stringset unused inGenerateClass_PreferencesArrayOf
    ?generateClass_StandAloneArrayOf: @stringset unused inGenerateClass_StandAloneArrayOf
    ?generateClass_ProxyArrayOf: @stringset unused inGenerateClass_ProxyArrayOf
    ?generateClass_TransientArrayOfSuperOf: @stringset unused inGenerateClass_TransientArrayOfSuperOf
    ?generateClass_TransientArrayOf: @stringset unused inGenerateClass_TransientArrayOf
    ?generateClass_StoredArrayOf: @stringset unused inGenerateClass_StoredArrayOf
    ?!@stringset ioGeneratedFileSet) {
  if [self.mDependencyList length] > 0 then
    let s = [filewrapper transientManager.transientComputationFunctionFile
      !self.mOwnerName
      !self.mTransientName
      !self.mDependencyList
      !self.mTransientType
      !self.mGeneratedFunctionNamePrefix
    ]
    let header = "//" + @string.stringWithSequenceOfCharacters (!'—' !118) + "\n"
      "//  THIS FILE IS REGENERATED BY EASY BINDINGS, ONLY MODIFY IT WITHIN USER ZONES\n"
      "//" + @string.stringWithSequenceOfCharacters (!'—' !118) + "\n\n"
      + "import Cocoa\n\n"
      + "//" + @string.stringWithSequenceOfCharacters (!'—' !118) + "\n\n"
   let fileName = self.mGeneratedFunctionNamePrefix + "-" + self.mOwnerName + "-" + self.mTransientName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName:fileName
      !lineComment:"//"
      !header:header
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:"\n" # Defaut user zone2
      !generatedZone3:"}\n\n//" + @string.stringWithSequenceOfCharacters (!'—' !118) + "\n"
      !makeExecutable:false
    ]
  end
}

#·······················································································································

private filewrapper transientManager in "../generation-templates" {
}{
}{
  template transientComputationFunctionFile "transient-computation-function.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string TRANSIENT_NAME
    ?@transientDependencyListForGeneration DEPENDENCY_LIST
    ?@typeKind TRANSIENT_TYPE
    ?@string TRANSIENT_FUNCTION_NAME_PREFIX
}

#-----------------------------------------------------------------------------------------------------------------------
