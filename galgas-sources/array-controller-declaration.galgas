#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @arrayControllerBoundColumnListAST {
  @lstring mColumnName
  @lstring mColumnOutletTypeName
  @lstring mObservablePropertyName
  @bindingOptionList mBindingOptionList
}

#......................................................................................................................*

list @arrayControllerDeclarationListAST {
  @lstring mControllerName
  @lstring mRootRelationshipName
  @lstring mTableViewOutletName
  @lstringlist mFilterProperties
  @arrayControllerBoundColumnListAST mArrayControllerBoundColumnListAST
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #....................................................................................................................*
  
  rule <array_controller_declaration>
    ?!@arrayControllerDeclarationListAST ioArrayControllerDeclarationListAST
  {
    $arrayController$
    $identifier$ ?let @lstring controllerName
    $bind$
    $root$
    $.$
    $identifier$ ?let @lstring relationshipName
    $to$
    $identifier$ ?let @lstring tableViewOutletName
    @arrayControllerBoundColumnListAST arrayControllerBoundColumnListAST = {}
    @lstringlist filterProperties = {}
    select
    or
      $filter$
      repeat
        $identifier$ ?let @lstring filterProperty
        filterProperties += !filterProperty
      while
        $,$
      end
    end
    repeat
      $column$
      $literal_string$ ?let columnName
      $Identifier$ ?let columnOutletTypeName
      $identifier$ ?let observablePropertyName
      <binding_option_list> ?let bindingOptions
      arrayControllerBoundColumnListAST +=
        !columnName
        !columnOutletTypeName
        !observablePropertyName
        !bindingOptions
    while
    end
    $;$
    ioArrayControllerDeclarationListAST +=
      !controllerName
      !relationshipName
      !tableViewOutletName
      !filterProperties
      !arrayControllerBoundColumnListAST
  }

  #....................................................................................................................*

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC TYPE                                                                                                      *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

map @arrayControllerMap {
  insert insertKey error message "the '%K' array controller is already declared"
  search searchKey error message "the '%K' array controller is not declared"
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @arrayControllerDeclarationListAST buildArrayControllerMap
  !@arrayControllerMap outArrayControllerMap
{
  outArrayControllerMap = {}
  for () in self do
    [!?outArrayControllerMap insertKey !mControllerName]
  end
}

#......................................................................................................................*

method @arrayControllerDeclarationListAST arrayControllerSemanticAnalysis
  ?let @string inOwnerName
  ?let @observablePropertyMap inRootObservablePropertyMap
  ?let @string inRootEntityName
  ?let @semanticContext inSemanticContext
  ?let @decoratedOutletMap inOutletMap
  ?let @observablePropertyMap unused inObservablePropertyMap
  !@arrayControllerForGeneration outDocumentArrayControllerForGeneration
  ?!@arrayControllerForGeneration ioArrayControllerForGeneration
{
  outDocumentArrayControllerForGeneration = {}
  for () in self do
  #--- Check model is a tomany relationship
    [inRootObservablePropertyMap searchKey
      !mRootRelationshipName
      ?let @typeKind modelType
      ?let @propertyKind kind
      ?let @propertyMultiplicity multiplicity
      ?*
    ]
  #--- Check bound model type is an entity
    @string entityName
    switch modelType
    case boolType, colorType, dateType, doubleType, integerType, stringType :
      error mRootRelationshipName : "the model should be a tomany relationship" : entityName
    case enumType (* *) :
      error mRootRelationshipName : "the model should be a tomany relationship" : entityName
    case entityType (@string kEntityName) :
      entityName = kEntityName
    end
  #--- Check bound model is a collection
    if multiplicity != .collection then
      error mRootRelationshipName : "the bound property should be a collection"
    end
  #--- Check bound model is stored
    if kind == .transient then
      error mRootRelationshipName : "the bound property should be stored (not a transient)"
    end
  #--- Check outlet is a PMTableView
    [inOutletMap searchKey !mTableViewOutletName ?let outletTypeName]
    if outletTypeName != "PMTableView" then
      error mTableViewOutletName : "this outlet is not an instance of PMTableView"
    end
  #--- Check colum bound models (TODO : get also super entity observable property)
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      ![entityName nowhere]
      ?let @observablePropertyMap boundModelObservablePropertyMap
    ]
    @arrayControllerBoundColumnListForGeneration arrayControllerBoundColumnListForGeneration = {}
    for () in mArrayControllerBoundColumnListAST do
      [boundModelObservablePropertyMap searchKey
        !mObservablePropertyName
        ?let @typeKind columnType
        ?let @propertyKind unused columnKind
        ?let @propertyMultiplicity columnMultiplicity
        ?let @string unused inverseRelationshipName # "" if not a relationship
      ]
    #--- Check bound model type is a simple type
      switch columnType
      case boolType, colorType, dateType, doubleType, integerType, stringType : # Ok
      case enumType (* *) : # Ok
      case entityType (@string unused entityTypeName) :
        error [mObservablePropertyName location] : "the model should not be a relationship"
      end
      if columnMultiplicity == .collection then
        error mObservablePropertyName : "the bound column property should not be a collection"
      end
      arrayControllerBoundColumnListForGeneration +=
        !mColumnName.string
        !mColumnOutletTypeName.string
        !mObservablePropertyName.string
        !columnType
        !mBindingOptionList
    end
  #--- Check filter properties
    @arrayControllerFilterListForGeneration arrayControllerFilterListForGeneration = {}
    for (filterPropertyName) in mFilterProperties do
      [boundModelObservablePropertyMap searchKey
        !filterPropertyName
        ?let @typeKind filterType
        ?let @propertyKind unused columnKind
        ?let @propertyMultiplicity filterMultiplicity
        ?let @string unused inverseRelationshipName # "" if not a relationship
      ]
      if filterMultiplicity == .collection then
        error filterPropertyName : "the filter property should not be a collection"
      end
      arrayControllerFilterListForGeneration += !filterPropertyName.string !filterType
    end
  #---
    outDocumentArrayControllerForGeneration +=
      !inOwnerName
      !mControllerName.string
      !inRootEntityName
      !mRootRelationshipName.string
      ![modelType swiftTypeName]
      !mTableViewOutletName.string
      !arrayControllerFilterListForGeneration
      !arrayControllerBoundColumnListForGeneration
  end
  ioArrayControllerForGeneration += outDocumentArrayControllerForGeneration
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPES                                                                                                   *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @arrayControllerForGeneration {
  @string mOwnerName # document name, prefs name, ...
  @string mControllerName
  @string mRelationshipTypeName
  @string mToManyRelationshipName
  @string mElementTypeName
  @string mTableViewOutletName
  @arrayControllerFilterListForGeneration mArrayControllerFilterListForGeneration
  @arrayControllerBoundColumnListForGeneration mArrayControllerBoundColumnListForGeneration
}

#......................................................................................................................*

list @arrayControllerFilterListForGeneration {
  @string mFilterPropertyName
  @typeKind mFilterPropertyType
}

#......................................................................................................................*

list @arrayControllerBoundColumnListForGeneration {
  @string mColumnName
  @string mColumnOutletTypeName
  @string mObservablePropertyName
  @typeKind mPropertyType
  @bindingOptionList mBindingOptionList
}

#......................................................................................................................*

getter @typeKind transformerForTableViewAction ?let @string inSenderOutletName -> @string outResult {
  switch self
  case stringType : outResult = inSenderOutletName + ".stringValue"
  case integerType : outResult = inSenderOutletName + ".integerValue"
#--- TODO
  case boolType : outResult = "NSNumber (bool:" + inSenderOutletName + ")"
  case doubleType : outResult = "NSNumber (double:" + inSenderOutletName + ")"
  case colorType : outResult = inSenderOutletName + ".stringValue"
  case dateType : outResult = "NSDate"
  case enumType (* *) :
    outResult = "<unhandled enum for transformerForTableViewAction>"
  case entityType (*) :
    outResult = "<unhandled entity for transformerForTableViewAction>"
  end
}

#......................................................................................................................*

getter @typeKind formatterStringForFormatPrinting -> @string outResult {
  switch self
  case stringType : outResult = "%@"
  case integerType : outResult = "%d"
  case boolType : outResult = "%d"
  case doubleType : outResult = "%g"
  case colorType : outResult = "%@"
  case dateType : outResult = "%@"
  case enumType (* *) :
    outResult = "<internal error enum, formatterStringForFormatPrinting' >"
  case entityType (*) :
    outResult = "<internal error entity, formatterStringForFormatPrinting' >"
  end
}

#......................................................................................................................*

private filewrapper collectionControllerGenerationTemplate in "../generation-templates/collection-controller-templates" {
  "txt"
}{
}{
  template arrayControllerImplementationInSwift "array-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string ARRAY_CONTROLLER_NAME
    ?@string RELATIONSHIP_NAME
    ?@string RELATIONSHIP_TYPE_NAME
    ?@string ELEMENT_TYPE_NAME
    ?@string TABLE_VIEW_OUTLET_NAME
    ?@arrayControllerFilterListForGeneration FILTER_PROPERTIES
    ?@arrayControllerBoundColumnListForGeneration BOUND_COLUMNS
    ?@filewrapper FILE_WRAPPER

  template filterFunction "array-controller-filter-function.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string ARRAY_CONTROLLER_NAME
    ?@arrayControllerFilterListForGeneration FILTER_PROPERTIES
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION                                                                                                         *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generateArrayControllers
  ?let @arrayControllerForGeneration inArrayControllerListForGeneration
  ?let @string inOutputDirectory {
  for () in inArrayControllerListForGeneration do
    let s = [filewrapper collectionControllerGenerationTemplate.arrayControllerImplementationInSwift
      !mOwnerName
      !mControllerName
      !mToManyRelationshipName
      !mRelationshipTypeName
      !mElementTypeName
      !mTableViewOutletName
      !mArrayControllerFilterListForGeneration
      !mArrayControllerBoundColumnListForGeneration
      ![filewrapper collectionControllerGenerationTemplate]
    ]
    [@string generateFile
      !inOutputDirectory
      !"arrayController-" + mOwnerName + "-" +  mControllerName + ".swift"
      !s
    ]
    if [mArrayControllerFilterListForGeneration length] > 0 then
    let s = [filewrapper collectionControllerGenerationTemplate.filterFunction
      !mOwnerName
      !mControllerName
      !mArrayControllerFilterListForGeneration
    ]
    let fileName = "arrayControllerFilter-" + mOwnerName + "-" +  mControllerName + ".swift"
    [@string generateFileWithPattern
      !inOutputDirectory
      !fileName
      !"//"
      !"\n\n" # Defaut user zone1
      !s
      !"  return WHAT ?\n" # Defaut user zone2
      !"}\n\n//" + ["" stringByRightPadding !116 !'—'] + "*\n"
    ]
    end
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
