//----------------------------------------------------------------------------------------------------------------------
#   GRAMMAR                               
//----------------------------------------------------------------------------------------------------------------------

grammar easyBindings_grammar "SLR" {
  syntax easyBindings_syntax
  <start_symbol> (?!@astDeclarationStruct ioDeclarationAST, !@location outEndOfFile)
}

//----------------------------------------------------------------------------------------------------------------------
#   AST   
//----------------------------------------------------------------------------------------------------------------------

struct @astDeclarationStruct {
  public var @declarationListAST mUnifiedDeclarationList = []
  public var @outletClassBindingSpecificationList mBindingSpecificationListMap = []
  public var @autoLayoutViewClassBindingSpecificationList mAutoLayoutBindingSpecificationList = []
  public var @prefDeclaration mPreferences = @prefDeclaration ()
  public var @lstring mXcodeProject = "".nowhere ()
  public var @lstringlist mGraphvizList = []
  public var @astAutoLayoutViewFunctionMap mAutoLayoutVStackFunctionMap = []
  public var @astAutoLayoutViewFunctionMap mAutoLayoutHStackFunctionMap = []
  public var @enumerationFunctionListAST mEnumerationFunctionListAST = []
}

//----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
//----------------------------------------------------------------------------------------------------------------------

syntax easyBindings_syntax (easyBindings_lexique) {

  #·····················································································································
  
  rule <start_symbol>(
    ?!@astDeclarationStruct ioDeclarationAST,
    !@location outEndOfFile)
  {
    repeat
    while
      $include$
      $literal_string$ (?let @lstring filePath)
      $;$
      let sourceFile = @lstring (
        filePath.string.absolutePathFromPath (@string.stringWithSourceFilePath.stringByDeletingLastPathComponent ()),
        filePath.location
      )
      grammar easyBindings_grammar (!?ioDeclarationAST, ?*) in sourceFile
    while
      <autolayout_class_declaration> (!?ioDeclarationAST)
   while
      <graphviz_declaration> (!?ioDeclarationAST)
    while
      <enum_declaration> (!?ioDeclarationAST)
   while
      <enum_function_declaration> (!?ioDeclarationAST)
    while
      <entity_declaration> (!?ioDeclarationAST)
    while
      <class_declaration> ( !?ioDeclarationAST)
    while
      <preferences_declaration> (!?ioDeclarationAST)
    while
      <document_declaration> (!?ioDeclarationAST)
    while
      <outlet_class_declaration> (!?ioDeclarationAST)
    while
      <outlet_binding_specification> (!?ioDeclarationAST.mBindingSpecificationListMap)
    while
      <xcode_project> (!?ioDeclarationAST.mXcodeProject)
    while
      <main_xib> (!?ioDeclarationAST.mPreferences.mMainXibDescriptorList)
    end
    outEndOfFile = .here
  }

  #·····················································································································

}


//----------------------------------------------------------------------------------------------------------------------
