#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

struct @prefDeclaration {
  @outletDeclarationList mOutletDeclarationList
  @simpleStoredPropertyList mSimpleStoredAttributeList
  @lstringlist mActionDeclarationList
  @secondaryPropertyList mSecondaryPropertyList
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #....................................................................................................................*
  
  rule <preferences_declaration>
    ?let @bool unused inIsUserDefined
    ?!@astDeclarationStruct ioDeclarationAST
  {
    $prefs$
    ${$
    repeat
    while
      <action_declaration> !?ioDeclarationAST.mPreferences.mActionDeclarationList
    while
      <simple_stored_declaration> !?ioDeclarationAST.mPreferences.mSimpleStoredAttributeList
    while
      <outlet_declaration> !?ioDeclarationAST.mPreferences.mOutletDeclarationList
    while
      <transient_declaration> !?ioDeclarationAST.mPreferences.mSecondaryPropertyList
    end
    $}$
  }

  #....................................................................................................................*

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   TYPE INVENTORY                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @prefDeclaration typeInventory
  ?!@unifiedTypeMap ioUnifiedTypeMap
{
  [mSimpleStoredAttributeList typeInventory !?ioUnifiedTypeMap]
  for (item) in mSecondaryPropertyList do
    [item typeInventory
      !?ioUnifiedTypeMap
    ]
  end
}

#......................................................................................................................*

method @prefDeclaration solveSecondaryProperty
  ?!@semanticContext ioSemanticContext
  ?let @unifiedTypeMap unused inUnifiedTypeMap
  ?let @secondaryPropertyList inPropertiesToSolve
  !@secondaryPropertyList outUnsolvedProperties
{
  outUnsolvedProperties = {}
  var preferencesObservablePropertyMap = ioSemanticContext.mPreferencesObservablePropertyMap
  for (property) in inPropertiesToSolve do
    [property tryToSolveSecondaryProperty
      !?ioSemanticContext
      !{} # No root entity
      !?preferencesObservablePropertyMap
      !?outUnsolvedProperties
    ]
  end
  ioSemanticContext.mPreferencesObservablePropertyMap = preferencesObservablePropertyMap
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC TYPES                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

map @prefsObservablePropertyMap {
  @observablePropertyMap mObservablePropertyMap
  insert insertKey error message "the '%K' prefs is already declared in %L"
  search searchKey error message "there is no '%K' prefs"
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @prefDeclaration buildObservablePropertyMapsFromStoredProperties
  ?let @unifiedTypeMap inUnifiedTypeMap
  ?!@semanticContext ioSemanticContext
{
#--- Analyze simple stored properties
  [mSimpleStoredAttributeList buildObservablePropertyMap
    !inUnifiedTypeMap
    !?ioSemanticContext.mPreferencesObservablePropertyMap
  ]
}

#......................................................................................................................*

method @prefDeclaration semanticAnalysis
  ?let @semanticContext inSemanticContext
  ?!@structForGeneration ioGeneration
{
#--- Analyze simple stored properties
  [mSimpleStoredAttributeList simpleStoredPropertySemanticAnalysis
    !preferencesName ()
    !inSemanticContext.mUnifiedTypeMap
    !?ioGeneration.mValidationStubRoutineListForGeneration
    ?let @simpleStoredPropertyListForGeneration simpleStoredPropertyListForGeneration
  ]
#--- Analyze transients
  @transientDefinitionListForGeneration transientDefinitionListForGeneration = {}
  for (item) in mSecondaryPropertyList do
    [item secondaryPropertySemanticAnalysis
      !preferencesName ()
      !"" # No root entity name
      !{} # No root entity
      !inSemanticContext
      !inSemanticContext.mPreferencesObservablePropertyMap
      !?transientDefinitionListForGeneration
      !?* # No Array controller
      !?* # No selection controller
    ]
  end
  ioGeneration.mTransientListForGeneration += transientDefinitionListForGeneration
#--- Build action map
  buildActionMap (!mActionDeclarationList ?let @actionMap actionMap)
  for () in actionMap do
    ioGeneration.mActionListForGeneration +=
      !preferencesName ()
      !lkey.string
  end
#--- Analyze outlets and bindings
  analyzeOutlets (
    !{} # No root entity
    !inSemanticContext
    !mOutletDeclarationList
    !inSemanticContext.mPreferencesObservablePropertyMap
    !actionMap
    !?ioGeneration
    ?let @regularBindingsGenerationList regularBindingsGenerationList
    ?let @multipleBindingGenerationList multipleBindingGenerationList
    ?let @actionBindingListForGeneration actionBindingListForGeneration
    ?let @decoratedOutletMap outletMap
    ?let @tableViewBindingGenerationList unused tableViewBindingGenerationList
  )
#--- Code generation
  ioGeneration.mPreferencesForGeneration.mSimpleStoredPropertyListForGeneration = simpleStoredPropertyListForGeneration
  ioGeneration.mPreferencesForGeneration.mTransientDefinitionListForGeneration = transientDefinitionListForGeneration
  ioGeneration.mPreferencesForGeneration.mRegularBindingsGenerationList = regularBindingsGenerationList
  ioGeneration.mPreferencesForGeneration.mActionBindingListForGeneration = actionBindingListForGeneration
  ioGeneration.mPreferencesForGeneration.mMultipleBindingGenerationList = multipleBindingGenerationList
  ioGeneration.mPreferencesForGeneration.mOutletMap = outletMap
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPES                                                                                                   *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

getter @typeKind preferencesSwiftGetter -> @string outResult {
  switch self
  case boolType : outResult = "(value as! NSNumber).boolValue"
  case integerType : outResult = "(value as! NSNumber).integerValue"
  case doubleType : outResult = "à compléter preferencesSwiftGetter"
  case stringType : outResult = "value as! String"
  case colorType : outResult = "NSUnarchiver.unarchiveObjectWithData (value as! NSData) as! NSColor"
  case dateType : outResult = "value as! NSDate"
  case enumType (* *) : outResult = "à compléter preferencesSwiftGetter"
  case entityType (*) :
    outResult = "<invalid entity type>"
  end
}

#......................................................................................................................*

getter @typeKind preferencesSwiftSetter ?let @string inPropertyName -> @string outResult
{
  switch self
  case boolType : outResult = "NSNumber (bool:" + inPropertyName + ".prop.0)"
  case integerType : outResult = "NSNumber (integer:" + inPropertyName + ".prop.0)"
  case doubleType : outResult = "NSNumber (double:" + inPropertyName + ".prop.0)"
  case colorType : outResult = "NSArchiver.archivedDataWithRootObject (" + inPropertyName + ".prop.0)"
  case dateType, stringType : outResult = inPropertyName + ".prop.0"
  case enumType (* *) : outResult = "à compléter preferencesSwiftSetter"
  case entityType (*) :
    outResult = "<invalid entity type>"
  end
}

#......................................................................................................................*

struct @preferencesForGeneration {
  @simpleStoredPropertyListForGeneration mSimpleStoredPropertyListForGeneration
  @transientDefinitionListForGeneration mTransientDefinitionListForGeneration
  @regularBindingsGenerationList mRegularBindingsGenerationList
  @multipleBindingGenerationList mMultipleBindingGenerationList
  @actionBindingListForGeneration mActionBindingListForGeneration
  @decoratedOutletMap mOutletMap
}

#......................................................................................................................*

private filewrapper prefsGenerationTemplate in "../generation-templates" {
}{
}{
  template preferencesInSwift "preferences.swift.galgasTemplate"
    ?@simpleStoredPropertyListForGeneration SIMPLE_SIMPLE_PROPERTY_LIST_FOR_GENERATION
    ?@transientDefinitionListForGeneration TRANSIENT_LIST_FOR_IMPLEMENTATION
    ?@decoratedOutletMap OUTLET_MAP
    ?@regularBindingsGenerationList REGULAR_BINDINGS_GENERATION_LIST 
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION                                                                                                         *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generatePreferences
  ?let @preferencesForGeneration inPreferenceListForGeneration
  ?let @string inOutputDirectory
{
  let s = [filewrapper prefsGenerationTemplate.preferencesInSwift
    !inPreferenceListForGeneration.mSimpleStoredPropertyListForGeneration
    !inPreferenceListForGeneration.mTransientDefinitionListForGeneration
    !inPreferenceListForGeneration.mOutletMap
    !inPreferenceListForGeneration.mRegularBindingsGenerationList
  ]
  [@string generateFile
    !inOutputDirectory
    !preferencesName () + ".swift"
    !s
  ]
}

#......................................................................................................................*

func %once preferencesName -> @string outResult {
  outResult = "Preferences"
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
