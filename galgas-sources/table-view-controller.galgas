#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @tableViewControllerBoundColumnListAST {
  @lstring mColumnName
  @lstring mColumnOutletTypeName
  @tableViewColumnBindingAST mColumnBindingAST
  @lstring mSortPropertyName
}

#·······················································································································

struct @tableViewColumnBindingAST {
  @lstring mRunActionName # Empty string if no run binding
  @multipleBindingDescriptor mEnabledBindingDescriptor
  @multipleBindingDescriptor mHiddenBindingDescriptor
  @regularBindingList mRegularBindingList
}

#·······················································································································

list @tableViewControllerAttributListAST {
  @lstring mAttributeName
  @abstractDefaultValue mAttributeValue
}

#·······················································································································

class @tableViewControllerDeclarationAST : @abstractDeclarationAST {
  @lstring mControllerName
  @bool mIsRoot
  @lstring mRootEntityName
  @lstring mToManyPropertyName
  @lstringlist mFilterProperties
  @tableViewControllerBoundColumnListAST mTableViewControllerBoundColumnListAST
  @tableViewControllerAttributListAST mTableViewControllerAttributListAST
}

#·······················································································································

override method @tableViewControllerDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  let node = @lstring.new {!mClassName.string + " " + mControllerName !mControllerName.location}
  [!?ioGraph addNode !node !self]
  [!?ioGraph addEdge !node !mClassName]
  if not mIsRoot then
    [!?ioGraph addEdge !node !.new {!mClassName.string + " " + mToManyPropertyName !mToManyPropertyName.location}]
  elsif mRootEntityName.string != "" then
    [!?ioGraph addEdge !node !.new {!mRootEntityName.string + " " + mToManyPropertyName !mToManyPropertyName.location}]
  end
}

#·······················································································································

override getter @tableViewControllerDeclarationAST nodeKey -> @lstring {
  result = @lstring.new {!mClassName.string + " " + mControllerName !mControllerName.location}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <controller_declaration>
            ?let @lstring inCurrentEntity
            ?let @lstring inRootEntity
            ?!@astDeclarationStruct ioDeclarationAST
  {
    $tableViewController$
    $controllerName$ ?let @lstring controllerName
    @tableViewControllerAttributListAST arrayControllerAttributListAST = {}
    ${$
    repeat
      $identifier$ ?let @lstring attributeName
      $:$
      <explicit_value> ?let @abstractDefaultValue attributeValue
      arrayControllerAttributListAST += !attributeName !attributeValue
    while
      $,$
    end
    $}$
  #--- Model
    @bool isRoot
    select
      $root$
      isRoot = true
    or
      $self$
      isRoot = false
    end
    $.$
    $identifier$ ?let @lstring relationshipName
    @lstringlist filterProperties = {}
    select
    or
      $filter$
      repeat
        $identifier$ ?let @lstring filterProperty
        filterProperties += !filterProperty
      while
        $,$
      end
    end
    @tableViewControllerBoundColumnListAST arrayControllerBoundColumnListAST = {}
    select
      $;$
    or
      ${$
      repeat
      while
        $column$
        $literal_string$ ?let columnName
        @lstring sortPropertyName
        select
          sortPropertyName = ["" nowhere]
        or
          $sort$
          $identifier$ ?sortPropertyName
        end
        $Identifier$ ?let columnOutletTypeName
        <column_bindings> ?let @tableViewColumnBindingAST columnBindingAST
        arrayControllerBoundColumnListAST +=
          !columnName
          !columnOutletTypeName
          !columnBindingAST
          !sortPropertyName
      end
      $}$
    end
    ioDeclarationAST.mUnifiedDeclarationList += !@tableViewControllerDeclarationAST.new {
      !inCurrentEntity
      !controllerName
      !isRoot
      !inRootEntity
      !relationshipName
      !filterProperties
      !arrayControllerBoundColumnListAST
      !arrayControllerAttributListAST
    }
  }

#·······················································································································

  rule <column_bindings> !@tableViewColumnBindingAST outColumnBindingAST {
    @lstring runActionDescriptor = ["" nowhere]
    var enabledBindingDescriptor = @multipleBindingDescriptor.noBinding
    var hiddenBindingDescriptor = @multipleBindingDescriptor.noBinding
    @regularBindingList regularBindingList = {}
    repeat
    while
      $run$
      if runActionDescriptor.string != "" then
        error .here : "duplicated $run binding"
      end
      $identifier$ ?runActionDescriptor
  #--- Enabled
    while
      $enabled$
      if enabledBindingDescriptor != .noBinding then
        error .here : "duplicated $enabled binding"
      end
      <booleanMultipleBindingExpression> ?let @abstractBooleanMultipleBindingExpressionAST bindingExpression
      enabledBindingDescriptor = .binding {!expression:bindingExpression}
  #--- Hidden
    while
      $hidden$
      if hiddenBindingDescriptor != .noBinding then
        error .here : "duplicated $hidden binding"
      end
      <booleanMultipleBindingExpression> ?let @abstractBooleanMultipleBindingExpressionAST bindingExpression
      hiddenBindingDescriptor = .binding {!expression:bindingExpression}
  #--- Regular binding
    while
      $bindingName$ ?let @lstring bindingName
      @observablePropertyList observablePropertyList = {}
      repeat
        <observable_property> ?let @observablePropertyAST observableProperty
        observablePropertyList += !observableProperty
      while
        $,$
      end
      <binding_option_list> ?let bindingOptions
      regularBindingList +=
        !bindingName
        !observablePropertyList
        !bindingOptions
    end
  #---
    outColumnBindingAST = @tableViewColumnBindingAST.new {
      !runActionDescriptor
      !enabledBindingDescriptor
      !hiddenBindingDescriptor
      !regularBindingList
    }
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @tableViewControllerDeclarationAST firstAnalysisPhase 
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct unused ioGeneration
{
  with mClassName in !?ioSemanticContext.mClassMap 
    error message searchKey
  do
    let @actionMap controllerActions = {!["add" nowhere], !["remove" nowhere] }
    if mIsRoot then
      [ioSemanticContext.mClassMap searchKey !mRootEntityName ?* ?let rootProperties ?2*]
      [rootProperties searchKey !mToManyPropertyName ?let classKind ?2*]
      switch classKind
      case property (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      case toMany (typeName * graphic *) :
        let kind = @propertyKind.arrayController {
          !typeName: typeName
          !graphic: graphic
        }
        [!?mPropertyMap insertKey !mControllerName !kind !controllerActions !false]
    #--- Code Generation
      mPropertyGenerationList += !@tableViewControllerPropertyGeneration.new {
        !mControllerName.string
        !"rootObject." + mToManyPropertyName
        !mClassName
      }
      case toOne (4*) :
        error mRootEntityName : "a toMany relationship is required here"
      case arrayController (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      case selectionController (*) :
        error mRootEntityName : "a toMany relationship is required here"
      case objectController (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      end
    else
      [mPropertyMap searchKey !mToManyPropertyName ?let classKind ?2*]
      switch classKind
      case property (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      case toMany (typeName * graphic *) :
        let kind = @propertyKind.arrayController {
          !typeName: typeName
          !graphic: graphic
        }
        [!?mPropertyMap insertKey !mControllerName !kind !controllerActions !false]
    #--- Code Generation
      mPropertyGenerationList += !@tableViewControllerPropertyGeneration.new {
        !mControllerName.string
        !mToManyPropertyName
        !mClassName
      }
      case toOne (4*) :
        error mRootEntityName : "a toMany relationship is required here"
      case arrayController (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      case selectionController (*) :
        error mRootEntityName : "a toMany relationship is required here"
      case objectController (2*) :
        error mRootEntityName : "a toMany relationship is required here"
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SECOND ANALYSIS PHASE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @tableViewControllerDeclarationAST thirdAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
#------------------------------------------------------------ Check array controller attributes
  let attributes = @stringset {!"allowsEmptySelection", !"allowsMultipleSelection"}
  let allowedTypes = @typeKindList {!.boolType}
  @stringset definedAttributes = {}
  @2stringlist attributeListForGeneration = {}
  for (name value) in mTableViewControllerAttributListAST do
    if not [attributes hasKey !name.string] then
      error name : "unknown attribute"
    elsif [definedAttributes hasKey !name.string] then
      error name : "attribute multiply defined"
    else
      definedAttributes += !name.string
    end
    [value analyzeDefaultValueType !allowedTypes ?let valueAsString]
    attributeListForGeneration += !name.string !valueAsString
  end
  for s in attributes do
    if not [definedAttributes hasKey !s] then
      error mControllerName : "attribute '" + s + "' not defined"
    end
  end
#------
  @propertyMap boundModelPropertyMap
  if mIsRoot then
    [ioSemanticContext.mClassMap searchKey
       !mRootEntityName
       ?*
       ?boundModelPropertyMap 
       ?2*
     ]
  else
    [ioSemanticContext.mClassMap searchKey
       !mClassName
       ?*
       ?boundModelPropertyMap 
       ?2*
     ]  
  end
  [boundModelPropertyMap searchKey
     !mToManyPropertyName
     ?let @propertyKind boundModelClassKind
     ?2*
  ]
#------------------------------------------------------------ Check colum bound models
  @actionMap actionMap
  @propertyMap propertyMap
  @tableViewControllerModelKind arrayControllerModelKind
  @bool graphic
  @string elementTypeName
  switch boundModelClassKind
  case property (2*) :
    error mToManyPropertyName
      : "a toMany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case toMany (typeName accessibility isGraphic *) :
    elementTypeName = typeName
    graphic = isGraphic
    arrayControllerModelKind = if [accessibility isTransient]
      then .transientArray
      else .entityArray
    end
    [ioSemanticContext.mClassMap searchKey !typeName ?* ?propertyMap ?actionMap ?*]
  case toOne (4*) :
    error mToManyPropertyName
      : "a toMany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case arrayController (2*) :
    error mToManyPropertyName
      : "a toMany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case objectController (2*) :
    error mToManyPropertyName
      : "a toMany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case selectionController (*) :
    error mToManyPropertyName
      : "a toMany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  end
  @tableViewControllerBoundColumnListForGeneration arrayControllerBoundColumnListForGeneration = {}
  @tableViewControllerSortedColumnListForGeneration arrayControllerSortedColumnListForGeneration = {}
  for () in mTableViewControllerBoundColumnListAST do
  #-------- Run action ? Check action is declared
    if mColumnBindingAST.mRunActionName.string != "" then
      [actionMap searchKey !mColumnBindingAST.mRunActionName]
    end
  #-------- Simple bindings
    @regularBindingsGenerationList regularBindingsGenerationList = {}
    analyzeRegularBinding (
      !{} # No root observable property map
      !ioSemanticContext
      !propertyMap
      !mColumnOutletTypeName
      !"<<not used>>"
      !mColumnBindingAST.mRegularBindingList
      !"object"
      !?regularBindingsGenerationList
    )
  #-------- Sort property
    if mSortPropertyName.string != "" then
      [propertyMap searchKey
        !mSortPropertyName
        ?let sortPropertyKind
        ?2*
      ]
      if not [sortPropertyKind isComparable] then
        error mSortPropertyName : "this property is not comparable"
      end 
      arrayControllerSortedColumnListForGeneration +=    
        !mColumnName.string
        !sortPropertyKind
        !mSortPropertyName.string
    end
  #-------- Binding generation
    arrayControllerBoundColumnListForGeneration +=
      !mColumnName.string
      !mColumnOutletTypeName.string
      !mColumnBindingAST.mRunActionName.string
      !regularBindingsGenerationList
  #--- Outlet class is needed ?
    [ioSemanticContext.mOutletClassMap searchKey !mColumnOutletTypeName ?6* ?let outletClassIsUserDefined]
    if not outletClassIsUserDefined then
      ioGeneration.mNeededOutletClasses += !mColumnOutletTypeName.string
    end
  end
#------------------------------------------------------------ Check filter properties
  @tableViewControllerFilterListForGeneration arrayControllerFilterListForGeneration = {}
  for (filterPropertyName) in mFilterProperties do
    [propertyMap searchKey
      !filterPropertyName
      ?let @propertyKind columnKind
      ?2*
    ]
    arrayControllerFilterListForGeneration += !filterPropertyName.string !columnKind
  end
  let modelString = if mIsRoot
    then "self.rootObject." + mToManyPropertyName + "_property"
    else "self." + mToManyPropertyName + "_property"
  end
#---
  ioGeneration.mFileGenerationList += !@tableViewControllerGeneration.new {
    !mClassName
    !mControllerName
    !arrayControllerFilterListForGeneration
    !modelString
    !arrayControllerBoundColumnListForGeneration
    !arrayControllerSortedColumnListForGeneration
    !"ReadWriteArrayOf_" + elementTypeName # [kind swiftTypeName]
    !arrayControllerModelKind # boundModelClassKind
    !elementTypeName # [kind swiftTypeName]
    !graphic
    !attributeListForGeneration
  }
}


#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @tableViewControllerPropertyGeneration : @propertyGeneration {
  @string mModelString
  @string mOwnerName
}

#·······················································································································

override getter @tableViewControllerPropertyGeneration propertyDeclarationCode ?let @stringset unused inOverriddenTransients -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n"
  result += "  //   Array controller: " + mPropertyName + "\n"
  result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
  result += "  var " + mPropertyName + " = Controller_" + mOwnerName + "_" + mPropertyName + " ()\n\n"
}

#·······················································································································

override getter @tableViewControllerPropertyGeneration configurationCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  result += "    self." + mPropertyName + ".bind_model (self." + mModelString + "_property)\n"
}

#·······················································································································

override getter @tableViewControllerPropertyGeneration populateExplorerWindowCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  result += "    self." + mPropertyName + ".addExplorer (name: \"" + mPropertyName + "\", y:&y, view:view)\n"
}

#·······················································································································

override getter @tableViewControllerPropertyGeneration terminationCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  result += "    self." + mPropertyName + ".unbind_model ()\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @tableViewControllerModelKind {
  case entityArray

  case transientArray
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @tableViewControllerGeneration : @abstractFileGeneration {
  @string mOwnerName # document name, prefs name, ...
  @lstring mControllerName
  @tableViewControllerFilterListForGeneration mTableViewControllerFilterListForGeneration
  @string mModelString
  @tableViewControllerBoundColumnListForGeneration mTableViewControllerBoundColumnListForGeneration
  @tableViewControllerSortedColumnListForGeneration mTableViewControllerSortedColumnListForGeneration
  @string mModelTypeName
  @tableViewControllerModelKind mTableViewControllerModelKind
  @string mElementTypeName
  @bool mElementTypeIsGraphic
  @2stringlist mAttributeListForGeneration
}

#·······················································································································

list @tableViewControllerFilterListForGeneration {
  @string mFilterPropertyName
  @propertyKind mFilterPropertyKind
}

#·······················································································································

list @tableViewControllerBoundColumnListForGeneration {
  @string mColumnName
  @string mColumnOutletTypeName
  @string mRunAction # Empty string if no action
  @regularBindingsGenerationList mRegularBindingsGenerationList
}

#·······················································································································

list @tableViewControllerSortedColumnListForGeneration {
  @string mColumnName
  @propertyKind mSortPropertyKind
  @string mObservablePropertyForSorting
}

#·······················································································································

getter @tableViewControllerFilterListForGeneration filterCode ?let @string inFilterFunction -> @string outResult {
  var filterProperties = self
  [!?filterProperties popFirst ?let firstFilterProperty ?*]
  @string indent = "            "
  outResult  = indent + "switch object." + firstFilterProperty + "_property_selection {\n"
  outResult += indent + "case .empty :\n"
  outResult += indent + "  return .empty\n"
  outResult += indent + "case .multiple :\n"
  outResult += indent + "  isMultiple = true\n"
  outResult += indent + "case .single (let v1) :\n"
  @uint idx = 2
  @string args = ""
  @string blockEnd = indent + "}\n"
  for () in filterProperties do
    indent += "  "
    outResult += indent + "switch object." + mFilterPropertyName + "_property_selection {\n"
    outResult += indent + "case .empty :\n"
    outResult += indent + "  return .empty\n"
    outResult += indent + "case .multiple :\n"
    outResult += indent + "  isMultiple = true\n"
    outResult += indent + "case .single (let v" + idx + ") :\n"
    blockEnd = indent + "}\n" + blockEnd
    args += ", v" + idx
    idx ++
  end
  indent += "  "
  outResult += indent + "if " + inFilterFunction + " (v1" + args + ") {\n"
  outResult += indent + "  filteredArray.append (object)\n"
  outResult += indent + "}\n"
  outResult += blockEnd
}

#·······················································································································

private filewrapper tableViewControllerGenerationTemplate in "../generation-templates/controllers" {
}{
}{
  template tableViewControllerImplementationInSwift "table-view-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string TABLE_VIEW_CONTROLLER_NAME
    ?@tableViewControllerFilterListForGeneration FILTER_PROPERTIES
    ?@string MODEL_STRING
    ?@tableViewControllerModelKind MODEL_KIND
    ?@string MODEL_TYPE_NAME
    ?@tableViewControllerBoundColumnListForGeneration BOUND_COLUMNS
    ?@tableViewControllerSortedColumnListForGeneration SORTED_COLUMNS
    ?@string ELEMENT_TYPE_NAME
    ?@stringset OBSERVED_PROPERTIES_FOR_SORTING_AND_FILERING
    ?@2stringlist ATTRIBUTE_VALUES

  template filterFunction "table-view-controller-filter-function.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string TABLE_VIEW_CONTROLLER_NAME
    ?@tableViewControllerFilterListForGeneration FILTER_PROPERTIES
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @tableViewControllerGeneration generateCode
    ?let @string inOutputDirectory
    ?!@stringset ioGeneratedFileSet
{
#--- Build observed property set (for sorting and filtering)
  @stringset observedProperties = {}
  for () in mTableViewControllerSortedColumnListForGeneration do
    observedProperties += !mObservablePropertyForSorting
  end
  for () in mTableViewControllerFilterListForGeneration do
    observedProperties += !mFilterPropertyName
  end
  let s = [filewrapper tableViewControllerGenerationTemplate.tableViewControllerImplementationInSwift
    !mOwnerName
    !mControllerName
    !mTableViewControllerFilterListForGeneration
    !mModelString
    !mTableViewControllerModelKind
    !mModelTypeName
    !mTableViewControllerBoundColumnListForGeneration
    !mTableViewControllerSortedColumnListForGeneration
    !mElementTypeName
    !observedProperties
    !mAttributeListForGeneration
  ]
  [@string generateFile
    !inOutputDirectory
    !"controller-table-view-" + mOwnerName + "-" +  mControllerName + ".swift"
    !s
  ]
  if [mTableViewControllerFilterListForGeneration length] > 0 then
    let s = [filewrapper tableViewControllerGenerationTemplate.filterFunction
      !mOwnerName
      !mControllerName
      !mTableViewControllerFilterListForGeneration
    ]
    let fileName = "arrayControllerFilter-" + mOwnerName + "-" +  mControllerName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName:fileName
      !lineComment:"//"
      !header:""
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:"  return WHAT ?\n" # Defaut user zone2
      !generatedZone3:"}\n\n//" + ["" stringByRightPadding !117 !'—'] + "\n"
      !makeExecutable:false
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
