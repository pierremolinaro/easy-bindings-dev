#-----------------------------------------------------------------------------------------------------------------------
#   AST   
#-----------------------------------------------------------------------------------------------------------------------

list @simpleStoredPropertyList {
  @lstring mPropertyTypeName
  @lstring mPropertyName
  @abstractDefaultValue mDefaultValue
  @bool mNeedsValidation
}

#·······················································································································

refclass @atomicPropertyDeclarationAST : @abstractDeclarationAST {
  @lstring mPropertyTypeName
  @lstring mPropertyName
  @abstractDefaultValue mDefaultValue
  @bool mNeedsValidation
}

#·······················································································································

override method @atomicPropertyDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  let node = @lstring.new {!self.mClassName.string + " " + self.mPropertyName !self.mPropertyName.location}
  [!?ioGraph addNode !node !self]
  [!?ioGraph addEdge !node !self.mClassName]
  [!?ioGraph addEdge !node !self.mPropertyTypeName]
  [self.mDefaultValue enterDefaultValuePrecedence !node !?ioGraph]
}

#·······················································································································

override getter @atomicPropertyDeclarationAST nodeKey -> @lstring {
  result = @lstring.new {!self.mClassName.string + " " + self.mPropertyName !self.mPropertyName.location}
}

#-----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
#-----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <simple_stored_declaration>
    ?let @lstring inClassName
    ?!@simpleStoredPropertyList ioAttributeList
    ?!@stringset ioSignatureList
    ?!@astDeclarationStruct ioDeclarationAST
  {
    $property$
    @bool usedForSignature = false
    @bool needsValidation = false
    repeat
    while
      $@attribute$ ?let attributeName
      if attributeName.string == "signature" then
        if needsValidation then
          error attributeName : "duplicated attribute" fixit { remove }
        end
        usedForSignature = true
      elsif attributeName.string == "validation" then
        if needsValidation then
          error attributeName : "duplicated attribute" fixit { remove }
        end
        needsValidation = true
      else
        error attributeName : "unknow attribute" fixit { replace "@signature" replace "@validation" }
      end
    end
    $Identifier$ ?var @lstring attributeTypeName
    $identifier$ ?var @lstring attributeName
    if usedForSignature then
      ioSignatureList += !attributeName.string
    end
    $default$
    <explicit_value> ?var @abstractDefaultValue defaultValue
    $;$
    ioDeclarationAST.mUnifiedDeclarationList += !@atomicPropertyDeclarationAST.new{
      !inClassName
      !attributeTypeName
      !attributeName
      !defaultValue
      !needsValidation

    }
    ioAttributeList +=
      !attributeTypeName
      !attributeName
      !defaultValue
      !needsValidation
  }

  #·····················································································································

}

#-----------------------------------------------------------------------------------------------------------------------
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#-----------------------------------------------------------------------------------------------------------------------

override method @atomicPropertyDeclarationAST firstAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
  with self.mClassName in !?ioSemanticContext.mClassMap 
    error message searchKey
  do
    [ioSemanticContext.mClassMap searchKey !self.mPropertyTypeName ?let classKind ?3*]
    [ioSemanticContext.mClassMap searchKey
      !.new {!preferencesName () !.nowhere}
      ?*
      ?let preferencesPropertyMap
      ?2*
    ]
    switch classKind
    case prefs :
      error self.mPropertyTypeName : "an atomic class is required here"
    case atomic (kind) :
      [!?mPropertyMap insertKey !self.mPropertyName !.property {!type: kind !accessibility: .stored} !{} !false]
    #--- Analyze default value
      [self.mDefaultValue analyzeDefaultValueType !{!kind} !preferencesPropertyMap ?let @string swiftDefaultValueAsString]
    #--- Code Generation
      mPropertyGenerationList += !@atomicPropertyGeneration.new {
        !self.mPropertyName.string
        !self.mNeedsValidation
        !kind
        !false
        !swiftDefaultValueAsString
        !self.mClassName.string == preferencesName ()
      }
    #--- Needs validation ?
      if self.mNeedsValidation then
        ioGeneration.mValidationStubRoutineListForGeneration +=
          !self.mClassName
          !self.mPropertyName.string
          ![kind swiftTypeName]
      end
    case document (*) :
      error self.mPropertyTypeName : "an atomic class is required here"
    case entity (3*) :
      error self.mPropertyTypeName : "an atomic class is required here"
    end
  end
}

#-----------------------------------------------------------------------------------------------------------------------
#   CODE GENERATION                       
#-----------------------------------------------------------------------------------------------------------------------

refclass @atomicPropertyGeneration : @propertyGeneration {
  @bool mNeedsValidation
  @typeKind mType
  @bool mIsProxy
  @string mDefaultValueInSwift
  @bool mInPreferences
}

#·······················································································································

list @atomicPropertyGenerationList {
  @atomicPropertyGeneration mProperty
}

#·······················································································································

override getter @atomicPropertyGeneration prefKeyDefinitionCode -> @string {
  result = "let Preferences_" + self.mPropertyName + " = \"Preferences:" + self.mPropertyName + "\"\n"
}

#·······················································································································

override getter @atomicPropertyGeneration declarationInSelectionControllerCode -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n"
  result += "  //   Selection observable property: " + self.mPropertyName + "\n"
  result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
  result += "  let " + self.mPropertyName + "_property = EBPropertyProxy_" + [self.mType swiftTypeName] + " ()\n"
}

#·······················································································································

override getter @atomicPropertyGeneration bindPropertyInSelectionController -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
  result += "  private final func bind_property_" + self.mPropertyName + " () {\n"
  result += "    self.selectedArray_property.addEBObserverOf_" + self.mPropertyName + " (self." + self.mPropertyName + "_property)\n"
  result += "    self." + self.mPropertyName + "_property.mReadModelFunction = { [weak self] in\n"
  result += "      if let model = self?.selectedArray_property {\n"
  result += "        switch model.selection {\n"
  result += "        case .empty :\n"
  result += "          return .empty\n"
  result += "        case .multiple :\n"
  result += "          return .multiple\n"
  result += "        case .single (let v) :\n"
  result += "          var s = Set <" + [self.mType swiftTypeName] + "> ()\n"
  result += "          var isMultipleSelection = false\n"
  result += "          for object in v {\n"
  result += "            switch object." + self.mPropertyName + "_property.selection {\n"
  result += "            case .empty :\n"
  result += "              return .empty\n"
  result += "            case .multiple :\n"
  result += "              isMultipleSelection = true\n"
  result += "            case .single (let vProp) :\n"
  result += "              s.insert (vProp)\n"
  result += "            }\n"
  result += "          }\n"
  result += "          if isMultipleSelection {\n"
  result += "            return .multiple\n"
  result += "          }else if s.count == 0 {\n"
  result += "            return .empty\n"
  result += "          }else if s.count == 1 {\n"
  result += "            return .single (s.first!)\n"
  result += "          }else{\n"
  result += "            return .multiple\n"
  result += "          }\n"
  result += "        }\n"
  result += "      }else{\n"
  result += "        return .empty\n"
  result += "      }\n"
  result += "    }\n"

  result += "    self." + self.mPropertyName + "_property.mWriteModelFunction = { [weak self] (inValue : " + [self.mType swiftTypeName] + ") in\n"
  result += "      if let model = self?.selectedArray_property {\n"
  result += "        switch model.selection {\n"
  result += "        case .empty, .multiple :\n"
  result += "          break\n"
  result += "        case .single (let v) :\n"
  result += "          for object in v {\n"
  result += "            object." + self.mPropertyName + "_property.setProp (inValue)\n"
  result += "          }\n"
  result += "        }\n"
  result += "      }\n"
  result += "    }\n"
  result += "    self." + self.mPropertyName + "_property.mValidateAndWriteModelFunction = { [weak self] (candidateValue : " + [self.mType swiftTypeName] + ", windowForSheet : NSWindow?) in\n"
  result += "      if let model = self?.selectedArray_property {\n"
  result += "        switch model.selection {\n"
  result += "        case .empty, .multiple :\n"
  result += "          return false\n"
  result += "        case .single (let v) :\n"
  result += "          for object in v {\n"
  result += "            let result = object." + self.mPropertyName + "_property.validateAndSetProp (candidateValue, windowForSheet:windowForSheet)\n"
  result += "            if !result {\n"
  result += "              return false\n"
  result += "            }\n"
  result += "          }\n"
  result += "          return true\n"
  result += "        }\n"
  result += "      }else{\n"
  result += "        return false\n"
  result += "      }\n"
  result += "    }\n"
  result += "  }\n"
}

#·······················································································································

override getter @atomicPropertyGeneration propertyDeclarationCode
   ?let @bool unused inPreferences
   ?let @stringset unused inOverriddenTransients -> @string
{
  if self.mInPreferences then
    result  = "//" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n"
    result += "//   Atomic property: " + self.mPropertyName + "\n"
    result += "//" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  let preferences_" + self.mPropertyName + "_property = EBPreferencesProperty_" + [self.mType swiftTypeName]
    result += " (defaultValue: " + self.mDefaultValueInSwift + ", prefKey: Preferences_" + self.mPropertyName  + ")\n\n"
    result += "//" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  var preferences_" + self.mPropertyName + " : " + [self.mType swiftTypeName] + " {\n"
    result += "    get { return preferences_" + self.mPropertyName + "_property.propval }\n"
    result += "    set { preferences_" + self.mPropertyName + "_property.setProp (newValue) }\n"
    result += "  }\n\n"
  else
    result  = "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n"
    result += "  //   Atomic property: " + self.mPropertyName + "\n"
    result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  final let " + self.mPropertyName + "_property : EBStoredProperty_" + [self.mType swiftTypeName] + "\n\n"
    result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  final func reset_" + self.mPropertyName + "_toDefaultValue () {\n"
    result += "    self." + self.mPropertyName + " = " + self.mDefaultValueInSwift + "\n"
    result += "  }\n\n"
    result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  final var " + self.mPropertyName + " : " + [self.mType swiftTypeName] + " {\n"
    result += "    get { return self." + self.mPropertyName + "_property.propval }\n"
    result += "    set { self." + self.mPropertyName + "_property.setProp (newValue) }\n"
    result += "  }\n\n"
  end
  if self.mNeedsValidation then
    result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
    result += "  final func " + self.mPropertyName + "_validateAndSetProp (_ inCandidateValue : " + [self.mType swiftTypeName] + ", windowForSheet inWindow : NSWindow?) -> Bool {\n"
    result += "    return self." + self.mPropertyName + "_property.validateAndSetProp (inCandidateValue, windowForSheet: inWindow)\n"
    result += "  }\n\n"
  end
}

#·······················································································································

override getter @atomicPropertyGeneration initCode -> @string {
  result  = "    self." + self.mPropertyName + "_property = EBStoredProperty_" + [self.mType swiftTypeName]
  result += " (defaultValue: " + self.mDefaultValueInSwift + ", undoManager: ebUndoManager)\n"
}

#·······················································································································

override getter @atomicPropertyGeneration configurationCode ?let @bool unused inPreferences -> @string {
  result = ""
  if self.mNeedsValidation then
    result += "  //--- Atomic property: " + self.mPropertyName + "\n"
    result += "    self." + self.mPropertyName + "_property.validationFunction = { [weak self] in\n"
    result += "      return self?.validate_" + self.mPropertyName + " (currentValue: $0, proposedValue: $1) ?? .rejectWithBeep\n"
    result += "    }\n"
  end
}

#·······················································································································

override getter @atomicPropertyGeneration setupAtomicPropertyFromDictionaryCode -> @string {
  result  = "  //--- Atomic property: " + self.mPropertyName + "\n"
  result += "    self." + self.mPropertyName + "_property.readFrom (dictionary: inDictionary, forKey: \"" + self.mPropertyName + "\")\n"
}

#·······················································································································

override getter @atomicPropertyGeneration saveIntoDictionaryCode -> @string {
  result  = "    //--- Atomic property: " + self.mPropertyName + "\n"
  result += "      self." + self.mPropertyName + "_property.storeIn (dictionary: ioDictionary, forKey: \"" + self.mPropertyName + "\")\n"
}

#·······················································································································

override getter @atomicPropertyGeneration clearObjectExplorerCode -> @string {
  result  = "  //--- Atomic property: " + self.mPropertyName + "\n"
  result += "    self." + self.mPropertyName + "_property.mObserverExplorer = nil\n"
  result += "    self." + self.mPropertyName + "_property.mValueExplorer = nil\n"
}

#-----------------------------------------------------------------------------------------------------------------------
