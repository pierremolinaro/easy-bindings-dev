#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

class @customObjectControllerDeclaration : @abstractSecondaryProperty {
  @lstring mCustomObjectControllerName
  @lstring mClassNameForSwift
  @lstring mPropertyEntityName
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <array_controller_declaration>
    ?!@secondaryPropertyList ioSecondaryPropertyList
  {
    $customObjectController$
    $identifier$ ?let @lstring customObjectControllerName
    $classForSwift$
    $Identifier$ ?let @lstring classNameForSwift
    $:$
    $Identifier$ ?let @lstring propertyClassName
    $;$
    ioSecondaryPropertyList += !@customObjectControllerDeclaration.new {
      !customObjectControllerName
      !classNameForSwift
      !propertyClassName
    }
  }

  #·····················································································································

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

override method @customObjectControllerDeclaration typeInventory
  ?!@unifiedTypeMap unused ioUnifiedTypeMap
{
}

#·······················································································································

override method @customObjectControllerDeclaration tryToSolveSecondaryProperty
  ?!@semanticContext ioSemanticContext
  ?let @observablePropertyMap unused inRootObservableProperties
  ?!@observablePropertyMap ioObservableProperties
  ?!@secondaryPropertyList ioUnsolvedProperties
{
  if [ioSemanticContext.mUnifiedTypeMap hasKey !mPropertyEntityName.string] then
  #--- Get model controller
    [ioSemanticContext.mUnifiedTypeMap searchKey
      !mPropertyEntityName
      ?let kind
      ?* # Action map
    ]
  #--- Check type
    @string entityName
    switch kind
    case stringType, boolType, dateType, doubleType, integerType, fontType :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case enumType (* *) :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case classType (*) :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case atomicClassType (* *) :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case propertyClassType (**) :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case transientPropertyClassType (*) :
      error mPropertyEntityName : "the controller model should be an entity" : entityName
    case entityType (kEntityName) :
      entityName = kEntityName
    end
  #--- Store selection controller
    [!?ioObservableProperties insertKey
      !mCustomObjectControllerName
      !kind
      !.selectionController
      !.single
      !"" # No inverse relationship
      !{} # No action
    ]
  else
    ioUnsolvedProperties += !self
  end
}

#·······················································································································

override method @customObjectControllerDeclaration secondaryPropertySemanticAnalysis
  ?let @string inOwnerName
  ?let @string unused inRootEntityName
  ?let @observablePropertyMap unused inRootObservableProperties
  ?let @semanticContext inSemanticContext
  ?let @observablePropertyMap unused inObservableProperties
  ?!@transientDefinitionListForGeneration unused ioTransientDefinitionListForGeneration
  ?!@arrayControllerForGeneration unused ioArrayControllerForGeneration
  ?!@selectionControllerForGeneration unused ioSelectionControllerForGeneration
  ?!@customObjectControllerForGeneration ioCustomObjectControllerDeclaration
  ?!@stringset unused ioNeededOutletClasses
{
#--- Find entity observable properties
  [inSemanticContext.mEntityObservablePropertyMap searchKey
    !mPropertyEntityName
    ?let @observablePropertyMap selectionObservablePropertyMap
  ]
#--- Generation
  ioCustomObjectControllerDeclaration +=
    !inOwnerName
    !mCustomObjectControllerName.string
    !mClassNameForSwift.string
    !mPropertyEntityName.string
    !selectionObservablePropertyMap
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPES                                                                                                   *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @customObjectControllerForGeneration {
  @string mOwnerName # document name, prefs name, ...
  @string mCustomObjectControllerName
  @string mClassNameForSwift
  @string mEntityTypeName
  @observablePropertyMap mSelectionObservablePropertyMap
}

#·······················································································································

private filewrapper customObjectControllerGenerationTemplate in "../generation-templates/controllers" {
}{
}{
  template implementation "custom-object-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string CUSTOM_OBJECT_CONTROLLER_NAME
    ?@string ENTITY_TYPE_NAME
    ?@observablePropertyMap SELECTION_OBSERVABLE_PROPERTY_MAP
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION                                                                                                         *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generateCustomObjectControllers
  ?let @customObjectControllerForGeneration inArrayControllerListForGeneration
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  for () in inArrayControllerListForGeneration do
    let s = [filewrapper customObjectControllerGenerationTemplate.implementation
      !mOwnerName
      !mCustomObjectControllerName
      !mEntityTypeName
      !mSelectionObservablePropertyMap
    ]
    let fileName = "customObjectController-" + mOwnerName + "-" +  mCustomObjectControllerName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
