#   SEMANTIC TYPES                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

struct @structForGeneration {
  @transientDefinitionListForGeneration mTransientListForGeneration
  @actionListForGeneration mActionListForGeneration
  @preferencesForGeneration mPreferencesForGeneration
  @documentListForGeneration mDocumentListForGeneration
  @entityListForGeneration mEntityListForGeneration
  @classListForGeneration mClassListForGeneration
  @enumListForGeneration mEnumListForGeneration
  @stringset mNeededOutletClasses
  @arrayControllerForGeneration mAllArrayControllerForGeneration
  @validationStubRoutineListForGeneration mValidationStubRoutineListForGeneration
  @selectionControllerForGeneration mSelectionControllerListForGeneration
  @customObjectControllerForGeneration mCustomObjectControllerListForGeneration
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   CODE GENERATION                                                                                                    *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generateCode
  ?let @string inXcodeProjectString
  ?let @structForGeneration inGeneration
  ?let @string inSourceFile
{
  let outputDirectory = [inSourceFile stringByDeletingLastPathComponent]
  let projectName = [[inSourceFile lastPathComponent] stringByDeletingPathExtension]
  let fileOutputDirectory = outputDirectory + "/" + projectName
  @stringset generatedFileSet = {}
  generateTransients (
    !fileOutputDirectory
    !inGeneration.mTransientListForGeneration
    !?generatedFileSet
  )
  generateActions (
    !fileOutputDirectory
    !inGeneration.mActionListForGeneration
    !?generatedFileSet
  )
  generatePreferences (
    !inGeneration.mPreferencesForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateOutletClasses (
    !inGeneration.mNeededOutletClasses
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateClasses (
    !inGeneration.mClassListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateEntities (
    !inGeneration.mEntityListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateEnums (
    !inGeneration.mEnumListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateDocuments (
    !inGeneration.mDocumentListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateArrayControllers (
    !inGeneration.mAllArrayControllerForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateSelectionControllers (
    !inGeneration.mSelectionControllerListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateCustomObjectControllers (
    !inGeneration.mCustomObjectControllerListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateSwiftApplicationFiles (
    !fileOutputDirectory
    ![inGeneration.mEntityListForGeneration length] > 0
    !?generatedFileSet
  )
  generateValidationRoutineStubs (
    !fileOutputDirectory
    !inGeneration.mValidationStubRoutineListForGeneration
    !?generatedFileSet
  )
  generateStandardProperties (
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateXcodeProject (
    !outputDirectory
    !inXcodeProjectString
    !generatedFileSet
    !projectName
  )
  generateTestFile (
    !inXcodeProjectString
    !projectName
    !inGeneration.mPreferencesForGeneration.mMainXibDescriptorList
    !outputDirectory
  )
}

#......................................................................................................................*

filewrapper swift_sources in "../generation-templates/swift-sources" {
  "xib", "swift"
}{
}{
}

#......................................................................................................................*

private proc generateSwiftApplicationFiles
  ?let @string inOutputDirectory
  ?let @bool inHasEntities
  ?!@stringset ioGeneratedFileSet
{
  @stringlist swiftFiles = {
    !"main",
    !"EBAllocationDebug",
    !"easy-bindings-utilities",
    !"generic-bindings",
    !"application"
  }
  if inHasEntities then
    swiftFiles += !"EBManagedObject"
    swiftFiles += !"EBUndoManager"
    swiftFiles += !"EBDataScanner"
  end
  @stringlist xibFiles = {!"EBAllocationDebug"}
  var fw = [filewrapper swift_sources]
  for (@string s) in swiftFiles do
    let contents = [fw textFileContentsAtPath !s + ".swift"]
    let fileName = s + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
  for (@string s) in xibFiles do
    let contents = [fw textFileContentsAtPath !s + ".xib"]
    let fileName = s + ".xib"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
}

#......................................................................................................................*

filewrapper standard_properties in "../generation-templates/standard-properties" {
}{
}{
  template stub "standard-properties-stub.swift.galgasTemplate"

  template scalarProperty "scalar-standard-property.swift.galgasTemplate"
    ?@string TYPE
    ?@string TO_NSNUMBER
    ?@string SIGNATURE

  template classProperty "class-standard-property.swift.galgasTemplate"
    ?@string CLASS_NAME
    ?@bool COMPARABLE
    ?@string EXPLORER_ACCESS
    ?@bool USE_ARCHIVER
}

#......................................................................................................................*

private proc generateStandardProperties
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  var contents = [filewrapper standard_properties.stub]
#--- Add Int type
  contents += [filewrapper standard_properties.scalarProperty !"Int"    !"integer" !"UInt64 (propval)"]
  contents += [filewrapper standard_properties.scalarProperty !"Bool"   !"bool"    !"propval ? 1 : 0"]
  contents += [filewrapper standard_properties.scalarProperty !"Double" !"double"  !"UInt64 (propval)"]
#--- Add Class type
  contents += [filewrapper standard_properties.classProperty !"String"  !true  !""             !false]
  contents += [filewrapper standard_properties.classProperty !"NSColor" !false !".description" !true ]
  contents += [filewrapper standard_properties.classProperty !"NSDate"  !true  !".description" !false]
  contents += [filewrapper standard_properties.classProperty !"NSFont"  !false !".description" !true ]
  contents += [filewrapper standard_properties.classProperty !"NSImage" !false !".description" !true ]
#--- Generate file
  let fileName = "standard-properties.swift"
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !contents
  ]
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
