#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @transientExternTypeList {
  @string mTypeName
  @bool mIsClass
}

#·······················································································································

abstract class @abstractFileGeneration {
}

#·······················································································································

abstract method @abstractFileGeneration generateCode
    ?let @string inOutputDirectory
    ?!@stringset ioGeneratedFileSet

#·······················································································································

list @fileGenerationList {
  @abstractFileGeneration mFileGeneration
}

#·······················································································································

struct @generationStruct {
  @validationStubRoutineListForGeneration mValidationStubRoutineListForGeneration
  @fileGenerationList mFileGenerationList
  @entityListForGeneratingEBManagedObjectContext mEntityListForGeneration
  @bool mGenerateEBManagedDocumentSwift
  @stringset mNeededOutletClasses
  @mainXibDescriptorList mMainXibDescriptorList


  @selectionControllerForGeneration mSelectionControllerListForGeneration
  @stringlist mPropertyClassList
  @transientExternTypeList mTransientPropertyTypeList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc generateCode
  ?let @string inXcodeProjectString
  ?let @generationStruct inGeneration
  ?let @string inSourceFile
{
  let outputDirectory = [inSourceFile stringByDeletingLastPathComponent]
  let projectName = [[inSourceFile lastPathComponent] stringByDeletingPathExtension]
  let fileOutputDirectory = outputDirectory + "/" + projectName
  @stringset generatedFileSet = {}
  for (fileGeneration) in inGeneration.mFileGenerationList do
    [fileGeneration generateCode !fileOutputDirectory !?generatedFileSet]
  end
  generateOutletClasses (
    !inGeneration.mNeededOutletClasses
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateEBManagedObjectContext (
    !inGeneration.mEntityListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  if inGeneration.mGenerateEBManagedDocumentSwift then
    generateEBManagedDocumentSwift (!fileOutputDirectory !?generatedFileSet)
  end
  generateSelectionControllers (
    !inGeneration.mSelectionControllerListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateSwiftApplicationFiles (
    !fileOutputDirectory
    ![inGeneration.mEntityListForGeneration length] > 0
    !?generatedFileSet
  )
  generateValidationRoutineStubs (
    !fileOutputDirectory
    !inGeneration.mValidationStubRoutineListForGeneration
    !?generatedFileSet
  )
  generateStandardProperties (
    !fileOutputDirectory
    !inGeneration.mPropertyClassList
    !inGeneration.mTransientPropertyTypeList
    !?generatedFileSet
  )
  generateXcodeProject (
    !outputDirectory
    !inXcodeProjectString
    !generatedFileSet
    !projectName
  )
  generateTestFile (
    !inXcodeProjectString
    !projectName
    !inGeneration.mMainXibDescriptorList
    !outputDirectory
  )
}

#·······················································································································

filewrapper swift_sources in "../generation-templates/swift-sources" {
  "xib", "swift"
}{
}{
}

#·······················································································································

private proc generateSwiftApplicationFiles
  ?let @string inOutputDirectory
  ?let @bool inHasEntities
  ?!@stringset ioGeneratedFileSet
{
  @stringlist swiftFiles = {
    !"main",
    !"EBAllocationDebug",
    !"EBUndoManager",
    !"easy-bindings-utilities",
    !"multiple-binding-controllers",
    !"Application",
    !"model-events",
    !"outlet-events"
  }
  if inHasEntities then
    swiftFiles += !"EBManagedObject"
    swiftFiles += !"EBDataScanner"
    swiftFiles += !"EBGraphicManagedObject"
    swiftFiles += !"buildPDFimage"
  #--- EBView
    swiftFiles += !"EBScrollView-dragging-destination"
    swiftFiles += !"EBScrollView-placards"
    swiftFiles += !"EBScrollView"
    swiftFiles += !"EBView-binding-arrowKeyMagnitude"
    swiftFiles += !"EBView-binding-backColor"
    swiftFiles += !"EBView-binding-gridCrossColor"
    swiftFiles += !"EBView-binding-gridDisplayFactor"
    swiftFiles += !"EBView-binding-gridLineColor"
    swiftFiles += !"EBView-binding-gridStep"
    swiftFiles += !"EBView-binding-gridStyle"
    swiftFiles += !"EBView-binding-horizontalFlip"
    swiftFiles += !"EBView-binding-mouseGrid"
    swiftFiles += !"EBView-binding-overObjectsDisplay"
    swiftFiles += !"EBView-binding-shiftArrowKeyMagnitude"
    swiftFiles += !"EBView-binding-underObjectsDisplay"
    swiftFiles += !"EBView-binding-verticalFlip"
    swiftFiles += !"EBView-binding-xPlacardUnit"
    swiftFiles += !"EBView-binding-yPlacardUnit"
    swiftFiles += !"EBView-binding-zoom"
    swiftFiles += !"EBView-dragging-source"
    swiftFiles += !"EBView-draw"
    swiftFiles += !"EBView-guide"
    swiftFiles += !"EBView-issue"
    swiftFiles += !"EBView-key-down"
    swiftFiles += !"EBView-magnify-and-zoom"
    swiftFiles += !"EBView-menu-actions"
    swiftFiles += !"EBView-mouse"
    swiftFiles += !"EBView-move-to-superview"
    swiftFiles += !"EBView-placards"
    swiftFiles += !"EBView-tracking-areas"
    swiftFiles += !"EBView"
    swiftFiles += !"EBViewControllerProtocol"
    swiftFiles += !"EBViewScaleProvider"
  #--- Extensions
    swiftFiles += !"extension-CGPath"
    swiftFiles += !"extension-CGPoint-NSPoint"
    swiftFiles += !"extension-CGRect-NSRect"
    swiftFiles += !"extension-CGSize-NSSize"
    swiftFiles += !"extension-Data"
    swiftFiles += !"extension-NSBezierPath"
    swiftFiles += !"extension-NSControl"
    swiftFiles += !"extension-NSObject"
    swiftFiles += !"extension-NSTextView"
    swiftFiles += !"extension-String"
    swiftFiles += !"extension-UInt32"
 #--- Shapes
    swiftFiles += !"EBFilledBezierPathShape"
    swiftFiles += !"EBKnobShape"
    swiftFiles += !"EBKnobTextShape"
    swiftFiles += !"EBShape"
    swiftFiles += !"EBStrokeBezierPathShape"
    swiftFiles += !"EBTextShape"
  end
  @stringlist xibFiles = {!"EBAllocationDebug"}
  let fw = [filewrapper swift_sources]
  for (@string s) in swiftFiles do
    let fileName = s + ".swift"
    let contents = [fw textFileContentsAtPath !fileName]
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
  for (@string s) in xibFiles do
    let contents = [fw textFileContentsAtPath !s + ".xib"]
    let fileName = s + ".xib"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
}

#·······················································································································

filewrapper standard_properties in "../generation-templates/standard-properties" {
}{
}{
  template stub "standard-properties-stub.swift.galgasTemplate"

  template scalarProperty "scalar-standard-property.swift.galgasTemplate"
    ?@string TYPE
    ?@bool TRANSIENT

  template classProperty "class-standard-property.swift.galgasTemplate"
    ?@string CLASS_NAME
    ?@bool COMPARABLE
    ?@bool TRANSIENT
}

#·······················································································································

private proc generateStandardProperties
  ?let @string inOutputDirectory
  ?let @stringlist inPropertyClassList
  ?let @transientExternTypeList inTransientPropertyExternTypeList
  ?!@stringset ioGeneratedFileSet
{
  var contents = [filewrapper standard_properties.stub]
#--- Add scalar property types
  contents += [filewrapper standard_properties.scalarProperty !"Int"    !false]
  contents += [filewrapper standard_properties.scalarProperty !"Bool"   !false]
  contents += [filewrapper standard_properties.scalarProperty !"Double" !false]
  contents += [filewrapper standard_properties.scalarProperty !"String" !false]
  contents += [filewrapper standard_properties.scalarProperty !"Data"   !false]
  contents += [filewrapper standard_properties.scalarProperty !"Date"   !false]
  contents += [filewrapper standard_properties.scalarProperty !"BezierPathArray" !false]
#--- Add class property types
  for (propertyClass) in inPropertyClassList do
    contents += [filewrapper standard_properties.classProperty !propertyClass !false !false]
  end
  for (propertyType isClass) in inTransientPropertyExternTypeList do
    if isClass then
      contents += [filewrapper standard_properties.classProperty !propertyType !false !true]
    else
      contents += [filewrapper standard_properties.scalarProperty !propertyType !true]
    end
  end
  contents += [filewrapper standard_properties.classProperty !"NSBezierPath" !false !false]
  contents += [filewrapper standard_properties.classProperty !"NSFont"  !false !false]
  contents += [filewrapper standard_properties.classProperty !"NSColor" !false !false]
#--- Generate file
  let fileName = "standard-properties.swift"
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !contents
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
