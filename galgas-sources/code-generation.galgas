#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @transientExternTypeList {
  @string mTypeName
  @bool mIsClass
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @generationStruct {
  @validationStubRoutineListForGeneration mValidationStubRoutineListForGeneration



  @transientDefinitionListForGeneration mTransientListForGeneration
  @actionListForGeneration mActionListForGeneration
  @preferencesForGeneration mPreferencesForGeneration
  @documentListForGeneration mDocumentListForGeneration
  @entityListForGeneration mEntityListForGeneration
  @classListForGeneration mClassListForGeneration
  @enumListForGeneration mEnumListForGeneration
  @stringset mNeededOutletClasses
  @arrayControllerForGeneration mArrayControllerListForGeneration
  @selectionControllerForGeneration mSelectionControllerListForGeneration
  @stringlist mPropertyClassList
  @transientExternTypeList mTransientPropertyTypeList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc generateCode
  ?let @string inXcodeProjectString
  ?let @generationStruct inGenerationEX
  ?let @string inSourceFile
{
  let outputDirectory = [inSourceFile stringByDeletingLastPathComponent]
  let projectName = [[inSourceFile lastPathComponent] stringByDeletingPathExtension]
  let fileOutputDirectory = outputDirectory + "/" + projectName
  @stringset generatedFileSet = {}
  generateTransients (
    !fileOutputDirectory
    !inGenerationEX.mTransientListForGeneration
    !?generatedFileSet
  )
  generateActions (
    !fileOutputDirectory
    !inGenerationEX.mActionListForGeneration
    !?generatedFileSet
  )
  generatePreferences (
    !inGenerationEX.mPreferencesForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateOutletClasses (
    !inGenerationEX.mNeededOutletClasses
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateClasses (
    !inGenerationEX.mClassListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateEntities (
    !inGenerationEX.mEntityListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateEnums (
    !inGenerationEX.mEnumListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateDocuments (
    !inGenerationEX.mDocumentListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateArrayControllers (
    !inGenerationEX.mArrayControllerListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateSelectionControllers (
    !inGenerationEX.mSelectionControllerListForGeneration
    !fileOutputDirectory
    !?generatedFileSet
  )
  generateSwiftApplicationFiles (
    !fileOutputDirectory
    ![inGenerationEX.mEntityListForGeneration length] > 0
    !?generatedFileSet
  )
  generateValidationRoutineStubs (
    !fileOutputDirectory
    !inGenerationEX.mValidationStubRoutineListForGeneration
    !?generatedFileSet
  )
  generateStandardProperties (
    !fileOutputDirectory
    !inGenerationEX.mPropertyClassList
    !inGenerationEX.mTransientPropertyTypeList
    !?generatedFileSet
  )
  generateXcodeProject (
    !outputDirectory
    !inXcodeProjectString
    !generatedFileSet
    !projectName
  )
  generateTestFile (
    !inXcodeProjectString
    !projectName
    !inGenerationEX.mPreferencesForGeneration.mMainXibDescriptorList
    !outputDirectory
  )
}

#·······················································································································

filewrapper swift_sources in "../generation-templates/swift-sources" {
  "xib", "swift"
}{
}{
}

#·······················································································································

private proc generateSwiftApplicationFiles
  ?let @string inOutputDirectory
  ?let @bool inHasEntities
  ?!@stringset ioGeneratedFileSet
{
  @stringlist swiftFiles = {
    !"main",
    !"EBAllocationDebug",
    !"easy-bindings-utilities",
    !"generic-bindings",
    !"application"
  }
  if inHasEntities then
    swiftFiles += !"EBManagedObject"
    swiftFiles += !"EBUndoManager"
    swiftFiles += !"EBDataScanner"
    swiftFiles += !"graphic-classes-and-utilities"
#    swiftFiles += !"EBView"
  end
  @stringlist xibFiles = {!"EBAllocationDebug"}
  var fw = [filewrapper swift_sources]
  for (@string s) in swiftFiles do
    let contents = [fw textFileContentsAtPath !s + ".swift"]
    let fileName = s + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
  for (@string s) in xibFiles do
    let contents = [fw textFileContentsAtPath !s + ".xib"]
    let fileName = s + ".xib"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !contents
    ]
  end
}

#·······················································································································

filewrapper standard_properties in "../generation-templates/standard-properties" {
}{
}{
  template stub "standard-properties-stub.swift.galgasTemplate"

  template scalarProperty "scalar-standard-property.swift.galgasTemplate"
    ?@string TYPE
    ?@bool TRANSIENT

  template classProperty "class-standard-property.swift.galgasTemplate"
    ?@string CLASS_NAME
    ?@bool COMPARABLE
    ?@bool TRANSIENT
}

#·······················································································································

private proc generateStandardProperties
  ?let @string inOutputDirectory
  ?let @stringlist inPropertyClassList
  ?let @transientExternTypeList inTransientPropertyExternTypeList
  ?!@stringset ioGeneratedFileSet
{
  var contents = [filewrapper standard_properties.stub]
#--- Add scalar property types
  contents += [filewrapper standard_properties.scalarProperty !"Int"    !false]
  contents += [filewrapper standard_properties.scalarProperty !"Bool"   !false]
  contents += [filewrapper standard_properties.scalarProperty !"Double" !false]
  contents += [filewrapper standard_properties.scalarProperty !"String" !false]
#--- Add class property types
  for (propertyClass) in inPropertyClassList do
    contents += [filewrapper standard_properties.classProperty !propertyClass !false !false]
  end
  for (propertyType isClass) in inTransientPropertyExternTypeList do
    if isClass then
      contents += [filewrapper standard_properties.classProperty !propertyType !false !true]
    else
      contents += [filewrapper standard_properties.scalarProperty !propertyType !true]
    end
  end
  contents += [filewrapper standard_properties.scalarProperty !"Date" !false]
  contents += [filewrapper standard_properties.classProperty !"NSFont"  !false !false]
  contents += [filewrapper standard_properties.classProperty !"NSColor" !false !false]
#--- Generate file
  let fileName = "standard-properties.swift"
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !contents
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
