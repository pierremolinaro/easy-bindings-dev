#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @outletClassDeclarationList {
  @bool mUserDefined
  @lstring mOutletClassName
  @bool mHasRunAction
  @bool mHasEnabled
  @bool mHandlesTableValueBinding
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #....................................................................................................................*
  
  rule <outlet_class_declaration>
    ?let @bool inIsUserDefined
    ?!@outletClassDeclarationList ioOutletClassDeclarationList
  {
    $outletClass$
    $Identifier$ ?let @lstring outletClassName
    @bool hasRunAction = false
    @bool hasEnabled = false
    @bool handlesTableViewBinding = false
    repeat
    while
      $tableValue$
      handlesTableViewBinding = true
    while
      $run$
      hasRunAction = true
    while
      $enabled$
      hasEnabled = true
    end
    $;$
    ioOutletClassDeclarationList +=
      !inIsUserDefined
      !outletClassName
      !hasRunAction
      !hasEnabled
      !handlesTableViewBinding
  }

  #....................................................................................................................*

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#  SEMANTIC TYPE                                                                                                       *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

map @outletClassMap {
  @bool mHandlesRunAction
  @bool mHandlesTableValueBinding
  @bool mHandleEnabledBinding
  @bool mUserDefined
  insert insertKey error message "the '%K' outlet class is already declared in %L"
  search searchKey error message "there is no '%K' outlet class"
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#  SEMANTICS                                                                                                           *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @outletClassDeclarationList buildOutletClassMap
  !@outletClassMap outOutletClassMap
{
  outOutletClassMap = {}
  for () in self do
    [!?outOutletClassMap insertKey
      !mOutletClassName
      !mHasRunAction
      !mHandlesTableValueBinding
      !mHasEnabled
      !mUserDefined
    ]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#  CODE GENERATION                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

filewrapper outletClassGeneration in "../generation-templates/outlet-classes" {
  "swift"
}{
}{
}

#----------------------------------------------------------------------------------------------------------------------*

proc generateOutletClasses
  ?let @stringset inNeededOutletClasses
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  let @filewrapper w = [filewrapper outletClassGeneration]
  for key in inNeededOutletClasses do
    let s = [w textFileContentsAtPath !"/" + key + ".swift"]
    let fileName = key + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile !inOutputDirectory !fileName !s]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
