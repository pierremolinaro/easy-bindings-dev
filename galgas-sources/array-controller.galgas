#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @arrayControllerBoundColumnListAST {
  @lstring mColumnName
  @lstring mColumnOutletTypeName
  @columnBindingAST mColumnBindingAST
  @lstring mSortPropertyName
}

#·······················································································································

struct @columnBindingAST {
  @lstring mRunActionName # Empty string if no run binding
  @multipleBindingDescriptor mEnabledBindingDescriptor
  @multipleBindingDescriptor mHiddenBindingDescriptor
  @regularBindingList mRegularBindingList
}

#·······················································································································

list @arrayControllerAttributListAST {
  @lstring mAttributeName
  @abstractDefaultValue mAttributeValue
}

#·······················································································································

class @arrayControllerDeclarationAST : @abstractDeclarationAST {
  @lstring mControllerName
  @bool mIsRoot
  @lstring mRootEntityName
  @lstring mToManyPropertyName
  @lstringlist mFilterProperties
  @arrayControllerBoundColumnListAST mArrayControllerBoundColumnListAST
  @arrayControllerAttributListAST mArrayControllerAttributListAST
}

#·······················································································································

override method @arrayControllerDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  let node = @lstring.new {!mClassName.string + " " + mControllerName !mControllerName.location}
  [!?ioGraph addNode !node !self]
  [!?ioGraph addEdge !node !mClassName]
  if not mIsRoot then
    [!?ioGraph addEdge !node !.new {!mClassName.string + " " + mToManyPropertyName !mToManyPropertyName.location}]
  elsif mRootEntityName.string != "" then
    [!?ioGraph addEdge !node !.new {!mRootEntityName.string + " " + mToManyPropertyName !mToManyPropertyName.location}]
  end
}

#·······················································································································

override getter @arrayControllerDeclarationAST lkey -> @lstring {
  result = @lstring.new {!mClassName.string + " " + mControllerName !mControllerName.location}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <controller_declaration>
            ?let @lstring inCurrentEntity
            ?let @lstring inRootEntity
            ?!@astDeclarationStruct ioDeclarationAST
  {
    $arrayController$
    $identifier$ ?let @lstring controllerName
    @arrayControllerAttributListAST arrayControllerAttributListAST = {}
    ${$
    repeat
      $identifier$ ?let @lstring attributeName
      $:$
      <explicit_value> ?let @abstractDefaultValue attributeValue
      arrayControllerAttributListAST += !attributeName !attributeValue
    while
      $,$
    end
    $}$
  #--- Model
    @bool isRoot
    select
      $root$
      isRoot = true
    or
      $self$
      isRoot = false
    end
    $.$
    $identifier$ ?let @lstring relationshipName
    @lstringlist filterProperties = {}
    select
    or
      $filter$
      repeat
        $identifier$ ?let @lstring filterProperty
        filterProperties += !filterProperty
      while
        $,$
      end
    end
    @arrayControllerBoundColumnListAST arrayControllerBoundColumnListAST = {}
    select
      $;$
    or
      ${$
      repeat
      while
        $column$
        $literal_string$ ?let columnName
        @lstring sortPropertyName
        select
          sortPropertyName = ["" nowhere]
        or
          $sort$
          $identifier$ ?sortPropertyName
        end
        $Identifier$ ?let columnOutletTypeName
        <column_bindings> ?let @columnBindingAST columnBindingAST
        arrayControllerBoundColumnListAST +=
          !columnName
          !columnOutletTypeName
          !columnBindingAST
          !sortPropertyName
      end
      $}$
    end
    ioDeclarationAST.mUnifiedDeclarationList += !@arrayControllerDeclarationAST.new {
      !inCurrentEntity
      !controllerName
      !isRoot
      !inRootEntity
      !relationshipName
      !filterProperties
      !arrayControllerBoundColumnListAST
      !arrayControllerAttributListAST
    }
  }

#·······················································································································

  rule <column_bindings> !@columnBindingAST outColumnBindingAST {
    @lstring runActionDescriptor = ["" nowhere]
    var enabledBindingDescriptor = @multipleBindingDescriptor.noBinding
    var hiddenBindingDescriptor = @multipleBindingDescriptor.noBinding
    @regularBindingList regularBindingList = {}
    repeat
    while
      $run$
      if runActionDescriptor.string != "" then
        error .here : "duplicated $run binding"
      end
      $self$
      $.$
      $identifier$ ?runActionDescriptor
  #--- Enabled
    while
      $enabled$
      if enabledBindingDescriptor != .noBinding then
        error .here : "duplicated $enabled binding"
      end
      <booleanMultipleBindingExpression> ?let @abstractBooleanMultipleBindingExpressionAST bindingExpression
      enabledBindingDescriptor = .binding {!expression:bindingExpression}
  #--- Hidden
    while
      $hidden$
      if hiddenBindingDescriptor != .noBinding then
        error .here : "duplicated $hidden binding"
      end
      <booleanMultipleBindingExpression> ?let @abstractBooleanMultipleBindingExpressionAST bindingExpression
      hiddenBindingDescriptor = .binding {!expression:bindingExpression}
  #--- Regular binding
    while
      $bindingName$ ?let @lstring bindingName
      @observablePropertyList observablePropertyList = {}
      repeat
        <observable_property> ?let @observablePropertyAST observableProperty
        observablePropertyList += !observableProperty
      while
        $,$
      end
      <binding_option_list> ?let bindingOptions
      regularBindingList +=
        !bindingName
        !observablePropertyList
        !bindingOptions
    end
  #---
    outColumnBindingAST = @columnBindingAST.new {
      !runActionDescriptor
      !enabledBindingDescriptor
      !hiddenBindingDescriptor
      !regularBindingList
    }
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @arrayControllerDeclarationAST firstAnalysisPhase 
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct unused ioGeneration
{
  with mClassName in !?ioSemanticContext.mClassMap 
    error message searchKey
  do
    let @actionMap controllerActions = {!["add" nowhere], !["remove" nowhere] }
    if mIsRoot then
      [ioSemanticContext.mClassMap searchKey !mRootEntityName ?* ?let rootProperties ?2*]
      [rootProperties searchKey !mToManyPropertyName ?let classKind ?*]
      switch classKind
      case property (2*) :
        error mRootEntityName : "a tomany relationship is required here"
      case toMany (typeName * graphic isEntity) :
        let kind = @propertyKind.arrayController {
          !typeName: typeName
          !graphic: graphic
          !isEntity: isEntity
        }
        [!?mPropertyMap insertKey !mControllerName !kind !controllerActions]
    #--- Code Generation
      mPropertyGenerationList += !@arrayControllerPropertyGeneration.new {
        !mControllerName.string
        !"rootObject." + mToManyPropertyName
        !mClassName
        !true
      }
      case toOne (3*) :
        error mRootEntityName : "a tomany relationship is required here"
      case arrayController (3*) :
        error mRootEntityName : "a tomany relationship is required here"
      case selectionController (*) :
        error mRootEntityName : "a tomany relationship is required here"
      end
    else
      [mPropertyMap searchKey !mToManyPropertyName ?let classKind ?*]
      switch classKind
      case property (2*) :
        error mRootEntityName : "a tomany relationship is required here"
      case toMany (typeName * graphic isEntity) :
        let kind = @propertyKind.arrayController {
          !typeName: typeName
          !graphic: graphic
          !isEntity: isEntity
        }
        [!?mPropertyMap insertKey !mControllerName !kind !controllerActions]
    #--- Code Generation
      mPropertyGenerationList += !@arrayControllerPropertyGeneration.new {
        !mControllerName.string
        !mToManyPropertyName
        !mClassName
        !isEntity
      }
      case toOne (3*) :
        error mRootEntityName : "a tomany relationship is required here"
      case arrayController (3*) :
        error mRootEntityName : "a tomany relationship is required here"
      case selectionController (*) :
        error mRootEntityName : "a tomany relationship is required here"
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SECOND ANALYSIS PHASE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @arrayControllerDeclarationAST secondAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
#------------------------------------------------------------ Check array controller attributes
  let attributes = @stringset {!"allowsEmptySelection", !"allowsMultipleSelection"}
  let allowedTypes = @typeKindList {!.boolType}
  @stringset definedAttributes = {}
  @2stringlist attributeListForGeneration = {}
  for (name value) in mArrayControllerAttributListAST do
    if not [attributes hasKey !name.string] then
      error name : "unknown attribute"
    elsif [definedAttributes hasKey !name.string] then
      error name : "attribute multiply defined"
    else
      definedAttributes += !name.string
    end
    [value analyzeDefaultValueType !allowedTypes ?let valueAsString]
    attributeListForGeneration += !name.string !valueAsString
  end
  for s in attributes do
    if not [definedAttributes hasKey !s] then
      error mControllerName : "attribute '" + s + "' not defined"
    end
  end
#------
  @propertyMap boundModelPropertyMap
  if mIsRoot then
    [ioSemanticContext.mClassMap searchKey
       !mRootEntityName
       ?*
       ?boundModelPropertyMap 
       ?2*
     ]
  else
    [ioSemanticContext.mClassMap searchKey
       !mClassName
       ?*
       ?boundModelPropertyMap 
       ?2*
     ]  
  end
  [boundModelPropertyMap searchKey
     !mToManyPropertyName
     ?let @propertyKind boundModelClassKind
     ?*
  ]
#------------------------------------------------------------ Check colum bound models
  @actionMap actionMap
  @propertyMap propertyMap
  @arrayControllerModelKind arrayControllerModelKind
  @bool graphic
  @string elementTypeName
  switch boundModelClassKind
  case property (2*) :
    error mToManyPropertyName
      : "a tomany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case toMany (typeName accessibility isGraphic isEntity) :
    elementTypeName = typeName
    graphic = isGraphic
    arrayControllerModelKind = if [accessibility isTransient]
      then .transientArray
      else if isEntity then .entityArray else .classArray end
    end
    [ioSemanticContext.mClassMap searchKey !typeName ?* ?propertyMap ?actionMap ?*]
  case toOne (3*) :
    error mToManyPropertyName
      : "a tomany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case arrayController (3*) :
    error mToManyPropertyName
      : "a tomany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  case selectionController (*) :
    error mToManyPropertyName
      : "a tomany property is required here"
      : graphic, arrayControllerModelKind, propertyMap, actionMap, elementTypeName
  end
  @arrayControllerBoundColumnListForGeneration arrayControllerBoundColumnListForGeneration = {}
  @arrayControllerSortedColumnListForGeneration arrayControllerSortedColumnListForGeneration = {}
  for () in mArrayControllerBoundColumnListAST do
  #-------- Run action ? Check action is declared
    if mColumnBindingAST.mRunActionName.string != "" then
      [actionMap searchKey !mColumnBindingAST.mRunActionName]
    end
  #-------- Simple bindings
    @regularBindingsGenerationList regularBindingsGenerationList = {}
    analyzeRegularBinding (
      !{} # No root observable property map
      !ioSemanticContext
      !propertyMap
      !mColumnOutletTypeName
      !"<<not used>>"
      !mColumnBindingAST.mRegularBindingList
      !"object"
      !?regularBindingsGenerationList
    )
  #-------- Sort property
    if mSortPropertyName.string != "" then
      [propertyMap searchKey
        !mSortPropertyName
        ?let sortPropertyKind
        ?*
      ]
      if not [sortPropertyKind isComparable] then
        error mSortPropertyName : "this property is not comparable"
      end 
      arrayControllerSortedColumnListForGeneration +=    
        !mColumnName.string
        !sortPropertyKind
        !mSortPropertyName.string
    end
  #-------- Binding generation
    arrayControllerBoundColumnListForGeneration +=
      !mColumnName.string
      !mColumnOutletTypeName.string
      !mColumnBindingAST.mRunActionName.string
      !regularBindingsGenerationList
    ioGeneration.mNeededOutletClasses += !mColumnOutletTypeName.string
  end
#------------------------------------------------------------ Check filter properties
  @arrayControllerFilterListForGeneration arrayControllerFilterListForGeneration = {}
  for (filterPropertyName) in mFilterProperties do
    [propertyMap searchKey
      !filterPropertyName
      ?let @propertyKind columnKind
      ?*
    ]
    arrayControllerFilterListForGeneration += !filterPropertyName.string !columnKind
  end
  let modelString = if mIsRoot
    then "self.rootObject." + mToManyPropertyName + "_property"
    else "self." + mToManyPropertyName + "_property"
  end
#---
  ioGeneration.mFileGenerationList += !@arrayControllerGeneration.new {
    !mClassName
    !mControllerName
    !arrayControllerFilterListForGeneration
    !modelString
    !arrayControllerBoundColumnListForGeneration
    !arrayControllerSortedColumnListForGeneration
    !"ReadWriteArrayOf_" + elementTypeName # [kind swiftTypeName]
    !arrayControllerModelKind # boundModelClassKind
    !elementTypeName # [kind swiftTypeName]
    !graphic
    !attributeListForGeneration
  }
}


#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @arrayControllerPropertyGeneration : @propertyGeneration {
  @string mModelString
  @string mOwnerName
  @bool mUsesManagedObjectContext
}

#·······················································································································

override getter @arrayControllerPropertyGeneration declarationCode -> @string {
  result  = "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n"
  result += "  //   Array controller: " + mPropertyName + "\n"
  result += "  //" + @string.stringWithSequenceOfCharacters  {!'·' !116} + "\n\n"
  result += "  var " + mPropertyName + " = ArrayController_" + mOwnerName + "_" + mPropertyName + " ()\n\n"
}

#·······················································································································

override getter @arrayControllerPropertyGeneration configurationCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  if mUsesManagedObjectContext then
    result += "    self." + mPropertyName + ".setManagedObjectContext (self.managedObjectContext ())\n"
  end
  result += "    self." + mPropertyName + ".bind_model (self." + mModelString + "_property)\n"
}

#·······················································································································

override getter @arrayControllerPropertyGeneration populateExplorerWindowCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  result += "    self." + mPropertyName + ".addExplorer (name: \"" + mPropertyName + "\", y:&y, view:view)\n"
}

#·······················································································································

override getter @arrayControllerPropertyGeneration terminationCode -> @string {
  result  = "  //--- Array controller property: " + mPropertyName + "\n"
  result += "    self." + mPropertyName + ".unbind_model ()\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @arrayControllerModelKind {
  case entityArray
  case classArray
  case transientArray
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @arrayControllerGeneration : @abstractFileGeneration {
  @string mOwnerName # document name, prefs name, ...
  @lstring mControllerName
  @arrayControllerFilterListForGeneration mArrayControllerFilterListForGeneration
  @string mModelString
  @arrayControllerBoundColumnListForGeneration mArrayControllerBoundColumnListForGeneration
  @arrayControllerSortedColumnListForGeneration mArrayControllerSortedColumnListForGeneration
  @string mModelTypeName
  @arrayControllerModelKind mArrayControllerModelKind
  @string mElementTypeName
  @bool mElementTypeIsGraphic
  @2stringlist mAttributeListForGeneration
}

#·······················································································································

list @arrayControllerFilterListForGeneration {
  @string mFilterPropertyName
  @propertyKind mFilterPropertyKind
}

#·······················································································································

list @arrayControllerBoundColumnListForGeneration {
  @string mColumnName
  @string mColumnOutletTypeName
  @string mRunAction # Empty string if no action
  @regularBindingsGenerationList mRegularBindingsGenerationList
}

#·······················································································································

list @arrayControllerSortedColumnListForGeneration {
  @string mColumnName
  @propertyKind mSortPropertyKind
  @string mObservablePropertyForSorting
}

#·······················································································································

getter @arrayControllerFilterListForGeneration filterCode ?let @string inFilterFunction -> @string outResult {
  var filterProperties = self
  [!?filterProperties popFirst ?let firstFilterProperty ?*]
  @string indent = "            "
  outResult  = indent + "switch object." + firstFilterProperty + "_property_selection {\n"
  outResult += indent + "case .empty :\n"
  outResult += indent + "  return .empty\n"
  outResult += indent + "case .multiple :\n"
  outResult += indent + "  isMultiple = true\n"
  outResult += indent + "case .single (let v1) :\n"
  @uint idx = 2
  @string args = ""
  @string blockEnd = indent + "}\n"
  for () in filterProperties do
    indent += "  "
    outResult += indent + "switch object." + mFilterPropertyName + "_property_selection {\n"
    outResult += indent + "case .empty :\n"
    outResult += indent + "  return .empty\n"
    outResult += indent + "case .multiple :\n"
    outResult += indent + "  isMultiple = true\n"
    outResult += indent + "case .single (let v" + idx + ") :\n"
    blockEnd = indent + "}\n" + blockEnd
    args += ", v" + idx
    idx ++
  end
  indent += "  "
  outResult += indent + "if " + inFilterFunction + " (v1" + args + ") {\n"
  outResult += indent + "  filteredArray.append (object)\n"
  outResult += indent + "}\n"
  outResult += blockEnd
}

#·······················································································································

private filewrapper collectionControllerGenerationTemplate in "../generation-templates/controllers" {
}{
}{
  template arrayControllerImplementationInSwift "array-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string ARRAY_CONTROLLER_NAME
    ?@arrayControllerFilterListForGeneration FILTER_PROPERTIES
    ?@string MODEL_STRING
    ?@arrayControllerModelKind MODEL_KIND
    ?@string MODEL_TYPE_NAME
    ?@arrayControllerBoundColumnListForGeneration BOUND_COLUMNS
    ?@arrayControllerSortedColumnListForGeneration SORTED_COLUMNS
    ?@string ELEMENT_TYPE_NAME
    ?@bool ELEMENT_TYPE_IS_GRAPHIC
    ?@stringset OBSERVED_PROPERTIES_FOR_SORTING_AND_FILERING
    ?@2stringlist ATTRIBUTE_VALUES

  template filterFunction "array-controller-filter-function.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string ARRAY_CONTROLLER_NAME
    ?@arrayControllerFilterListForGeneration FILTER_PROPERTIES
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @arrayControllerGeneration generateCode
    ?let @string inOutputDirectory
    ?!@stringset ioGeneratedFileSet
{
#--- Build observed property set (for sorting and filtering)
  @stringset observedProperties = {}
  for () in mArrayControllerSortedColumnListForGeneration do
    observedProperties += !mObservablePropertyForSorting
  end
  for () in mArrayControllerFilterListForGeneration do
    observedProperties += !mFilterPropertyName
  end
  let s = [filewrapper collectionControllerGenerationTemplate.arrayControllerImplementationInSwift
    !mOwnerName
    !mControllerName
    !mArrayControllerFilterListForGeneration
    !mModelString
    !mArrayControllerModelKind
    !mModelTypeName
    !mArrayControllerBoundColumnListForGeneration
    !mArrayControllerSortedColumnListForGeneration
    !mElementTypeName
    !mElementTypeIsGraphic
    !observedProperties
    !mAttributeListForGeneration
  ]
  [@string generateFile
    !inOutputDirectory
    !"arrayController-" + mOwnerName + "-" +  mControllerName + ".swift"
    !s
  ]
  if [mArrayControllerFilterListForGeneration length] > 0 then
    let s = [filewrapper collectionControllerGenerationTemplate.filterFunction
      !mOwnerName
      !mControllerName
      !mArrayControllerFilterListForGeneration
    ]
    let fileName = "arrayControllerFilter-" + mOwnerName + "-" +  mControllerName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName:fileName
      !lineComment:"//"
      !header:""
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:"  return WHAT ?\n" # Defaut user zone2
      !generatedZone3:"}\n\n//" + ["" stringByRightPadding !117 !'—'] + "\n"
      !makeExecutable:false
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
