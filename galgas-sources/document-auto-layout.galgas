#-----------------------------------------------------------------------------------------------------------------------
#   AST
#-----------------------------------------------------------------------------------------------------------------------

class @autoLayoutDocumentDeclarationAST : @abstractDeclarationAST {
  @lstring mRootEntityName
  @lstring mMainViewName
  @outletDeclarationList mOutletDeclarationList
  @lstringlist mActionDeclarationList
  @arrayControllerBindingListAST mArrayControllerBindingListAST
  @astViewDeclarationList mViewDeclarationList
  @astAutoLayoutToolbarItemList mAutoLayoutToolbarItemList
  @string mCustomSuperClassName # Empty String if no custom superclass
}

#·······················································································································

enum @astAutoLayoutToolbarItem {
  case view (@lstring title @astComputedViewInstruction instruction)
  case space
}

#·······················································································································

list @astAutoLayoutToolbarItemList {
  @astAutoLayoutToolbarItem mItem
}

#·······················································································································

override method @autoLayoutDocumentDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  [!?ioGraph addNode !mClassName !self]
  [!?ioGraph addEdge !mClassName !mRootEntityName]
}

#·······················································································································

override getter @autoLayoutDocumentDeclarationAST nodeKey -> @lstring {
  result = mClassName
}

#-----------------------------------------------------------------------------------------------------------------------
#   SYNTAX
#-----------------------------------------------------------------------------------------------------------------------

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <document_declaration> ?!@astDeclarationStruct ioDeclarationAST {
    $autoLayoutDocument$
    $Identifier$ ?let @lstring documentName
    @string customSuperClassName
    select
      customSuperClassName = ""
    or
      $:$
      $Identifier$ ?let name
      customSuperClassName = name.string
    end
    $root$
    $Identifier$ ?let @lstring rootEntityName
    $stackView$
    $identifier$ ?let @lstring mainViewName
    ${$
    @outletDeclarationList outletDeclarationList = {}
    @regularBindingList bindingList = {}
    @lstringlist actionDeclarationList = {}
    @stringset signatureList = {}
    @arrayControllerBindingListAST arrayControllerBindingListAST = {}
    @astViewDeclarationList viewDeclarationList = {}
    @astAutoLayoutToolbarItemList autoLayoutToolbarItemList = {}
    repeat
    while
      <stack_view_declaration> !?viewDeclarationList
    while
      <action_declaration> !?actionDeclarationList
    while
      <transient_declaration> !documentName !rootEntityName !?ioDeclarationAST
    while
      <outlet_declaration> !?outletDeclarationList
    while
      <controller_declaration> !documentName !rootEntityName !?arrayControllerBindingListAST !?ioDeclarationAST
    while
      $toolbarItem$
      select
        $space$
        $;$
        autoLayoutToolbarItemList += !.space
      or
        $literal_string$ ?let title
        $:$
        <computed_view_instruction> ?let instruction
        autoLayoutToolbarItemList += !.view {!title: title !instruction: instruction}
      end
    end
    $}$
    ioDeclarationAST.mUnifiedDeclarationList += !@autoLayoutDocumentDeclarationAST.new {
      !documentName
      !rootEntityName
      !mainViewName
      !outletDeclarationList
      !actionDeclarationList
      !arrayControllerBindingListAST
      !viewDeclarationList
      !autoLayoutToolbarItemList
      !customSuperClassName
    }
    ioDeclarationAST.mUnifiedDeclarationList += !@transientDeclarationAST.new {
      !documentName
      !rootEntityName
      !["String" here]
      !["documentFilePath" here]
      !{}
      !false
    }
  }

  #·····················································································································

}

#-----------------------------------------------------------------------------------------------------------------------
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#-----------------------------------------------------------------------------------------------------------------------

override method @autoLayoutDocumentDeclarationAST firstAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
  buildActionMap (!mActionDeclarationList ?let @actionMap actionMap)
  [!?ioSemanticContext.mClassMap insertKey
    !mClassName
    !.document {!rootEntityName: mRootEntityName}
    !{}
    !actionMap
    !{}
  ]
  for () in actionMap do
    ioGeneration.mFileGenerationList += !@actionFileGeneration.new {
      !mClassName.string
      !lkey.string
    }
  end
}

#·······················································································································

override method @autoLayoutDocumentDeclarationAST fourthAnalysisPhase
       ?!@semanticContext ioSemanticContext
       ?!@generationStruct ioGeneration
{
  [ioSemanticContext.mClassMap searchKey
    !.new {!preferencesName () !.nowhere}
    ?*
    ?let preferencesPropertyMap
    ?2*
  ]
#--- Get document property map
  [ioSemanticContext.mClassMap searchKey
    !.new {!mClassName !.nowhere}
    ?*
    ?let documentPropertyMap
    ?let documentActionMap
    ?let documentPropertyGenerationList
  ]
#--- Get root entity property map
  [ioSemanticContext.mClassMap searchKey
    !mRootEntityName
    ?*
    ?let rootPropertyMap
    ?let unused rootActionMap
    ?let unused rootPropertyGenerationList
  ]
#--- Analyze outlets and bindings
  analyzeOutlets (
    !false
    !rootPropertyMap
    !preferencesPropertyMap
    !mRootEntityName.string
    !ioSemanticContext
    !mOutletDeclarationList
    !documentPropertyMap
    !documentActionMap
    !mClassName
    !?ioGeneration
    ?let @regularBindingsGenerationList regularBindingsGenerationList
    ?@multipleBindingGenerationList multipleBindingGenerationList
    ?let @actionBindingListForGeneration actionBindingListForGeneration
    ?let @decoratedOutletMap outletMap
    ?let @tableViewBindingGenerationList tableViewBindingGenerationList
    ?let @ebViewGraphicControllerBindingGenerationList ebViewGraphicControllerBindingGenerationList
  )
#--- Analyze controller bindings
  for (controllerName hiddenBinding) in mArrayControllerBindingListAST do
  #--- Hidden
    switch hiddenBinding
    case noBinding :
    case binding (@abstractBooleanMultipleBindingExpressionAST expression) :
      [expression analyzeExpressionForMultipleBinding
        !false
        !rootPropertyMap
        !ioSemanticContext
        !documentPropertyMap
        ?let @abstractBooleanMultipleBindingExpressionForGeneration hiddenExpression
        ?let @typeKind type
        ?let @location errorLocation
      ]
      if not [type isBoolType] then
        error errorLocation : "expression is not boolean"
      end
      multipleBindingGenerationList +=
        !controllerName.string
        !"hidden"
        !hiddenExpression
    end
  end
#--- Check auto layout view declarations
  @autoLayoutViewDeclarationMap viewDeclarationMap = {}
  @viewGenerationList viewGenerationList = {}
  @implicitViewFunctionGenerationList implicitViewFunctionGenerationList = {}
  @autoLayoutConfiguratorMap configuratorMap = {}
  for (viewName viewDeclaration) in mViewDeclarationList do
    [!?viewDeclarationMap insertKey !viewName]
    [viewDeclaration check
      !viewName
      !viewDeclarationMap
      !false
      !rootPropertyMap
      !preferencesPropertyMap
      !ioSemanticContext
      !documentPropertyMap
      !documentActionMap
      !mClassName
      !?implicitViewFunctionGenerationList
      !?configuratorMap
      ?let viewGeneration
    ]
    viewGenerationList += !viewName !viewGeneration
  end
  [viewDeclarationMap searchKey !mMainViewName]
#--- Analyze toolbar item declarations
  @autoLayoutToolbarItemGenerationList autoLayoutToolbarItemGenerationList = {}
  for (item) in mAutoLayoutToolbarItemList do
    switch item
    case space :
      autoLayoutToolbarItemGenerationList += !.space
    case view (title instruction) :
      [instruction generateCode
        !viewDeclarationMap
        !false
        !rootPropertyMap
        !preferencesPropertyMap
        !ioSemanticContext
        !documentPropertyMap
        !documentActionMap
        !mClassName
        !?implicitViewFunctionGenerationList
        !?configuratorMap
        ?let @abstractViewInstructionGeneration generatedInstruction
      ]
      autoLayoutToolbarItemGenerationList += !.view {!title: title.string !instruction: generatedInstruction}
    end
  end
#--- Code generation
  ioGeneration.mGenerateEBManagedAutoLayoutDocumentSwift = true
#---
  ioGeneration.mFileGenerationList += !@autoLayoutDocumentFileGeneration.new {
    !mClassName.string
    !mRootEntityName.string
    !mMainViewName.string
    !documentPropertyGenerationList
    !outletMap
    !actionBindingListForGeneration
    !regularBindingsGenerationList
    !multipleBindingGenerationList
    !tableViewBindingGenerationList
    !ebViewGraphicControllerBindingGenerationList
    !viewGenerationList
    !autoLayoutToolbarItemGenerationList
    !implicitViewFunctionGenerationList
    !configuratorMap
    !mCustomSuperClassName # Empty String if no custom superclass
  }
}

#-----------------------------------------------------------------------------------------------------------------------
#   GENERATION                            
#-----------------------------------------------------------------------------------------------------------------------

class @autoLayoutDocumentFileGeneration : @abstractFileGeneration {
  @string mDocumentName
  @string mRootEntityName
  @string mMainViewName
  @propertyGenerationList mDocumentPropertyGenearionList
  @decoratedOutletMap mOutletMap
  @actionBindingListForGeneration mTargetActionList 
  @regularBindingsGenerationList mRegularBindingsGenerationList 
  @multipleBindingGenerationList multipleBindingGenerationList
  @tableViewBindingGenerationList mTableViewBindingGenerationList
  @ebViewGraphicControllerBindingGenerationList mEBViewBindingGenerationList
  @viewGenerationList mViewDeclarationList
  @autoLayoutToolbarItemGenerationList mAutoLayoutToolbarItemGenerationList
  @implicitViewFunctionGenerationList mImplicitViewFunctionGenerationList
  @autoLayoutConfiguratorMap mConfiguratorMap
  @string mCustomSuperClassName # Empty String if no custom superclass
}

#·······················································································································

enum @autoLayoutToolbarItemGeneration {
  case view (@string title @abstractViewInstructionGeneration instruction)
  case space
}

#·······················································································································

list @autoLayoutToolbarItemGenerationList {
  @autoLayoutToolbarItemGeneration mItem
}

#·······················································································································

override method @autoLayoutDocumentFileGeneration generateCode
    ?let @string inOutputDirectory
    ?!@stringset ioGeneratedFileSet
{
  let s = [filewrapper autoLayoutDocumentGenerationTemplate.documentImplementation
    !mDocumentName
    !mRootEntityName
    !mMainViewName
    !mDocumentPropertyGenearionList
    !mOutletMap
    !mTargetActionList
    !mRegularBindingsGenerationList
    !multipleBindingGenerationList
    !mTableViewBindingGenerationList
    !mEBViewBindingGenerationList
    !mViewDeclarationList
    !mAutoLayoutToolbarItemGenerationList
    !mImplicitViewFunctionGenerationList
    !if mCustomSuperClassName == "" then "EBAutoLayoutManagedDocument" else mCustomSuperClassName end
  ]
  let fileName = mDocumentName + ".swift"
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !s
  ]
  for (outletName outletTypeName) in mConfiguratorMap do
    let s = [filewrapper autoLayoutDocumentGenerationTemplate.documentOutletConfiguratorImplementation
      !mDocumentName
      !outletName.string
      !outletTypeName
    ]
    let header = "//" + @string.stringWithSequenceOfCharacters {!'-' !118} + "\n"
      "//  THIS FILE IS REGENERATED BY EASY BINDINGS, ONLY MODIFY IT WITHIN USER ZONES\n"
      "//" + @string.stringWithSequenceOfCharacters {!'-' !118} + "\n\n"
      + "import Cocoa\n\n"
      + "//" + @string.stringWithSequenceOfCharacters {!'-' !118} + "\n\n"
    let fileName = "configurator-" + mDocumentName + "-" + outletName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFileWithPattern
      !startPath: inOutputDirectory
      !fileName: fileName
      !lineComment:"//"
      !header:header
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:"INSERT CONFIGURATION CODE\n" # Defaut user zone2
      !generatedZone3:"  }\n}\n\n//" + @string.stringWithSequenceOfCharacters {!'-' !118} + "\n"
      !makeExecutable:false
    ]
  end
}

#·······················································································································

private filewrapper autoLayoutDocumentGenerationTemplate in "../generation-templates" {
}{
}{
  template documentImplementation "document-auto-layout.swift.galgasTemplate"
    ?@string DOCUMENT_CLASS_NAME
    ?@string ROOT_ENTITY_NAME
    ?@string MAIN_VIEW_NAME
    ?@propertyGenerationList DOCUMENT_PROPERTY_LIST
    ?@decoratedOutletMap OUTLET_GENERATION_MAP
    ?@actionBindingListForGeneration TARGET_ACTION_LIST
    ?@regularBindingsGenerationList REGULAR_BINDINGS_GENERATION_LIST 
    ?@multipleBindingGenerationList MULTIPLE_BINDING_GENERATION_LIST
    ?@tableViewBindingGenerationList TABLE_VALUE_BINDING_GENERATION_LIST
    ?@ebViewGraphicControllerBindingGenerationList EB_VIEW_GRAPHIC_CONTROLLER_BINDING_GENERATION_LIST
    ?@viewGenerationList VIEW_GENERATION_LIST
    ?@autoLayoutToolbarItemGenerationList TOOLBAR_ITEM_LIST
    ?@implicitViewFunctionGenerationList IMPLICIT_VIEW_GENERATION_FUNCTION_LIST
    ?@string DOCUMENT_SUPER_CLASS_NAME

  template documentOutletConfiguratorImplementation "configure-auto-layout-outlet.swift.galgasTemplate"
    ?@string DOCUMENT_TYPE_NAME
    ?@string OUTLET_NAME
    ?@string OUTLET_TYPE_NAME
}

#-----------------------------------------------------------------------------------------------------------------------

proc generateAutoLayoutManagedDocumentSwift
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  let fileName = "EBAutoLayoutManagedDocument.swift"
  var fw = [filewrapper swift_sources]
  let contents = [fw textFileContentsAtPath !fileName]
  ioGeneratedFileSet += !fileName
  [@string generateFile
    !inOutputDirectory
    !fileName
    !contents
  ]
}

#-----------------------------------------------------------------------------------------------------------------------
