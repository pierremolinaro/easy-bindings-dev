#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

class @transientAST : @abstractSecondaryProperty {
  @lstring mTransientTypeName
  @bool mIsArray
  @lstring mTransientName
  @observablePropertyList mDependencyList
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <transient_declaration>
    ?!@secondaryPropertyList ioSecondaryPropertyList
  {
    $transient$
    @bool isArray
    select
      isArray = false
    or
      $array$
      isArray = true
    end
    $Identifier$ ?var @lstring transientTypeName
    $identifier$ ?let @lstring transientName
    $dependsFrom$
    @observablePropertyList dependanceList = {}
    repeat
      <observable_property> ?let @observablePropertyAST observablePropertyAST
      dependanceList += !observablePropertyAST
    while
      $,$
    end
    $;$
    ioSecondaryPropertyList += !@transientAST.new {
      !transientTypeName
      !isArray
      !transientName
      !dependanceList
    }
  }

  #·····················································································································

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   TYPE INVENTORY                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

override method @transientAST typeInventory
  ?!@unifiedTypeMap ioUnifiedTypeMap
{
  [@unifiedTypeMap-proxy makeProxy
    !?ioUnifiedTypeMap
    !mTransientTypeName
    ?*
  ]
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

override method @transientAST tryToSolveSecondaryProperty
  ?!@semanticContext ioSemanticContext
  ?let @observablePropertyMap inRootObservableProperties
  ?!@observablePropertyMap ioObservableProperties
  ?!@secondaryPropertyList ioUnsolvedProperties
{
  @bool solved = true
  for () in mDependencyList while solved do
    solved = [mObservableProperty isPropertyDefined
      !ioSemanticContext
      !inRootObservableProperties
      !ioObservableProperties
    ]
  end
  if solved then
    [ioSemanticContext.mUnifiedTypeMap searchKey !mTransientTypeName ?let type ?let actionMap]
    [!?ioObservableProperties insertKey
      !mTransientName
      !type
      !.transient
      !if mIsArray then .collection else .single end
      !""
      !actionMap
    ]
  else
    ioUnsolvedProperties += !self
  end
}

#·······················································································································

override method @transientAST secondaryPropertySemanticAnalysis
  ?let @string inOwnerName
  ?let @string unused inRootEntityName
  ?let @observablePropertyMap inRootObservableProperties
  ?let @semanticContext inSemanticContext
  ?let @observablePropertyMap inObservableProperties
  ?!@transientDefinitionListForGeneration ioTransientDefinitionListForGeneration
  ?!@arrayControllerForGeneration unused ioArrayControllerForGeneration
  ?!@selectionControllerForGeneration unused ioSelectionControllerForGeneration
  ?!@customObjectControllerForGeneration unused ioCustomObjectControllerDeclaration
  ?!@stringset unused ioNeededOutletClasses
{
#--- Property type
  [inSemanticContext.mUnifiedTypeMap searchKey !mTransientTypeName ?let type ?*]
#--- Check type
  @typeKindList typeList
  switch type
  case boolType, integerType, stringType, dateType, doubleType, fontType : # Ok
    typeList = {!type}
  case enumType (* *) : # Ok
    typeList = {!type}
  case entityType (*) :
    error mTransientName: "an entity cannot be used as simple property type" : typeList
  case classType (*) :
    typeList = {!type}
  case atomicClassType (* *) :
    typeList = {!type}
  case transientPropertyClassType (*) :
    typeList = {!type}
  case propertyClassType (**) :
    typeList = {!type}
  end
#--- Analyse master properties
  @transientDependencyListForGeneration dependencies = {}
  for () in mDependencyList do (idx)
    [mObservableProperty analyzeObservableProperty
      !inRootObservableProperties
      !inSemanticContext
      !inObservableProperties
      ?let @typeKind unused dependencyType
      ?let @propertyKind unused dependencyKind
      ?@propertyMultiplicity outMultiplicity
      ?let @string swiftTypeStringForTransientFunctionArgument
    ]
    if (outMultiplicity == .collection) && (dependencyKind == .stored)  then
      error [mObservableProperty location] : "a collection property cannot be observed"
    end
    dependencies +=
      !mObservableProperty
      ![[mObservableProperty modelString !"self"] nameRepresentation] + "_" + idx
      !swiftTypeStringForTransientFunctionArgument
  end
#--- Code generation
  ioTransientDefinitionListForGeneration +=
    !inOwnerName
    !mTransientName.string
    !mIsArray
    !type
    !dependencies
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPE                                                                                                    *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @transientDefinitionListForGeneration {
  @string mOwnerName
  @string mTransientName
  @bool mIsArray
  @typeKind mTransientType
  @transientDependencyListForGeneration mDependencyList
}

#·······················································································································

list @transientDependencyListForGeneration {
  @observablePropertyAST mDependency
  @string mFunctionArgumentName
  @string mFunctionArgumentTypeString
}

#·······················································································································

getter @transientDependencyListForGeneration transientComputeFunctionCall
  ?let @string inOwnerName
  ?let @string inTransientName
  -> @string outResult
{
  @string ident = "        "
  outResult = ""
  if [self length] == 1 then
    [self first ?let @observablePropertyAST first ?* ?*]
    outResult += ident + "let kind = " + [first modelString !"unwSelf"] + ".prop.kind ()\n"
  else
    var dependencies = self
    [!?dependencies popFirst ?let @observablePropertyAST first ?* ?*]
    outResult += ident + "var kind = " + [first modelString !"unwSelf"] + ".prop.kind ()\n"
    for () in dependencies do
      outResult += ident + "kind &= " + [mDependency modelString !"unwSelf"] + ".prop.kind ()\n"
    end
  end
  outResult += ident + "switch kind {\n"
  outResult += ident + "case .noSelectionKind :\n"
  outResult += ident + "  return .noSelection\n"
  outResult += ident + "case .multipleSelectionKind :\n"
  outResult += ident + "  return .multipleSelection\n"
  outResult += ident + "case .singleSelectionKind :\n"
  outResult += ident + "  switch ("
  for () in self
  do outResult += [mDependency modelString !"unwSelf"] + ".prop"
  between outResult += ", "
  end
  outResult += ") {\n"
  outResult += ident + "  case ("
  for () in self
  do (idx) outResult += ".singleSelection (let v" + idx + ")"
  between outResult += ", "
  end
  outResult += ") :\n"
  outResult += ident + "    return .singleSelection (compute_" + inOwnerName + "_" + inTransientName + " ("
  for () in self
  do (idx) outResult += "v" + idx
  between outResult += ", "
  end
  outResult += "))\n"
  outResult += ident + "  default :\n"
  outResult += ident + "    return .noSelection\n"
  outResult += ident + "  }\n"
  outResult += ident + "}\n"
}

#·······················································································································

getter @transientDependencyListForGeneration needs_unwSelf
  -> @bool outResult
{
  outResult = false
  for (dep * *) in self while not outResult do
    outResult = [dep needs_unwSelf]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION                                                                                                         *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

getter @observablePropertyAST generateAddObserverCall -> @string outResult {
  switch self
  case controllerProperty (@lstring controllerName @lstring propertyName) : 
    outResult = controllerName.string + "." + propertyName + ".addEBObserver"
  case controllerSecondaryProperty (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = controllerName.string + "." + propertyName + "." + secondaryPropertyName + ".addEBObserver"
  case selfProperty (@lstring masterName) :
    outResult = masterName.string + ".addEBObserver"
  case selfPropertyWithOption (@lstring masterName *) :
    outResult = masterName.string + ".addEBObserver"
  case selfPropertyArray (@lstring masterName @lstring elementPropertyName) :
    outResult = masterName.string + ".addEBObserverOf_" + elementPropertyName
  case prefsProperty (@lstring propertyName) :
    outResult = "g_" + preferencesName () + "?."  + propertyName + ".addEBObserver"
  case prefsPropertyWithOption (@lstring propertyName *) :
    outResult = "g_" + preferencesName () + "?."  + propertyName + ".addEBObserver"
  case rootProperty (@lstring masterName) :
    outResult = "rootObject." + masterName + ".addEBObserver"
  case rootPropertyWithOption (@lstring masterName @lstring optionName) :
    outResult = "rootObject." + masterName + "." + optionName + ".addEBObserver"
  case rootPropertyRelationship (@lstring relationshipName @lstring propertyName) :
    outResult = "self.rootObject." + relationshipName + ".addEBObserverOf_" + propertyName
  case signatureProperty (*) :
    outResult = "<< ligne 243 >>"
  case versionProperty (*) :
    outResult = "<< ligne 245 >>"
  case versionShouldChangeProperty (*) :
    outResult = "<< ligne 247 >>"
  end
}

#·······················································································································

getter @observablePropertyAST generateRemoveObserverCall -> @string outResult {
  switch self
  case controllerProperty (@lstring controllerName @lstring propertyName) : 
    outResult = controllerName.string + "." + propertyName + ".removeEBObserver"
  case controllerSecondaryProperty (@lstring controllerName @lstring propertyName @lstring secondaryPropertyName) : 
    outResult = controllerName.string + "." + propertyName + "." + secondaryPropertyName + ".removeEBObserver"
  case selfProperty (@lstring masterName) :
    outResult = masterName.string + ".removeEBObserver"
  case selfPropertyWithOption (@lstring masterName *) :
    outResult = masterName.string + ".removeEBObserver"
  case selfPropertyArray (@lstring masterName @lstring elementPropertyName) :
    outResult = masterName.string + ".removeEBObserverOf_" + elementPropertyName
  case prefsProperty (@lstring propertyName) :
    outResult = "g_" + preferencesName () + "?."  + propertyName + ".removeEBObserver"
  case prefsPropertyWithOption (@lstring propertyName *) :
    outResult = "g_" + preferencesName () + "?."  + propertyName + ".removeEBObserver"
  case rootProperty (@lstring masterName) :
    outResult = "rootObject." + masterName + ".removeEBObserver"
  case rootPropertyWithOption (@lstring masterName @lstring optionName) :
    outResult = "rootObject." + masterName + "." + optionName + ".removeEBObserver"
  case rootPropertyRelationship (@lstring relationshipName @lstring propertyName) :
    outResult = "self.rootObject." + relationshipName + ".removeEBObserverOf_" + propertyName
  case signatureProperty (*) :
    outResult = "<< ligne 270 >>"
  case versionProperty (*) :
    outResult = "<< ligne 272 >>"
  case versionShouldChangeProperty (*) :
    outResult = "<< ligne 274 >>"
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#    TRANSIENT ROUTINE GENERATION                                                                                      *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generateTransients
  ?let @string inOutputDirectory
  ?let @transientDefinitionListForGeneration inTransientListForGeneration
  ?!@stringset ioGeneratedFileSet
{
#--------------------------- Generate transient routines
  for () in inTransientListForGeneration do
    let s = [filewrapper transientManager.transientComputationFunctionFile
      !mOwnerName
      !mTransientName
      !mIsArray
      !mDependencyList
      !mTransientType 
    ]
    let fileName = "transient-" + mOwnerName + "-" + mTransientName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName:fileName
      !lineComment:"//"
      !header:""
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:"\n" # Defaut user zone2
      !generatedZone3:"}\n\n//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————\n"
      !makeExecutable:false
    ]
  end
}

#·······················································································································

private filewrapper transientManager in "../generation-templates" {
}{
}{
  template transientComputationFunctionFile "transient-computation-function.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string TRANSIENT_NAME
    ?@bool IS_ARRAY
    ?@transientDependencyListForGeneration DEPENDENCY_LIST
    ?@typeKind TRANSIENT_TYPE
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
