#-----------------------------------------------------------------------------------------------------------------------
#   GENERATION TYPES                      
#-----------------------------------------------------------------------------------------------------------------------

list @validationStubRoutineListForGeneration {
  public var @string mObjectTypeName
  public var @string mModelName
  public var @string mModelTypeName
}

#·······················································································································

filewrapper validationStubExtension in "generation-templates" {
}{
}{
  template actionGeneration "validation-stub-extension.swift.galgasTemplate"
    ?@string OBJECT_TYPE_NAME
    ?@string MODEL_NAME
    ?@string MODEL_TYPE_NAME
 }

#-----------------------------------------------------------------------------------------------------------------------
#   GENERATION                            
#-----------------------------------------------------------------------------------------------------------------------

proc generateValidationRoutineStubs(
  ?let @string inOutputDirectory,
  ?let @validationStubRoutineListForGeneration inValidationStubRoutineListForGeneration,
  ?!@stringset ioGeneratedFileSet)
{
  for (mObjectTypeName mModelName mModelTypeName) in inValidationStubRoutineListForGeneration do
    let s = filewrapper [validationStubExtension.actionGeneration:
      !mObjectTypeName,
      !mModelName,
      !mModelTypeName
    ]
    let fileName = "validation-" + mObjectTypeName + "-" + mModelName + ".swift"
    ioGeneratedFileSet += !fileName
    let defaultUserZone2 =
        "    var result : EBValidationResult <" + mModelTypeName + ">\n"
      + "    let validates = false // Add your validation condition here\n"
      + "    if validates {\n"
      + "      result = .ok (proposedValue)\n"
      + "    }else{\n"
      + "      result = .rejectWithAlert (\"Rejected in \\(#file), line \\(#line)\")\n"
      + "    }\n"
      + "    return result\n"    
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName:fileName
      !lineComment:"//"
      !header:""
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:s
      !defaultUserZone2:defaultUserZone2
      !generatedZone3:"  }\n}\n\n//---------------------------------------------------------------------------------------------------------------------*\n"
      !makeExecutable:false
    ]
  end
}

#-----------------------------------------------------------------------------------------------------------------------

