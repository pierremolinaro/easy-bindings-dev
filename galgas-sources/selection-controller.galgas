#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST   
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @selectionControllerDeclarationEX : @abstractSecondaryProperty {
  @lstring mSelectionControllerName
  @lstring mModelControllerName
  @lstring mModelControllerPropertyName
}

#·······················································································································

class @selectionControllerDeclarationAST : @abstractDeclarationAST {
  @lstring mSelectionControllerName
  @lstring mModelControllerName
  @lstring mModelControllerPropertyName
}

#·······················································································································

override method @selectionControllerDeclarationAST enterInPrecedenceGraph ?!@declarationPrecedenceGraph ioGraph {
  let node = @lstring.new {!mClassName.string + " " + mSelectionControllerName !mSelectionControllerName.location}
  [!?ioGraph addNode !node !self]
  [!?ioGraph addEdge !node !.new {!mClassName.string + " " + mModelControllerName !mModelControllerName.location}]
}

#·······················································································································

override getter @selectionControllerDeclarationAST lkey -> @lstring {
  result = @lstring.new {!mClassName.string + " " + mSelectionControllerName !mSelectionControllerName.location}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <controller_declaration>
            ?let @lstring inCurrentEntity
            ?let @lstring unused inRootEntity
            ?!@secondaryPropertyList ioSecondaryPropertyList
            ?!@astDeclarationStruct ioDeclarationAST
  {
    $selectionController$
    $identifier$ ?let @lstring selectionControllerName
    $:$
    $self$
    $.$
    $identifier$ ?let modelControllerName
    $.$
    $identifier$ ?let @lstring propertyName
    $;$
    ioSecondaryPropertyList += !@selectionControllerDeclarationEX.new {
      !selectionControllerName
      !modelControllerName
      !propertyName
    }
    ioDeclarationAST.mUnifiedDeclarationList += !@selectionControllerDeclarationAST.new {
      !inCurrentEntity
      !selectionControllerName
      !modelControllerName
      !propertyName
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CLASS AND PROPERTY SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selectionControllerDeclarationAST classAndPropertySemanticAnalysis
       ?!@classMap ioClassMap
       ?!@generationStruct unused ioGeneration
{
  with mClassName in !?ioClassMap 
    error message searchKey
  do
    [mPropertyMap searchKey !mModelControllerName ?let classKind]
    switch classKind
    case property (2*) :
      error mModelControllerName : "an array controller is required here"
    case toMany (4*) :
      error mModelControllerName : "an array controller is required here"
    case toOne (3*) :
      error mModelControllerName : "an array controller is required here"
    case arrayController (typeName 2*) :
      if mModelControllerPropertyName.string != "selectedArray" then
        error mModelControllerPropertyName : "'selectedArray' is required here"
      end
      let kind = @propertyKind.selectionController {
        !typeName: typeName
      }
      [!?mPropertyMap insertKey !mSelectionControllerName !kind]
    case selectionController (*) :
      error mModelControllerName : "an array controller is required here"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SECOND ANALYSIS PHASE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selectionControllerDeclarationAST secondAnalysisPhase
       ?!@classMap unused ioClassMap
       ?!@generationStruct unused ioGeneration
{
}




#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selectionControllerDeclarationEX typeInventory
  ?!@unifiedTypeMap unused ioUnifiedTypeMap
{
}

#·······················································································································

override method @selectionControllerDeclarationEX tryToDefineSecondaryProperty
  ?!@semanticContext unused ioSemanticContext
  ?let @observablePropertyMap unused inRootObservableProperties
  ?!@observablePropertyMap ioObservableProperties
  ?!@secondaryPropertyList ioUnsolvedProperties
  ?!@simpleStoredPropertyListForGenerationEX unused ioSimpleStoredPropertyListForGeneration
{
  if mModelControllerName.string == "self" then
    [ioObservableProperties searchKey
      !mModelControllerPropertyName
      ?let kind
      ?3*
    ]
  #--- Check type
    @string entityName
    switch kind
    case property (2*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case toMany (typeName 3*) :
      entityName = typeName
    case toOne (3*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case arrayController (3*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case selectionController (*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    end
  #--- Store selection controller
    [!?ioObservableProperties insertKey
      !mSelectionControllerName
      !.selectionController {!typeName:entityName}
      !"" # No inverse relationship
      !{} # No action
      !false
    ]
  elsif [ioObservableProperties hasKey !mModelControllerName.string] then
  #--- Get model controller
    [ioObservableProperties searchKey
      !mModelControllerName
      ?let kind
      ?3*
    ]
    @string entityName
    switch kind
    case property (2*) :
      error mModelControllerName : "the model should be an array controller" : entityName
    case toMany (4*) :
      error mModelControllerName : "the model should be an array controller" : entityName
    case toOne (3*) :
      error mModelControllerName : "the model should be an array controller" : entityName
    case arrayController (typeName 2*) :
      entityName = typeName
    case selectionController (*) :
      error mModelControllerName : "the model should be an array controller" : entityName
    end
  #--- Store selection controller
    [!?ioObservableProperties insertKey
      !mSelectionControllerName
      !.selectionController {!typeName:entityName}
      !"" # No inverse relationship
      !{} # No action
      !false
    ]  
  else
    ioUnsolvedProperties += !self
  end
}

#·······················································································································

override method @selectionControllerDeclarationEX secondaryPropertySemanticAnalysis
  ?let @string inOwnerName
  ?let @string unused inRootEntityName
  ?let @observablePropertyMap unused inRootObservableProperties
  ?let @semanticContext inSemanticContext
  ?let @observablePropertyMap inObservableProperties
  ?!@transientDefinitionListForGeneration unused ioTransientDefinitionListForGeneration
  ?!@arrayControllerForGeneration unused ioArrayControllerForGeneration
  ?!@selectionControllerForGeneration ioSelectionControllerForGeneration
  ?!@stringset unused ioNeededOutletClasses
{
#--- Get model controller
  @string selectionEntityName
  @observablePropertyMap selectionObservablePropertyMap
  if mModelControllerName.string == "self" then
    [inObservableProperties searchKey
      !mModelControllerPropertyName
      ?let kind
      ?3*
    ]
    switch kind
    case property (2*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case toMany (typeName 3*) :
      selectionEntityName = typeName
    case toOne (3*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case arrayController (3*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case selectionController (*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    end
  #--- Find root entity observable properties
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !.new {!selectionEntityName !mSelectionControllerName.location}
      ?selectionObservablePropertyMap
      ?*
    ]
  else
    [inObservableProperties searchKey
      !mModelControllerName
      ?let kind
      ?3*
    ]
    switch kind
    case property (2*) :
      error mModelControllerName : "the model should be an array controller" : selectionEntityName
    case toMany (4*) :
      error mModelControllerName : "the model should be an array controller" : selectionEntityName
    case toOne (3*) :
      error mModelControllerName : "the model should be an array controller" : selectionEntityName
    case arrayController (typeName 2*) :
      selectionEntityName = typeName
    case selectionController (*) :
      error mModelControllerName : "the model should be an array controller" : selectionEntityName
    end
  #--- Find root entity observable properties
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !.new {!selectionEntityName !mSelectionControllerName.location}
      ?selectionObservablePropertyMap
      ?*
    ]
  end
#--- Generation
  ioSelectionControllerForGeneration +=
    !inOwnerName
    !mSelectionControllerName.string
    !mModelControllerName.string
    !mModelControllerPropertyName.string
    !selectionEntityName
    !selectionObservablePropertyMap
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION TYPES                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @selectionControllerForGeneration {
  @string mOwnerName # document name, prefs name, ...
  @string mSelectionControllerName
  @string mBoundControllerName
  @string mBoundControllerPropertyName
  @string mSelectionTypeName
  @observablePropertyMap mSelectionObservablePropertyMap
}

#·······················································································································

private filewrapper selectionControllerGenerationTemplate in "../generation-templates/controllers" {
}{
}{
  template selectionControllerImplementation "selection-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string SELECTION_CONTROLLER_NAME
    ?@string ELEMENT_TYPE_NAME
    ?@observablePropertyMap SELECTION_OBSERVABLE_PROPERTY_MAP
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   GENERATION                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc generateSelectionControllers
  ?let @selectionControllerForGeneration inArrayControllerListForGeneration
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  for () in inArrayControllerListForGeneration do
    let s = [filewrapper selectionControllerGenerationTemplate.selectionControllerImplementation
      !mOwnerName
      !mSelectionControllerName
      !mSelectionTypeName
      !mSelectionObservablePropertyMap
    ]
    let fileName = "selectionController-" + mOwnerName + "-" +  mSelectionControllerName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
