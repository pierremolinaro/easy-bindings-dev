#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

class @selectionControllerDeclaration : @abstractSecondaryProperty {
  @lstring mSelectionControllerName
  @lstring mModelControllerName
  @lstring mModelControllerPropertyName
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <controller_declaration> ?!@secondaryPropertyList ioSecondaryPropertyList {
    $selectionController$
    $identifier$ ?let @lstring selectionControllerName
    $:$
    @lstring modelControllerName
    select
      $self$
      modelControllerName = ["self" here]
    or
      $identifier$ ?modelControllerName
    end
    $.$
    $identifier$ ?let @lstring propertyName
    $;$
    ioSecondaryPropertyList += !@selectionControllerDeclaration.new {
      !selectionControllerName
      !modelControllerName
      !propertyName
    }
  }

  #·····················································································································

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

override method @selectionControllerDeclaration typeInventory
  ?!@unifiedTypeMap unused ioUnifiedTypeMap
{
}

#·······················································································································

override method @selectionControllerDeclaration tryToSolveSecondaryProperty
  ?!@semanticContext unused ioSemanticContext
  ?let @observablePropertyMap unused inRootObservableProperties
  ?!@observablePropertyMap ioObservableProperties
  ?!@secondaryPropertyList ioUnsolvedProperties
{
  if mModelControllerName.string == "self" then
    [ioObservableProperties searchKey
      !mModelControllerPropertyName
      ?let type
      ?let unused kind
      ?let multiplicity
      ?3*
    ]
  #--- Check type
    @string entityName
    switch type
    case stringType, boolType, dateType, doubleType, integerType, fontType, colorType :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case enumType (* *) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case classType (*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case transientPropertyExternType (*) :
      error mModelControllerPropertyName : "the model should be a collection of entities" : entityName
    case entityType (kEntityName *) :
      entityName = kEntityName
      if multiplicity != .collection then
        error mModelControllerPropertyName : "the model should be a collection of entities"
      end
    end
  #--- Store selection controller
    [!?ioObservableProperties insertKey
      !mSelectionControllerName
      !type
      !.selectionController
      !.collection
      !"" # No inverse relationship
      !{} # No action
      !false
    ]
  elsif [ioObservableProperties hasKey !mModelControllerName.string] then
  #--- Get model controller
    [ioObservableProperties searchKey
      !mModelControllerName
      ?let type
      ?let unused kind
      ?let multiplicity
      ?3*
    ]
  #--- Check kind
#    if kind != .arrayController then
#      error mModelControllerName : "the model should be an array controller"
#    end
  #--- Check multiplicity
    if multiplicity != .collection then
      error mModelControllerName : "the model should be an array controller"
    end
  #--- Check type
    @string entityName
    switch type
    case stringType, boolType, dateType, doubleType, integerType, fontType, colorType :
      error mModelControllerName : "the array controller model should be an entity" : entityName
    case enumType (* *) :
      error mModelControllerName : "the array controller model should be an entity" : entityName
    case classType (*) :
      error mModelControllerName : "the array controller model should be an entity" : entityName
    case transientPropertyExternType (*) :
      error mModelControllerName : "the array controller model should be an entity" : entityName
    case entityType (kEntityName *) :
      entityName = kEntityName
    end
  #--- Store selection controller
    [!?ioObservableProperties insertKey
      !mSelectionControllerName
      !type
      !.selectionController
      !.single
      !"" # No inverse relationship
      !{} # No action
      !false
    ]  
  else
    ioUnsolvedProperties += !self
  end
}

#·······················································································································

override method @selectionControllerDeclaration secondaryPropertySemanticAnalysis
  ?let @string inOwnerName
  ?let @string unused inRootEntityName
  ?let @observablePropertyMap unused inRootObservableProperties
  ?let @semanticContext inSemanticContext
  ?let @observablePropertyMap inObservableProperties
  ?!@transientDefinitionListForGeneration unused ioTransientDefinitionListForGeneration
  ?!@arrayControllerForGeneration unused ioArrayControllerForGeneration
  ?!@selectionControllerForGeneration ioSelectionControllerForGeneration
  ?!@stringset unused ioNeededOutletClasses
{
#--- Get model controller
  @string selectionEntityName
  @observablePropertyMap selectionObservablePropertyMap
  if mModelControllerName.string == "self" then
    [inObservableProperties searchKey
      !mModelControllerPropertyName
      ?let type
      ?let unused kind
      ?let multiplicity
      ?3*
    ]
  #--- Check type
    switch type
    case stringType, boolType, dateType, doubleType, integerType, fontType, colorType :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case enumType (* *) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case classType (*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    case entityType (kEntityName *) :
      selectionEntityName = kEntityName
      if multiplicity != .collection then
        error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
      end
    case transientPropertyExternType (*) :
      error mModelControllerPropertyName : "the property should be a collection of entities" : selectionEntityName
    end
  #--- Find root entity observable properties
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !.new {!selectionEntityName !mSelectionControllerName.location}
      ?selectionObservablePropertyMap
    ]
  else
    [inObservableProperties searchKey
      !mModelControllerName
      ?let type
      ?5*
    ]
  #--- Check type
    switch type
    case stringType, boolType, dateType, doubleType, integerType, fontType, colorType :
      error mModelControllerName : "the array controller model should be an entity" : selectionEntityName
    case enumType (* *) :
      error mModelControllerName : "the array controller model should be an entity" : selectionEntityName
    case classType (*) :
      error mModelControllerName : "the array controller model should be an entity" : selectionEntityName
    case entityType (kEntityName *) :
      selectionEntityName = kEntityName
    case transientPropertyExternType (*) :
      error mModelControllerName : "the array controller model should be an entity" : selectionEntityName
    end
  #--- Find root entity observable properties
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !.new {!selectionEntityName !mSelectionControllerName.location}
      ?selectionObservablePropertyMap
    ]
  end
#--- Generation
  ioSelectionControllerForGeneration +=
    !inOwnerName
    !mSelectionControllerName.string
    !mModelControllerName.string
    !mModelControllerPropertyName.string
    !selectionEntityName
    !selectionObservablePropertyMap
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPES                                                                                                   *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @selectionControllerForGeneration {
  @string mOwnerName # document name, prefs name, ...
  @string mSelectionControllerName
  @string mBoundControllerName
  @string mBoundControllerPropertyName
  @string mSelectionTypeName
  @observablePropertyMap mSelectionObservablePropertyMap
}

#·······················································································································

private filewrapper selectionControllerGenerationTemplate in "../generation-templates/controllers" {
}{
}{
  template selectionControllerImplementation "selection-controller.swift.galgasTemplate"
    ?@string OWNER_NAME
    ?@string SELECTION_CONTROLLER_NAME
    ?@string ELEMENT_TYPE_NAME
    ?@observablePropertyMap SELECTION_OBSERVABLE_PROPERTY_MAP
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION                                                                                                         *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

proc generateSelectionControllers
  ?let @selectionControllerForGeneration inArrayControllerListForGeneration
  ?let @string inOutputDirectory
  ?!@stringset ioGeneratedFileSet
{
  for () in inArrayControllerListForGeneration do
    let s = [filewrapper selectionControllerGenerationTemplate.selectionControllerImplementation
      !mOwnerName
      !mSelectionControllerName
      !mSelectionTypeName
      !mSelectionObservablePropertyMap
    ]
    let fileName = "selectionController-" + mOwnerName + "-" +  mSelectionControllerName + ".swift"
    ioGeneratedFileSet += !fileName
    [@string generateFile
      !inOutputDirectory
      !fileName
      !s
    ]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
