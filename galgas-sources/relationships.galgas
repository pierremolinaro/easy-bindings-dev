#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   AST                                                                                                                *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @toOneRelationshipList {
  @lstring mDestinationEntityName
  @lstring mToOneRelationshipName
  @lstring mInverseRelationshipName
  @propertyMultiplicity mInverseRelationMultiplicity
  @bool mCascading
}

#·······················································································································

list @toManyRelationshipList {
  @lstring mDestinationEntityName
  @lstring mToManyRelationshipName
  @lstring mInverseRelationshipName
  @bool mCascading
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SYNTAX                                                                                                             *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

syntax extension easyBindings_syntax {

  #·····················································································································
  
  rule <toMany_relationship> ?!@toManyRelationshipList ioToManyRelationshipList ?!@stringset ioSignatureList {
    $toMany$
    @bool usedForSignature = false
    @bool cascading = false
    repeat
    while
      $@attribute$ ?let attributeName
      if attributeName.string == "signature" then
        if usedForSignature then
          error attributeName : "duplicated attribute" fixit { remove }
        end
        usedForSignature = true
      elsif attributeName.string == "cascading" then
        if cascading then
          error attributeName : "duplicated attribute" fixit { remove }
        end
        cascading = true
      else
        error attributeName : "unknow attribute" fixit { replace "@signature" replace "@cascading" }
      end
    end
    $Identifier$ ?let destinationEntityName
    $identifier$ ?let relationshipName
    if usedForSignature then
      ioSignatureList += !relationshipName.string
    end
    @lstring inserseRelationshipName
    select
      $inverse$
      $toOne$
      $identifier$ ?inserseRelationshipName
    or
      inserseRelationshipName = .new {!"" !relationshipName.location}
    end
    $;$
    ioToManyRelationshipList +=
      !destinationEntityName
      !relationshipName
      !inserseRelationshipName
      !cascading
  }
  
  #·····················································································································

   rule <toOne_relationship> ?!@toOneRelationshipList ioToOneRelationshipList {
    $toOne$
    @bool cascading = false
    repeat
    while
      $@attribute$ ?let attributeName
      if attributeName.string == "cascading" then
        if cascading then
          error attributeName : "duplicated attribute" fixit { remove }
        end
        cascading = true
      else
        error attributeName : "unknow attribute" fixit { replace "@cascading" } # replace "@signature" }
      end
    end
    $Identifier$ ?let @lstring destinationEntityName
    $identifier$ ?let @lstring relationshipName
    $inverse$
    @propertyMultiplicity inverseRelationMultiplicity
    select
      $toOne$
      inverseRelationMultiplicity = .single
    or
      $toMany$
      inverseRelationMultiplicity = .collection
    end
    $identifier$ ?let @lstring inverseRelationshipName
    $;$
    ioToOneRelationshipList +=
      !destinationEntityName
      !relationshipName
      !inverseRelationshipName
      !inverseRelationMultiplicity
      !cascading
  }
  
  #·····················································································································

}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   TYPE INVENTORY                                                                                                     *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @toOneRelationshipList typeInventory
  ?!@unifiedTypeMap ioUnifiedTypeMap
{
  for () in self do
    [@unifiedTypeMap-proxy makeProxy
      !?ioUnifiedTypeMap
      !mDestinationEntityName
      ?*
    ]
  end
}

#·······················································································································

method @toManyRelationshipList typeInventory
  ?!@unifiedTypeMap ioUnifiedTypeMap
{
  for () in self do
    [@unifiedTypeMap-proxy makeProxy
      !?ioUnifiedTypeMap
      !mDestinationEntityName
      ?*
    ]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   OBSERVABLE PROPERTY MAP                                                                                            *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @toOneRelationshipList buildObservablePropertyMap
  ?let @unifiedTypeMap inUnifiedTypeMap
  ?!@observablePropertyMap ioObservableProperties
{
  for () in self do
  #--- Property type
    [inUnifiedTypeMap searchKey !mDestinationEntityName ?let type ?let actionMap]
  #--- Check type
    switch type
    case boolType, integerType, stringType, dateType, doubleType, fontType :
      error mDestinationEntityName: "a simple type cannot be used as entitytype"
    case enumType (* *) :
      error mDestinationEntityName: "an enumeration type cannot be used as entitytype"
    case classType (*) :
      error mDestinationEntityName: "a class type cannot be used as entitytype"
    case propertyClassType (**) :
      error mDestinationEntityName: "a property class type cannot be used as entitytype"
    case transientPropertyExternType (*) :
      error mInverseRelationshipName: "the opposite type cannot be used as entity type"
    case entityType (*) : # Ok
    end
  #--- Enter in property map
    [!?ioObservableProperties insertKey
      !mToOneRelationshipName
      !type
      !.stored
      !.single
      !mInverseRelationshipName.string
      !actionMap
      !mCascading
    ]
  end
}

#·······················································································································

method @toManyRelationshipList buildObservablePropertyMap
  ?let @unifiedTypeMap inUnifiedTypeMap
  ?!@observablePropertyMap ioObservableProperties
{
  for () in self do
  #--- Property type
    [inUnifiedTypeMap searchKey !mDestinationEntityName ?let type ?let actionMap]
  #--- Check type
    switch type
    case boolType, integerType, stringType, dateType, doubleType, fontType :
      error mDestinationEntityName: "a simple type cannot be used as entitytype"
    case enumType (* *) :
      error mDestinationEntityName: "an enumeration type cannot be used as entitytype"
    case classType (*) :
      error mDestinationEntityName: "a class type cannot be used as entitytype"
    case propertyClassType (**) :
      error mDestinationEntityName: "a property class type cannot be used as entitytype"
    case transientPropertyExternType (*) :
      error mInverseRelationshipName: "the opposite type cannot be used as entity type"
    case entityType (*) : # Ok
    end
  #--- Enter in property map
    [!?ioObservableProperties insertKey
      !mToManyRelationshipName
      !type
      !.stored
      !.collection
      !mInverseRelationshipName.string
      !actionMap
      !mCascading
    ]
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   SEMANTIC ANALYSIS                                                                                                  *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

method @toOneRelationshipList semanticAnalysis
  ?let @semanticContext inSemanticContext
  !@toOneEntityRelationshipListForGeneration outToOneEntityRelationshipListForGeneration
{
  outToOneEntityRelationshipListForGeneration = {}
  for () in self do
    [inSemanticContext.mUnifiedTypeMap searchKey !mDestinationEntityName ?let type ?*]
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !mDestinationEntityName
      ?@observablePropertyMap destinationEntityObservablePropertyMap
    ]
    outToOneEntityRelationshipListForGeneration +=
      !mToOneRelationshipName.string
      !type
      !mInverseRelationshipName.string
      !mInverseRelationMultiplicity
      !destinationEntityObservablePropertyMap
      !mCascading
  #--- Check inverse relationship
    [destinationEntityObservablePropertyMap searchForInverseRelationship
       !mInverseRelationshipName
       ?let @typeKind oppositeType
       ?let @propertyKind oppositeKind
       ?let @propertyMultiplicity oppositeMultiplicity
       ?let @string oppositeOfOpposite
       ?2*
     ]
   #--- Check opposite type
     switch oppositeType
     case boolType, integerType, stringType, dateType, doubleType, fontType :
       error mInverseRelationshipName: "the opposite type cannot be used as entity type"
     case enumType (* *) :
       error mInverseRelationshipName: "the opposite type cannot be used as entity type"
     case classType (*) :
       error mInverseRelationshipName: "the opposite type cannot be used as entity type"
     case propertyClassType (**) :
       error mInverseRelationshipName: "the opposite type cannot be used as entity type"
     case transientPropertyExternType (*) :
       error mInverseRelationshipName: "the opposite type cannot be used as entity type"
     case entityType (*) : # Ok
     end
     if oppositeKind == .transient then
       error mInverseRelationshipName : "this relationship is transient, should be stored"
     end
     if mInverseRelationMultiplicity != oppositeMultiplicity then
       error mInverseRelationshipName : "inverse relationship multiplicity error (toMany <-> toOne)"
     end
     if oppositeOfOpposite != mToOneRelationshipName.string then
       error mInverseRelationshipName : "inverse relationship does not name '"
         + mToOneRelationshipName + "' as opposite"
     end
  end
}

#·······················································································································

method @toManyRelationshipList semanticAnalysis
  ?let @semanticContext inSemanticContext
  !@toManyEntityRelationshipListForGeneration outToManyEntityRelationshipListForGeneration
{
  outToManyEntityRelationshipListForGeneration = {}
  for () in self do
    [inSemanticContext.mUnifiedTypeMap searchKey !mDestinationEntityName ?let type ?*]
    [inSemanticContext.mEntityObservablePropertyMap searchKey
      !mDestinationEntityName
      ?@observablePropertyMap destinationEntityObservablePropertyMap
    ]
    outToManyEntityRelationshipListForGeneration +=
      !mToManyRelationshipName.string
      !type
      !mInverseRelationshipName.string
      !destinationEntityObservablePropertyMap
      !mCascading
  #--- Check inverse relationship
    if mInverseRelationshipName.string != "" then
      [destinationEntityObservablePropertyMap searchForInverseRelationship
         !mInverseRelationshipName
         ?let @typeKind oppositeType
         ?let @propertyKind oppositeKind
         ?let @propertyMultiplicity oppositeMultiplicity
         ?let @string oppositeOfOpposite
         ?2*
       ]
       switch oppositeType
       case boolType, integerType, stringType, dateType, doubleType, fontType :
         error mInverseRelationshipName: "the opposite type cannot be used as entity type"
       case enumType (* *) :
         error mInverseRelationshipName: "the opposite type cannot be used as entity type"
       case classType (*) :
         error mInverseRelationshipName: "the opposite type cannot be used as entity type"
       case propertyClassType (**) :
         error mInverseRelationshipName: "the opposite type cannot be used as entity type"
       case transientPropertyExternType (*) :
         error mInverseRelationshipName: "the opposite type cannot be used as entity type"
       case entityType (*) : # Ok
       end
       if oppositeKind == .transient then
         error mInverseRelationshipName : "this relationship is transient, should be stored"
       end
       if oppositeMultiplicity != .single then
         error mInverseRelationshipName : "the inverse relationship should be toOne"
       end
       if oppositeOfOpposite != mToManyRelationshipName.string then
         error mInverseRelationshipName : "inverse relationship does not name '"
           + mToManyRelationshipName + "' as opposite"
       end
     end
  end
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
#   GENERATION TYPES                                                                                                   *
#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*

list @toOneEntityRelationshipListForGeneration {
  @string mToOneRelationshipName
  @typeKind mRelationshipType
  @string mOppositeRelationshipName
  @propertyMultiplicity mInverseRelationMultiplicity
  @observablePropertyMap mDestinationEntityObservablePropertyMap
  @bool mCascading
}

#·······················································································································

list @toManyEntityRelationshipListForGeneration {
  @string mToManyRelationshipName
  @typeKind mRelationshipType
  @string mOppositeRelationshipName
  @observablePropertyMap mDestinationEntityObservablePropertyMap
  @bool mCascading
}

#——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————*
